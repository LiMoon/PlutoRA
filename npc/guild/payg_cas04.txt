//===== rAthena Script =======================================
//= War of Emperium (Payon)
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= NPCs that relate to Payon Guild Castle 1
//===== Additional Comments: =================================
//= 1.0 First Version. No longer uses functions. [L0ne_W0lf]
//= 1.1 Corrected flag-in spawn point. [L0ne_W0lf]
//= 1.2 Removed dialog from interior flags. [L0ne_W0lf]
//=     Fixed position of Guild dungeon switch.
//= 1.3 Partially rewrote Script. [Masao]
//============================================================

// Guild Flags (Outside Castle)
//============================================================
-	script	紅樓#1::OutsideFlagsPA4	722,{

	set .@GID, GetCastleData("payg_cas04",1);

	if (.@GID == 0) {
		mes "　　 [ 盧恩-米德加茲王國 公告令 ]";
		mes " ";
		mes "1. 根據這裡的，盧恩-米德加茲王國令";
		mes "　　把這裡認定為";
		mes "　　沒有正式所有權的秘密基地";
		mes " ";
		mes "2. 戰勝守在這裡，可怕的考驗後，";
		mes "　　給破壞華麗金屬的人，";
		mes " ";
		mes "國王會賜予經國王認證的，秘密基地所有權";
		close;
	}
	if (getcharid(2) == .@GID) {
		mes "[ 響亮的嗓音 ]";
		mes "勇敢的人們...";
		mes "要回到您的名譽之地嗎?";
		next;
		if (select("回到秘密基地.:放棄") == 1) {
			close2;
			if (getcharid(2) == GetCastleData("payg_cas04",1)) {
					warp "payg_cas04",40,235;
				end;
			}
			end;
		}
		close;
	}
	mes "[ 盧恩-米德加茲王國 公告令 ]";
	mes " ";
	mes "1. 根據這裡的，盧恩-米德加茲王國令";
	mes "　　這裡被認定為 ^ff0000" + GetGuildName(.@GID) + "^000000工會的";
	mes "　　私有物";
	mes " ";
	mes "2. ^ff0000"+ GetGuildName(.@GID) + "^000000 工會的主人是";
	mes "　　^ff0000" + GetGuildMaster(.@GID) + "^000000 ， 無論是誰";
	mes "　　對這件事情有意見者";
	mes " ";
	mes "請你舉起您的劍，提高自己的名譽吧";
	close;

OnRecvCastlePy04:
	FlagEmblem GetCastleData("payg_cas04",1);
	end;
}

pay_gld,137,160,0	duplicate(OutsideFlagsPA4)	紅樓#1-2	722
pay_gld,143,160,0	duplicate(OutsideFlagsPA4)	紅樓#2	722
pay_gld,133,151,2	duplicate(OutsideFlagsPA4)	紅樓#3	722
pay_gld,153,166,1	duplicate(OutsideFlagsPA4)	紅樓#4	722

// Guild Flags (Inside Castle)
//============================================================
-	script	紅樓#5::InsideFlagsPA4	722,{
	end;

OnRecvCastlePy04:
	FlagEmblem GetCastleData("payg_cas04",1);
	end;
}

payg_cas04,255,259,0	duplicate(InsideFlagsPA4)	紅樓#5-2	722
payg_cas04,248,259,0	duplicate(InsideFlagsPA4)	紅樓#6	722
payg_cas04,248,168,6	duplicate(InsideFlagsPA4)	紅樓#7	722
payg_cas04,248,160,6	duplicate(InsideFlagsPA4)	紅樓#8	722
payg_cas04,232,181,4	duplicate(InsideFlagsPA4)	紅樓#9	722
payg_cas04,239,181,4	duplicate(InsideFlagsPA4)	紅樓#10	722

// Guild Flag (Town)
//============================================================
payon,118,322,4	duplicate(InsideFlagsPA4)	紅樓#11	722
payon,166,165,3	duplicate(InsideFlagsPA4)	紅樓#12	722

// Kafra Employee
//============================================================
payg_cas04,40,235,1	duplicate(guildkafra)	卡普拉 職員#payg_cas04	117

// Steward
//============================================================
payg_cas04,38,284,3	duplicate(cm)	第潔依#payg_cas04	55

// Treasure Room Exit
//============================================================
payg_cas04,151,47,0	script	#lever_PA04	111,{

	mes " ";
	mes "有個某種小的裝置 ，請問你要操作嗎?";
	next;
	if(select("操作:結束") == 1) {
		close2;
		warp "payg_cas04",38,285;
		end;
	}
	close;
}

// Guild Dungeon Switch
//============================================================
payg_cas04,52,48,0	duplicate(gdlever)	#payg_cas04	111

// Guild Treasure Spawns
//============================================================
payg_cas04,147,48,0	duplicate(Gld_Trea_Spawn)	Treasure#payg_cas04	-1

// AGIT Manager
//============================================================
payg_cas04,271,29,0	duplicate(Gld_Agit_Manager)	Agit#payg_cas04	-1

// Guardians
//============================================================
payg_cas04,38,284,3	duplicate(Gld_Guard_Template)	Guardian#payg_cas04	-1
