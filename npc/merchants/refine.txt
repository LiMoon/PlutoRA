//===== rAthena Script ======================================= 
//= Refining NPCs
//===== By: ==================================================
//= Syrus22 (1.1) dafide18 (1.4) Skotlex (1.5)
//===== Current Version: =====================================
//= 3.4
//===== Compatible With: =====================================
//= Any Athena Version
//===== Description: =========================================
//= Refining NPCs and Metal Salesmen.
//===== Additional Comments: =================================
//= 3.0 Updated several NPC names and locations. [Xantara]
//=     Added WoE map Refiners.
//= 3.1 Added the new refinement & Ore creation NPC's for +11 and above Refinement. [Masao]
//= 3.2 Moved some scripts to Renewal file, other minor changes. [Euphy]
//= 3.2a Added 'disable_items' command. [Euphy]
//= 3.3 Some official script updates. [Euphy]
//= 3.4 Added VIP features. [Euphy]
//============================================================
//= 中文化維護：笨冰
//============================================================


//===================================
// 克里斯多福，鍛治相關物品販賣
//===================================
geffen_in,110,172,0	script	克里斯多福#1	63,{
	mes "[克里斯多福]";
	mes "歡迎您來到克里斯多福鐵匠鋪。";
	mes "這裡販賣所有關於鍛治用的物品。";
	mes "您需要什麼呢?";
	next;
	switch(select("我想要買鐵砧。:我想要買鍛治的物品。:我想要買金屬。:我想要製作原石。:取消。")) {
	case 1:
		mes "[克里斯多福]";
		mes "用來製作武器的鐵砧當然是越堅硬的金屬越好。";
		mes "不過您應該也知道，愈堅硬的金屬價格上就愈貴。";
		mes "您可以依造自己的經濟狀況選擇合適的。";
		next;
		switch(select("鐵砧 - 30,000金幣。:神之金屬鐵砧 - 120,000金幣。:黃金鐵砧 - 300,000金幣:更堅硬的鐵砧。:結束交易。")) {
		case 1:
			if (Zeny < 30000) {
				mes "[克里斯多福]";
				mes "您所擁有的金錢不足喔！總不能要我賠錢賣給您吧！";
				close;
			}
			getitem 986,1; // Anvil
			set Zeny,Zeny-30000;
			mes "[克里斯多福]";
			mes "這是相當物美價廉的東西。";
			mes "總之，非常感謝您的購買。如果有任何需要的話，歡迎您再度光臨指教。";
			close;
		case 2:
			if (Zeny < 120000) {
				mes "[克里斯多福]";
				mes "您所擁有的金錢不足喔！總不能要我賠錢賣給您吧！";
				close;
			}
			getitem 987,1; // Oridecon_Anvil
			set Zeny,Zeny-120000;
			mes "[克里斯多福]";
			mes "您的眼光不錯嘛！身為鐵匠所應該擁有的鐵砧最起碼也要有這個程度。";
			mes "總之，非常感謝您的購買。如果有任何需要的話，歡迎您再度光臨指教。";
			close;
		case 3:
			if (Zeny < 300000) {
				mes "[克里斯多福]";
				mes "您所擁有的金錢不足喔！總不能要我賠錢賣給您吧！";
				close;
			}
			getitem 988,1; // Golden_Anvil
			set Zeny,Zeny-300000;
			mes "[克里斯多福]";
			mes "您所選擇的這個是鐵砧用最堅硬最美麗的金屬所製成的!";
			mes "只要有了這個，相信您依定可以成為一名偉大的鐵匠的。";
			mes "總之，非常感謝您的購買。如果有任何需要的話，歡迎您再度光臨指教。";
			close;
		case 4:
			mes "[克里斯多福]";
			mes "我們店鋪裡沒有販賣比黃金鐵砧更堅硬的鐵砧。";
			mes "不知道傳說中'凌格'所製作的鐵砧是不是已經流傳於世...用這種程度堅硬的金屬做成的鐵砧不只我店鋪裡沒有，其它店鋪也都不會有。";
			close;
		case 5:
			mes "[克里斯多福]";
			mes "如果有任何需要的話，歡迎您再度光臨指教。請慢走。";
			close;
		}
	case 2:
		mes "[克里斯多福]";
		mes "偉大的鐵匠當然得要用偉大的道具。";
		mes "如果在克里斯多福鐵匠鋪購買道具的話，";
		mes "您一定可以成為一名偉大的鐵匠的。";
		mes "選看看那一種道具最何您的心意吧！";
		next;
		switch(select("攜帶用熔礦爐 - 150金幣。:鋼鐵錘子 - 1,000金幣。:黃金錘子 - 3,000金幣。:神之金屬錘子 - 5,000金幣。:結束交易。")) {
		case 1:
			mes "[克里斯多福]";
			mes "這是製作金屬的必需品!!";
			next;
			mes "[克里斯多福]";
			mes "好的，請問您要購買多少數量?";
			mes "欲結束交易的話請按'0'。";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[克里斯多福]";
					mes "交易已經結束。請慢走。";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[克里斯多福]";
					mes "請將購買數量減到500個以下。";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 150;
			if (Zeny < .@sell) {
				mes "[克里斯多福]";
				mes "您所擁有的金錢不足喔！總不能要我賠錢賣給您吧！";
				close;
			}
			if (checkweight(612,.@input) == 0) {
				mes "[克里斯多福]";
				mes "在拿取東西之前請先整理一下您的物品。";
				close;
			}
			getitem 612,.@input;
			set Zeny,Zeny-.@sell;
			mes "[克里斯多福]";
			mes "非常感謝您的購買。如果有任何需要的話，歡迎您再度光臨指教。";
			close;
		case 2:
			mes "[克里斯多福]";
			mes "好的，請問您要購買多少數量?";
			mes "欲結束交易的話請按'0'。";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[克里斯多福]";
					mes "交易已經結束。請慢走。";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[克里斯多福]";
					mes "請將購買數量減到500個以下。";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 1000;
			if (Zeny < .@sell) {
				mes "[克里斯多福]";
				mes "您所擁有的金錢不足喔！總不能要我賠錢賣給您吧！";
				close;
			}
			if (checkweight(613,.@input) == 0) {
				mes "[克里斯多福]";
				mes "在拿取東西之前請先整理一下您的物品。";
				close;
			}
			getitem 613,.@input;
			set Zeny,Zeny-.@sell;
			mes "[克里斯多福]";
			mes "非常感謝您的購買。如果有任何需要的話，歡迎您再度光臨指教。";
			close;
		case 3:
			mes "[克里斯多福]";
			mes "好的，請問您要購買多少數量?";
			mes "欲結束交易的話請按'0'。";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[克里斯多福]";
					mes "交易已經結束。請慢走。";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[克里斯多福]";
					mes "請將購買數量減到500個以下。";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 3000;
			if (Zeny < .@sell) {
				mes "[克里斯多福]";
				mes "您所擁有的金錢不足喔！總不能要我賠錢賣給您吧！";
				close;
			}
			if (checkweight(614,.@input) == 0) {
				mes "[克里斯多福]";
				mes "在拿取東西之前請先整理一下您的物品。";
				close;
			}
			getitem 614,.@input;
			set Zeny,Zeny-.@sell;
			mes "[克里斯多福]";
			mes "非常感謝您的購買。如果有任何需要的話，歡迎您再度光臨指教。";
			close;
		case 4:
			mes "[克里斯多福]";
			mes "好的，請問您要購買多少數量?";
			mes "欲結束交易的話請按'0'。";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[克里斯多福]";
					mes "交易已經結束。請慢走。";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[克里斯多福]";
					mes "請將購買數量減到500個以下。";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 5000;
			if (Zeny < .@sell) {
				mes "[克里斯多福]";
				mes "您所擁有的金錢不足喔！總不能要我賠錢賣給您吧！";
				close;
			}
			if (checkweight(615,.@input) == 0) {
				mes "[克里斯多福]";
				mes "在拿取東西之前請先整理一下您的物品。";
				close;
			}
			getitem 615,.@input;
			set Zeny,Zeny-.@sell;
			mes "[克里斯多福]";
			mes "非常感謝您的購買。如果有任何需要的話，歡迎您再度光臨指教。";
			close;
		case 5:
			mes "[克里斯多福]";
			mes "如果有任何需要的話，歡迎您再度光臨指教。請慢走。";
			close;
		}
	case 3:
		mes "[克里斯多福]";
		mes "原來您是所選用的都是高檔貨啊！";
		mes "那麼，您想要購買哪種金屬呢?";
		next;
		switch(select("強化武器用金屬-強化級數一 - 200金幣。:強化武器用金屬-強化級數二 - 1,000金幣。:終止交易。")) {
		case 1:
			mes "[克里斯多福]";
			mes "好的，請問您要購買多少數量?";
			mes "欲結束交易的話請按'0'。";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[克里斯多福]";
					mes "交易已經結束。請慢走。";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[克里斯多福]";
					mes "請將購買數量減到500個以下。";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 200;
			if (Zeny < .@sell) {
				mes "[克里斯多福]";
				mes "您所擁有的金錢不足喔！總不能要我賠錢賣給您吧...本店可是小本經營的耶...";
				close;
			}
			if (checkweight(1010,.@input) == 0) {
				mes "[克里斯多福]";
				mes "在拿取東西之前請先整理一下您的物品。";
				close;
			}
			getitem 1010,.@input; // Phracon
			set Zeny,Zeny-.@sell;
			mes "[克里斯多福]";
			mes "非常感謝您的購買。如果有任何需要的話，歡迎您再度光臨指教。";
			close;
		case 2:
			mes "[克里斯多福]";
			mes "好的，請問您要購買多少數量?";
			mes "欲結束交易的話請按'0'。";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[克里斯多福]";
					mes "交易已經結束。請慢走。";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[克里斯多福]";
					mes "請將購買數量減到500個以下。";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 1000;
			if (Zeny < .@sell) {
				mes "[克里斯多福]";
				mes "您所擁有的金錢不足喔！總不能要我賠錢賣給您吧...本店可是小本經營的耶...";
				close;
			}
			if (checkweight(1011,.@input) == 0) {
				mes "[克里斯多福]";
				mes "在拿取東西之前請先整理一下您的物品。";
				close;
			}
			getitem 1011,.@input; // Emveretarcon
			set Zeny,Zeny-.@sell;
			mes "[克里斯多福]";
			mes "非常感謝您的購買。如果有任何需要的話，歡迎您再度光臨指教。";
			close;
		case 3:
			mes "[克里斯多福]";
			mes "如果有任何需要的話，歡迎您再度光臨指教。請慢走。";
			close;
		}
	case 4:
		mes "[克里斯多福]";
		mes "可以用來提煉的金屬有神之金屬與鋁兩種。";
		mes "只要各拿五個原石來提煉，就可以提煉出純度相當高的金屬。";
		mes "您想要製作哪種金屬呢?";
		next;
		switch(select("我想要製作神之金屬。:我想要製作鋁。:終止交易。")) {
		case 1:
			if (countitem(756) < 5) {
				mes "[克里斯多福]";
				mes "要是沒有五個神之金屬原石的話，我是沒有辦法製作出神之金屬來的。";
				close;
			}
			else {
				delitem 756,5;  //Oridecon_Stone
				getitem 984,1; // Oridecon
				mes "[克里斯多福]";
				mes "拿去吧！這是約定好要給您的神之金屬。";
				mes "如果有任何需要的話，歡迎您再度光臨指教。";
				close;
			}
		case 2:
			if (countitem(757) < 5) {
				mes "[克里斯多福]";
				mes "要是沒有五個鋁原石的話，我是沒有辦法製作出鋁來的。";
				close;
			}
			else {
				delitem 757,5;  //Elunium_Stone
				getitem 985,1; // Elunium
				mes "[克里斯多福]";
				mes "拿去吧！這是約定好要給您的鋁。";
				mes "如果有任何需要的話，歡迎您再度光臨指教。";
				close;
			}
		case 3:
			mes "[克里斯多福]";
			mes "非常感謝您的購買。如果有任何需要的話，歡迎您再度光臨指教。";
			close;
		}
	case 5:
		mes "[克里斯多福]";
		mes "如果有任何需要的話，歡迎您再度光臨指教。請慢走。";
		close;
	}
}

// Paul Spanner: Einbroch Blacksmith Supplier
//============================================================
ein_in01,38,29,0	script	福 斯培諾	63,{
	if (checkweight(1201,1) == 0) {
		mes "- 稍等下!!                 -";
		mes "- 當前你攜帶了太多東西了.  -";
		mes "- 先嘗試減輕你的重量吧.    -";
		close;
	}
	mes "[福 斯培諾]";
	mes "歡迎光臨鐵匠朋友，";
	mes "這裡販售與精鍊有關的所有商品";
	mes "你告訴我需要什麼東西呢";
	next;
	switch(select("購買鐵砧:購買精鍊物品:購買金屬:提煉原石:取消")) {
	case 1:
		mes "[福 斯培諾]";
		mes "鐵砧是精鍊之基本物品啊，";
		mes "由於是未來要跟你朝夕相處的傢伙，";
		mes "建議你最好選個優質的傢伙如何呢?";
		next;
		switch(select("鐵砧 - 30,000z:神之金屬鐵砧 - 120,000z:黃金鐵砧 - 300,000z:堅硬鐵砧:結束交易")) {
		case 1:
			if (zeny < 30000) {
				mes "[福 斯培諾]";
				mes "你的口袋那麼小";
				mes "甭說是短劍我看只能做個玩具木刀吧?";
				close;
			}
			getitem 986,1; //Anvil
			set zeny,zeny-30000;
			mes "[福 斯培諾]";
			mes "它是個物美價廉性能不錯的傢伙啊，";
			mes "總之謝謝你的選購，若還有需要的東西時說一聲就好了";
			close;
		case 2:
			if (zeny < 120000) {
				mes "[福 斯培諾]";
				mes "你的口袋那麼小";
				mes "甭說是短劍我看只能做個玩具木刀吧?";
				close;
			}
			getitem 987,1; //Oridecon_Anvil
			set zeny,zeny-120000;
			mes "[福 斯培諾]";
			mes "果然有眼光，身為鐵匠至少要有這種程度的鐵砧才對啊，";
			mes "總之謝謝你的選購，若還有需要的東西時說一聲就好了";
			close;
		case 3:
			if (zeny < 300000) {
				mes "[福 斯培諾]";
				mes "你的口袋那麼小";
				mes "甭說是短劍我看只能做個玩具木刀吧?";
				close;
			}
			getitem 988,1; //Golden_Anvil
			set zeny,zeny-300000;
			mes "[福 斯培諾]";
			mes "你選購了黃金鐵砧，我就知道你具備了優秀的鐵匠情操啊!";
			mes "希望你帶著這最佳的鐵砧，在未來製作出最優秀的武器吧";
			close;
		case 4:
			mes "[福 斯培諾]";
			mes "在我們店裡沒有販賣比黃金鐵砧更堅硬的鐵砧，";
			mes "或許是由傳說中的鐵砧製作人'凌格'所做的就可能不一樣了...";
			mes "比這個更堅硬的金屬物品，不僅是我們店鋪，任何地方也找不到的";
			close;
		case 5:
			mes "[福 斯培諾]";
			mes "若還有需要的東西時說一聲就好了，再見";
			close;
		}
	case 2:
		mes "[福 斯培諾]";
		mes "把礦物精鍊或著製作武器時會需要很多材料的..";
		mes "對你來說所需要的物品這裡都有，";
		mes "請你選擇你想要的傢伙吧";
		next;
		switch(select("攜帶用熔礦爐- 150z.:鋼鐵錘子- 1,000z.:黃金錘子- 3,000z.:神之金屬錘子- 5,000z.:結束交易")) {
		case 1:
			set .@item,612;
			set .@item_cost,150;
			set .@item_weight,200;
			mes "[福 斯培諾]";
			mes "要做金屬提煉時這傢伙是必要的!!";
			next;
			break;
		case 2:
			set .@item,613;
			set .@item_cost,1000;
			set .@item_weight,200;
			break;
		case 3:
			set .@item,614;
			set .@item_cost,3000;
			set .@item_weight,300;
			break;
		case 4:
			set .@item,615;
			set .@item_cost,5000;
			set .@item_weight,400;
			break;
		case 5:
			mes "[福 斯培諾]";
			mes "若還有需要的東西時說一聲就好了，再見";
			close;
		}
		mes "[福 斯培諾]";
		mes "那麼，你要購買多少個呢?";
		mes "想要停止交易的話 請輸入 '0'";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[福 斯培諾]";
				mes "交易停止了，再見";
				close;
			}
			else if ((.@input < 0) || (.@input > 500)) {
				mes "[福 斯培諾]";
				mes "請輸入500 個以下.";
				next;
			}
			else {
				break;
			}
		}
		set .@sell,.@input * .@item_cost;
		if (zeny < .@sell) {
			mes "[福 斯培諾]";
			mes "以你目前所擁有的錢...";
			mes "是不行的，我也不能賠本做生意啊";
			close;
		}
		if (checkweight(.@item,.@input) == 0) {
			mes "[福 斯培諾]";
			mes "你...";
			mes "臉色好難看啊，在接受物品之前請你先整理你的背包吧.";
			close;
		}
		set zeny,zeny-.@sell;
		getitem .@item,.@input;
		mes "[福 斯培諾]";
		mes "總之謝謝你的選購，若還有需要的東西時說一聲就好了";
		close;
	case 3:
		mes "[福 斯培諾]";
		mes "你應該全部選的都是純度較高的傢伙吧，";
		mes "來來，你要購買哪一種金屬呢?";
		next;
		switch(select("強化武器金屬-級數一- 200z:強化武器金屬-級數二- 1,000z:結束交易")) {
		case 1:
			set .@item,1010;
			set .@item_price,200;
			break;
		case 2:
			set .@item,1011;
			set .@item_price,1000;
			break;		
		case 3:
			mes "[福 斯培諾]";
			mes "若還有需要的東西時說一聲就好了，再見";
			close;
		}
		mes "[福 斯培諾]";
		mes "那麼，你要購買幾個呢?";
		mes "想要停止交易的話 請輸入 '0'";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[福 斯培諾]";
				mes "交易停止了，再見";
				close;
			}
			else if ((.@input < 0) || (.@input > 500)) {
				mes "[福 斯培諾]";
				mes "請輸入500 個以下";
				next;
			}
			else {
				break;
			}
		}
		set .@sell,.@input * .@item_price;
		if (zeny < .@sell) {
			mes "[福 斯培諾]";
			mes "你好像錢不夠啊，就買了這一點點東西不能製作出任何武器啊 請你銘記這一點";
			close;
		}
		if (checkweight(.@item,.@input) == 0) {
			mes "[福 斯培諾]";
			mes "你...";
			mes "臉色好難看啊，在接受物品之前請你先整理你的背包吧.";
			close;
		}
		getitem .@item,.@input;
		set zeny,zeny-.@sell;
		mes "[福 斯培諾]";
		mes "總之謝謝你的選購，若還有需要的東西時說一聲就好了";
		close;
	case 4:
		mes "[福 斯培諾]";
		mes "可提煉的金屬是只有神之金屬和鋁啊，";
		mes "各自拿5個原石來提煉的話可製作出純度較高的金屬，";
		mes "來來.. 你要製作出什麼樣的金屬呢?";
		switch(select("我要製作神之金屬:我要製作鋁:結束交易")) {
		case 1:
			if (countitem(756) < 5) {
				mes "[福 斯培諾]";
				mes "我不是說過沒有神之金屬原石5個的話，不能製作出神之金屬啊";
				close;
			}
			else {
				delitem 756,5; //Oridecon_Stone
				getitem 984,1; //Oridecon
				mes "[福 斯培諾]";
				mes "來，約定好的神之金屬在這裡啊，歡迎你隨時隨地的再來";
				close;
			}
		case 2:
			if (countitem(757) < 5) {
				mes "[福 斯培諾]";
				mes "我不是說過沒有鋁原石5個的話，不能製作出鋁啊";
				close;
			}
			else {
				delitem 757,5; //Elunium_Stone
				getitem 985,1; //Elunium
				mes "[福 斯培諾]";
				mes "來，約定好的鋁在這裡啊，歡迎你隨時隨地的再來";
				close;
			}
		case 3:
			mes "[福 斯培諾]";
			mes "若還有需要的東西時說一聲就好了，再見";
			close;
		}
	case 5:
		mes "[福 斯培諾]";
		mes "若還有需要的東西時說一聲就好了，再見";
		close;
	}
}

// Weapon/Armor Refiners
//============================================================
prt_in,63,60,0	script	忽克連	85,{
	callfunc "refinemain","忽克連",0;
	end;
}
morocc_in,73,38,6	script	亞拉甘	99,{
	callfunc "refinemain","亞拉甘",0;
	end;
}
payon,144,173,5	script	安東尼奧	88,{
	callfunc "refinemain","安東尼奧",0;
	end;
}
alberta_in,28,58,0	script	弗雷德利	85,{
	callfunc "refinemain","弗雷德利 賀樂曼孫",0;
	end;
}
yuno_in01,171,21,4	script	迪斯特伏	88,{
	callfunc "refinemain","迪斯特伏",0;
	end;
}
ein_in01,24,87,5	script	曼塔斯曼	826,{
	callfunc "refinemain","曼塔斯曼",0;
	end;
}
lhz_in02,282,20,7	script	福樂	869,{
	callfunc "refinemain","福樂",0;
	end;
}

//============================================================
//= Main Refiner Function
//============================================================
//= To allow auto safe refining/multiple refining set the
//= second argument to '1' in the function call.
//= If you enable this function, be sure to edit the value of
//= .@safe to the max safe refine in refine_db.txt as well.
//============================================================
function	script	refinemain	{
	disable_items;
	set .@features,getarg(1);
	mes "[" + getarg(0) + "]";
	mes "我是專門鍛治武器與防具的鐵匠。";
	mes "我可以為你鍛治裝置項目中的物品。";
	mes "你想要鍛治裝置項目中的哪一項物品呢?";
	next;

	setarray .@position$[1], "頭上","身體","左手","右手","外袍","鞋子","左裝飾品","右裝飾品","頭中","頭下";
	set .@menu$,"";
	for(set .@i,1; .@i<=10; set .@i,.@i+1) {
		if(getequipisequiped(.@i)) {
			set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";
			set .@equipped,1;
		}
		set .@menu$, .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "[" + getarg(0) + "]";
		mes "你沒有穿";
		mes "任何我可以鍛造的裝備";
		close;
	}
	set .@part, select(.@menu$);

	//檢查物品可否精煉
	if(!getequipisequiped(.@part)) { //custom check
		mes "[" + getarg(0) + "]";
		mes "咦...";
		mes "你說的這個項目是無法鍛治的項目耶...";
		emotion e_an;
		close;
	}
	//Check if the item is refinable...
	if(!getequipisenableref(.@part)) {
		mes "[" + getarg(0) + "]";
		mes "咦...";
		mes "你說的這個項目是未鑑定的項目耶...";
		close;
	}
	//Check to see if the items is already +10
	if(getequiprefinerycnt(.@part) >= 10) {
		mes "[" + getarg(0) + "]";
		mes "我不無法精煉它了。";
		mes "這項物品已經精煉到最高等級了!";
		close;
	}
	set .@refineitemid, getequipid(.@part); // save id of the item
	set .@refinerycnt, getequiprefinerycnt(.@part); //save refinery count
	switch(getequipweaponlv(.@part)){
	case 0: 	//Refine Armor
		set .@price,2000;
		set .@material,985; //Elunium
		set .@safe,4;
		break;
	case 1: 	//Refine Level 1 Weapon
		set .@price,50;
		set .@material,1010; //Phracon
		set .@safe,7;
		break;
	case 2: 	//Refine Level 2 Weapon
		set .@price,200;
		set .@material,1011; //Emveretarcon
		set .@safe,6;
		break;
	case 3: 	//Refine Level 3 Weapon
		set .@price,5000;
		set .@material,984; //Oridecon
		set .@safe,5;
		break;
	case 4: 	//Refine Level 4 Weapon
		set .@price,20000;
		set .@material,984; //Oridecon
		set .@safe,4;
		break;
	case 5: 	//Refine other stuff?
		set .@price,2000;
		set .@material,985; //Elunium
		set .@safe,4;
		break;
	}

	// If the VIP system is enabled, the prices for non-VIP players are considerably higher.
	if (getserverdef(VAR_VIP_SCRIPT) && !vip_status(1)) {
		switch(getequipweaponlv(.@part)) {
			case 0: set .@price, .@price * 10; break;
			case 1: set .@price, .@price * 40; break;
			case 2: set .@price, .@price * 50; break;
			case 3: set .@price, .@price * 2; break;
			case 4: set .@price, .@price * 2; break;
			case 5: set .@price, .@price * 10; break;
		}
	}

	if(.@features != 1) {
		mes "[" + getarg(0) + "]";
		mes "我需要下列這些東西來精煉";
		mes "一個 ^003366"+getitemname(.@material)+"^000000 和";
		mes "一次手續費 " + .@price + " Zeny。";
		mes "你要繼續嗎?";
		next;
		if(select("是:否") == 2){
			mes "[" + getarg(0) + "]";
			mes "是的...";
			mes "這裡不需要趕時間。";
			mes "慢慢地考慮一下吧!";
			close;
		}
		if(getequippercentrefinery(.@part) < 100) {
			mes "[" + getarg(0) + "]";
			mes "嗚哇!";
			mes "如果我再繼續鍛治的話, 可能會";
			switch(.@material) {
			case 985:
				mes "造成裝備毀滅! 這意味著 ^FF0000這個裝備^000000, 和附加在裝備上的 ^FF0000卡片^000000 或特殊屬性, ^FF0000將會消失^000000。";
				break;
			default:
				mes "造成武器毀滅, 而你將會 ^FF0000失去這把武器^000000, ^FF0000和在武器中的卡片^000000,";
				mes "或特殊屬性。";
				break;
			}
			next;
			mes "["+getarg(0)+"]";
			mes "我再提醒你最後一次。";
			mes "一件武器如果毀滅了,";	
			mes "是沒有辦法讓他還原的。";
			mes "你真的有機會";
			mes "永遠地 ^FF0000失去這武器^000000。";
			mes "你仍要鍛造嗎?";
			next;
			if(select("是:否") == 2){
				mes "[" + getarg(0) + "]";
				mes "我完全同意...";
				mes "我可能是偉大的鍛造者, 但我有時也會犯些錯誤。";
				close;
			}
		}
		if((countitem(.@material) < 1) || (Zeny < .@price)) {
			mes "[" + getarg(0) + "]";
			mes "你看起來沒有";
			mes "足夠的 Zeny or "+getitemname(.@material)+"...";
			mes "請多帶些來。 我會";
			mes "在這裡等你的。";
			close;
		}
		set Zeny, Zeny-.@price;
		delitem .@material,1;

		//custom checks
		if(getequipisequiped(.@part) == 0) { // hacker has removed the item (not changed, why?)
			mes "[" + getarg(0) + "]";
			mes "看這裡... 你沒有穿上任何裝備...";
			close;
		}
		if(getequiprefinerycnt(.@part) != .@refinerycnt || getequipid(.@part) != .@refineitemid) { // hacker has changed the item
			mes "[" + getarg(0) + "]";
			emotion e_an;
			mes "請等一下...";
			mes "你認為我是老人癡呆嗎?!";
			mes "你趁我沒注意時替換裝備! 給我滾出去!";
			close;
		}

		if(getequippercentrefinery(.@part) <= rand(100)) {
			failedrefitem .@part;
			mes "[" + getarg(0) + "]";
			emotion (!rand(5))?e_cash:e_omg;
			set .@lose,rand(1,3);
			if (.@lose == 1) {
				mes "我的天啊!";
				mes "該死! 別再來了!";
				mes "我向你鄭重道歉, 但你明白我的手藝是正宗的。";
				mes "恩, 沒錯吧? 嘿 嘿...";
			} else if(.@lose == 2) {
				mes "不~~~~~~!";
				mes "它毀滅了!";
				mes "我~~ 我對不起你!";
			} else {
				mes "不可能的!";
				mes "它竟然沒得到";
				mes "更多的韌度!";
				mes "我為此感到抱歉...";
			}
			close;
		}
		mes "["+getarg(0)+"]";
		successrefitem .@part;
		emotion e_heh;
		set .@win,rand(1,3);
		if (.@win == 1) {
			mes "完美!";
			mes "嘿 嘿!";
			mes "又一次,";
			mes "來自大師的";
			mes "完美作品~";
		} else if(.@win == 2) {
			mes "完成...!";
			mes "恩.. 我的天賦";
			mes "在今天又一次散發出驚人的光芒。";
		} else {
			mes "嘿 嘿!";
			mes "我全部完成了。";
			mes "毫無疑問的, 我的工作";
			mes "讓你滿意。";
			mes "絕對~ 絕對~ 絕對完美~";
		}
		close;
	}

// New Refining Functions ========================
	if(getequiprefinerycnt(.@part) < .@safe) {
		mes "[" + getarg(0) + "]";
		mes "我可以鍛造到安定值或需要的次數... 你的選擇...";
		next;
		set .@menu2,select("請到安定值即可。:我將會決定次數。:我改變心意了...");
	} else
		set .@menu2,2;
	switch(.@menu2){
	case 1: 
		set .@refinecnt,.@safe - getequiprefinerycnt(.@part);
		break;
	case 2:
		next;
		mes "[" + getarg(0) + "]";
		mes "你想要我鍛造你的裝備幾次?";
		next;
		input .@refinecnt;
		set .@refinecheck,.@refinecnt + getequiprefinerycnt(.@part);
		if (.@refinecnt < 1 || .@refinecheck > 10) {
			mes "[" + getarg(0) + "]";
			mes "我無法鍛造這項裝備太多次.";
			close;
		}
		if(.@refinecheck > .@safe) {
			set .@refinecheck,.@refinecheck - .@safe;
			mes "[" + getarg(0) + "]";
			mes "此裝備將會在超出安定值下連續鍛造 " + .@refinecheck + " 次。 你的裝備可能會損毀... 明白嗎?";
			next;
			if(select("Yes...","No...") == 2){
				mes "[" + getarg(0) + "]";
				mes "你已經這麼說..嗯!那就這樣吧...";
				close;
			}
		}
		break;
	case 3:
		next;
		mes "[" + getarg(0) + "]";
		mes "你已經這麼說..嗯!那就這樣吧...";
		close;
	}
	set .@fullprice,.@price * .@refinecnt;
	mes "[" + getarg(0) + "]";
	mes "你將要支付 " + .@refinecnt + " " + getitemname(.@material) + " 和 " + .@fullprice + " Zeny。 明白嗎?";
	next;
	if(select("Yes","No...") == 2){
		mes "[" + getarg(0) + "]";
		mes "你已經這麼說..嗯!那就這樣吧...";
		close;
	}
	if(countitem(.@material) < .@refinecnt || Zeny < .@fullprice) {
		mes "[" + getarg(0) + "]";
		mes "這是你的所有嗎? 真不幸的, 我不允許你支付較低手續費。 你替別人想想吧。";
		close;
	}
	set Zeny, Zeny - .@fullprice;
	delitem .@material,.@refinecnt;
	while(.@refinecnt){
		if (getequipisequiped(.@part) == 0) {
			mes "[" + getarg(0) + "]";
			mes "看這裡... 你沒有穿任何裝備...";
			close;
		}
		if (getequipid(.@part) != .@refineitemid || (.@menu2 == 1 && getequippercentrefinery(.@part) < 100)) {
			mes "[" + getarg(0) + "]";
			mes "痾... 在你印象中我是老人癡呆嗎 !?!";
			mes "你更換了裝備...";
			mes "在我用大地之擊打暈你之前給我滾出去!!!";
			close;
		}
		mes "鐺, 鐺!!!";
		if(.@menu2 == 2 && getequippercentrefinery(.@part) <= rand(100)) {
			failedrefitem .@part;
			emotion e_omg;
			mes "[" + getarg(0) + "]";
			mes "什~~什麼!!! 我感到抱歉... 我曾警告過你這會發生...";
			set .@refinecnt,.@refinecnt - 1;
			if(.@refinecnt == 0) close;
			mes "未使用 Zeny 和材料我將會歸還給你...";
			getitem .@material,.@refinecnt;
			set .@fullprice,.@refinecnt * .@price;
			set Zeny, Zeny + .@fullprice;
			close;
		}
		successrefitem .@part;
		emotion e_no1;
		set .@refinecnt,.@refinecnt - 1;
		next;
	}
	mes "[" + getarg(0) + "]";
	mes "全部都完成了... 請再度光臨。";
	close;
}

// Material Salesmen
//============================================================
prt_in,56,68,5	script	普勒越	86,{
	callfunc "phramain","普勒越";
	end;
}
payon,145,178,3	script	伯克納德	88,{
	callfunc "phramain","伯克納德";
	end;
}
morocc_in,63,32,6	script	沙德	99,{
	callfunc "phramain","沙德";
	end;
}
alberta_in,13,71,3	script	卡拉曼莉斯	86,{
	callfunc "phramain","卡拉曼莉斯";
	end;
}
yuno_in01,171,27,4	script	德雷曼	88,{
	callfunc "phramain","德雷曼";
	end;
}
ein_in01,15,87,3	script	迪樂意哈斯	86,{
	callfunc "phramain","迪樂意哈斯";
	end;
}
lhz_in02,278,24,3	script	客勒柯	86,{
	callfunc "phramain","客勒柯";
	end;
}

// Material Salesmen Functions
//============================================================
function	script	phramain	{
	if (checkweight(1201,1) == 0) {
		mes "- 稍等下!!                 -";
		mes "- 當前你攜帶了太多東西了.  -";
		mes "- 先嘗試減輕你的重量吧.    -";
		close;
	}
	mes "[" + getarg(0) + "]";
	mes "這裡有賣鐵或鋼鐵製武器鍛治時所需要的金屬!";
	mes "這裡有賣鍛治武器等級1的^007777強化武器用金屬-強化級數一^000000 與...鍛治武器等級";
	mes "2的^007777強化武器用金屬-強化級數二^000000";
	mes "等金屬。";
	next;
	switch(select("強化武器用金屬-強化級數一 - 200金幣:強化武器用金屬-強化級數二 - 1000金幣:詢問有沒有其他金屬")) {
	case 1:
		set .@material,1010;
		set .@price,200;
		break;
	case 2:
		set .@material,1011;
		set .@price,1000;
		break;
	case 3:
		mes "[" + getarg(0) + "]";
		mes "你問的應該是製作武器等級1與2以上所需要的金屬吧!";
		mes "我想你應該也知道，神之金屬和鋁...原本就比較難取得...";
		close;
	}
	mes "[" + getarg(0) + "]";
	mes "你想要購買多少數量?欲終止交易請按'0'鍵。";
	next;
	while(1) {
		input .@input;
		if (.@input == 0) {
			mes "[" + getarg(0) + "]";
			mes "交易已經終止。";
			close;
		}
		else if (.@input < 0 || .@input > 500) {
			mes "[" + getarg(0) + "]";
			mes "請將購買數量減至500個以下。";
			next;
		}
		else {
			break;
		}
	}
	set .@sell,.@input * .@price;
	if (Zeny < .@sell) {
		mes "[" + getarg(0) + "]";
		mes "唉呀...客人，你的金錢不足噢";
		mes "很抱歉，我們是小本經營，不能算便宜一點給你";
		close;
	}
	if (checkweight(.@material,.@input) == 0) {
		mes "[" + getarg(0) + "]";
		mes "呃..在拿取東西前請先整理一下你的物品..";
		close;
	}
	getitem .@material,.@input;
	set Zeny, Zeny-.@sell;
	mes "[" + getarg(0) + "]";
	mes "非常感謝你的購買。若有任何需要，歡迎你隨時再來找我。";
	close;
}

// Ori/Elu Refiners
//============================================================
prt_in,63,69,3	script	帝特立西	84,{
	callfunc "orimain","帝特立西";
	end;
}
payon,137,178,5	script	夏金	88,{
	callfunc "orimain","夏金";
	end;
}
morocc_in,72,32,6	script	亞度拉	99,{
	callfunc "orimain","亞度拉 拉樂帕";
	end;
}
alberta_in,21,63,5	script	智諾普	84,{
	callfunc "orimain","智諾普";
	end;
}
yuno_in01,164,27,4	script	德雷伊	88,{
	callfunc "orimain","德雷伊";
	end;
}
ein_in01,18,82,6	script	馬帝修塔音	84,{
	callfunc "orimain","馬帝修塔音";
	end;
}
lhz_in02,281,24,5	script	葡魯伊	84,{
	callfunc "orimain","葡魯伊";
	end;
}

// Ori/Elu Functions
//============================================================
function	script	orimain	{
	if (checkweight(1201,1) == 0) {
		mes "- 稍等下!!                 -";
		mes "- 當前你攜帶了太多東西了.  -";
		mes "- 先嘗試減輕你的重量吧.    -";
		close;
	}
	mes "[" + getarg(0) + "]";
	mes "只要你將神之金屬或鋁原石拿來給我，我";
	mes "馬上就可以為你提煉神之金屬以及鋁，";
	mes "你剛好各帶了五個原石來啊!";
	next;
	switch(select("提煉神之金屬:提煉鋁:詢問有關屬性石相關事項")) {
	case 1:
		if (countitem(756) > 4) {
			delitem 756,5;  //Oridecon_Stone
			getitem 984,1; // Oridecon
			mes "[" + getarg(0) + "]";
			mes "拿去吧!這是約定好要給你的神之金屬。";
			mes "有任何事情歡迎隨時再來找我。";
			close;
		}
		else {
			mes "[" + getarg(0) + "]";
			mes "哈哈哈哈...你真是愛開玩笑。";
			mes "我沒跟你說過要是沒有五個神之金屬原石的話是無法提煉神之金屬的嗎?";
			close;
		}
	case 2:
		if (countitem(757) > 4) {
			delitem 757,5;  //Elunium_Stone
			getitem 985,1; // Elunium
			mes "[" + getarg(0) + "]";
			mes "拿去吧!這是約定好要給你的鋁。";
			mes "有任何事情歡迎隨時再來找我。";
			close;
		}
		else {
			mes "[" + getarg(0) + "]";
			mes "哈哈哈哈...你真是愛開玩笑。";
			mes "我沒跟你說過要是沒有五個鋁原石的話是無法提煉鋁的嗎?";
			close;
		}
	case 3:
		mes "[" + getarg(0) + "]";
		mes "所謂的屬性石...";
		mes "我做鍛冶金屬這行已經做了二十年，聽過";
		mes "很多這類的傳說...但還沒實際見過呢!";
		mes "我聽說金屬都各自擁有水、地、火、風等";
		mes "屬性";
		next;
		mes "[" + getarg(0) + "]";
		mes "聽說在鍛冶武器時加入屬性石的話...所製作出來的武器將擁有那屬性石的超強力量。";
		mes "哈哈..不過話說回來，能做出這種武器的人想必一定擁有極高的鍛冶技術。";
		close;
	}
}

// Equipment Repairmen
//============================================================
alberta_in,31,65,4	script	修理工#alb	86,{
	callfunc "repairmain","修理工";
	end;
}

moc_ruins,107,94,4	script	修理工#moc	99,{
	callfunc "repairmain","修理工";
	end;
}

payon,143,165,4	script	修理工#pay	88,{
	callfunc "repairmain","修理工";
	end;
}

prt_in,63,54,2	script	修理工#prt	86,{
	callfunc "repairmain","Grendal";
	end;
}

yuno_in01,175,28,3	script	修理工#juno	86,{
	callfunc "repairmain","修理工";
	end;
}

geffen_in,34,166,3	script	修理工#gef	99,{
	callfunc "repairmain","修理工";
	end;
}

aldeba_in,38,60,3	script	修理工#alde	86,{
	callfunc "repairmain","修理工";
	end;
}

lhz_in02,284,14,3	script	修理工#lhz	86,{
	callfunc "repairmain","修理工";
	end;
}

prt_gld,139,117,4	script	修理工#prt_gld	86,{
	callfunc "repairmain","修理工";
	end;
}

gef_fild13,263,117,4	script	修理工#gef_fild	86,{
	callfunc "repairmain","修理工";
	end;
}

pay_gld,295,183,4	script	修理工#pay_gld	86,{
	callfunc "repairmain","修理工";
	end;
}

alde_gld,220,152,4	script	修理工#alde_gld	86,{
	callfunc "repairmain","修理工";
	end;
}

aru_gld,189,336,4	script	修理工#aru_gld	86,{
	callfunc "repairmain","修理工";
	end;
}

sch_gld,340,80,7	script	修理工#sch_gld	86,{
	callfunc "repairmain","修理工";
	end;
}

// Equipment Repair Function
//============================================================
function	script	repairmain	{
	set .@repairprice,5000;
	mes "["+getarg(0)+"]";
	mes "那位旅客!";
	mes "想要修理物品嗎？";
	mes "要修理的物品就交給我吧!!";
	next;
	switch(select("我有物品要修理...:現在還沒有物品要修理")) {
	case 1:
		set .@checkitem,1;
		while (1) {
			if (getbrokenid(.@checkitem) == 0) {
				break;
			}
			set .@checkitem,.@checkitem+1;
		}
		set .@checkitem,.@checkitem-1;
		if (!.@checkitem) {
			mes "["+getarg(0)+"]";
			mes "真的假的，看來你真的很會";
			mes "操作物品啊，沒有一個受損的物品啊？";
			mes "像你這種人越多，";
			mes "我不久就會失業了";
			close;
		}
		set .@totalcost,.@repairprice*.@checkitem;
		mes "["+getarg(0)+"]";
		mes "嗯嗯... 你所擁有的物品中";
		mes "受損的是 " + .@checkitem + "吧？";
		mes "修理一樣是 " + .@repairprice + " 金幣..";
		mes "全部修理要" + .@totalcost + "金幣!";
		mes "你要修理嗎?";
		next;
		switch(select("是:否")) {
		case 1:
			if (Zeny < .@totalcost) {
				mes "["+getarg(0)+"]";
				mes "你身上已經沒有錢了嗎...?";
				mes "我還有妻小要養啊...";
				close;
			}
			set .@checkitem2,1;
			while (1) {
				if (getbrokenid(.@checkitem2) == 0) {
					break;
				}
				set .@checkitem2,.@checkitem2+1;
			}
			set .@checkitem2,.@checkitem2-1;
			if (.@checkitem == .@checkitem2) {
				set zeny,zeny-.@totalcost;
				while (.@checkitem) {
					repair(.@checkitem);
					set .@checkitem,.@checkitem-1;
				}
				mes "["+getarg(0)+"]";
				mes "ＯＫ! 都修理好了，";
				mes "小心點使用吧，畢竟";
				mes "物品也是有生命的..";
				close;
			}
			else {
				mes "["+getarg(0)+"]";
				mes "嗯? 有些問題. 等一下... 拿上";
				mes "你需要修理的物品然後再來找我.";
				close;
			}
		case 2:
			mes "["+getarg(0)+"]";
			mes "嗯...我是沒差啦";
			mes "不過把受損的物品就這樣放著不太好，";
			mes "最好還是早點修理吧!";
			close;
		}
	case 2:
		mes "["+getarg(0)+"]";
		mes "呵呵呵... 沒有要修理的物品";
		mes "就沒有我的事了";
		close;
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.0 by A bunch of people!
//=     Syrus22 - Completely redid the script using functions... also
//=     added the option for auto safe refining and multiple refining.
//= 1.1 Negative input bug fixed [Lupus]
//= 1.2 Added additional reparimen in morroc and payon. Added
//=     Christopher the blacksmith in Geffen. Edited some dialogue [kobra_k88]
//= 1.3 New Payon Locations [Darkchild]
//=     Corrected zeny subtraction thx to jpnmania77.[kobra_k88]
//= 1.3a Temporary corrected an exploit. Need to check sources
//=     to fully fix bug [Shinigami]
//=     Fixed 修理工 prices [shadowlady]
//=     Fixed bug that skips requirements thanks to sir_loon [massdriller]
//=     Fixed itemid error thanks to -Vitamin- [massdriller]
//= 1.4 check again item in refining procedure to avoid
//=     hacker that can change item [dafide18]
//= 1.5 Fixed crashing due to badly used callfunc's [Skotlex]
//=     Lupus, don't rollback this important fix again! >.<
//= 1.5a Corrected an unneeded callfunc, fixed the anti-bot 
//=     exploit ruining the safe refine loop. [Skotlex]
//= 1.5b Fixed Spelling mistakes. [Nexon] 
//= 1.6 Replaced all breaks for ends as per the new script engine [Skotlex]
//= 1.7 Added Einbroch Refiners (Custom names ^^;) and a duplicated BS Shop. [Poki#3]
//= 1.8 Added Lighthalzen Refiners (Custom names again ^^;) [Poki#3]
//= 1.8a Fixed wrong indication thanks to NeoSaro [Lupus]
//= 1.9 Rewrote 修理工, removed the Steel from repair cost [DracoRPG]
//= 2.0 Fixed missed equppment presence check. Thx2 Coltaro [Lupus]
//= 2.0a Added weight checks thanks to Neouni [Playtester]
//= 2.0b Fixed the names of Lighthalzen and Einbroch refiners thanks to Maud_Dib [Kargha]
//= 2.1 Removed Duplicates [Silent]
//= 2.2 Changed name from "Emvertacon" to "Emveretarcon". [Samuray22]
//=     Thanks to Barron-Monster.
//= 2.2b Changed name from "Pharacon" to "Phracon". [Samuray22]
//=     Thanks to Barron-Monster.
//= 2.3 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.4 Updated Refiner function. cleaner, and less dated. [L0ne_w0lf]
//= 2.5 Rather large update to the refiner and merchants. :D [L0ne_W0lf]
//= 2.6 Fixed a few bugs with creating pure stones. [L0ne_W0lf]
//= 2.7 Refiner function accepts additional paramater. [L0ne_W0lf]
//=     0 = No special features; 1 = new refining features
//=     Updated Repairmen and function. No longer shows menu.
//= 2.7a A couple touch-ups to the 修理工 function. [L0ne_w0lf]
//= 2.8 Changed the nonexistent variable .@matname$ for getitemname(.@material). (bugreport:2340) [Samuray22]
//= 2.8 Added proper Blacksmith Supplier to Einroch. [L0ne_W0lf]
//=     Updated dated features comment to reflect new usage.
//= 2.8a Small bugfix. (bugreport:2418) [Paradox924X]
//= 2.9 Moved Morroc 修理工 to Morroc Ruins. [L0ne_W0lf]
//============================================================
