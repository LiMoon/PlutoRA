//===== rAthena Script ======================================= 
//= Armor Enchanter
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.3a
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Add a +1, +2, or +3 random stat to a specified armor.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Corrected typo (Sit -> Suit) (bugreport:4586)
//= 1.2 Optimized. [Euphy]
//= 1.3 Fixed some typos and optimized a little more. [Daegaladh]
//= 1.3a Added 'disable_items' command. [Euphy]
//============================================================ 

prontera,165,60,2	script	實習打洞師	73,{
	disable_items;
	mes "[實習打洞師]";
	if (Zeny > 399999) {
		mes "長期以來我一直在研究如何改造盔甲，使盔甲的能力極大化";
		next;
		mes "[實習打洞師]";
		mes "所謂鑽洞，是指在盔甲內隱藏的插槽裡注入神秘力量的跨時代技術";
		next;
		mes "[實習打洞師]";
		mes "當然要注意，同樣的盔甲如果擁有兩件以上，會按照物品在裝備視窗中的順序進行鑽洞";
		next;
		mes "[實習打洞師]";
		mes "在這種情況下，有可能會對不想鑽洞的盔甲鑽洞，因此請只攜帶^5555ff1個^000000需要鑽洞的盔甲過來即可";
		next;
		mes "[實習打洞師]";
		mes "如果帶著兩件以上的盔甲過來，結果出了意外，我是不負任何責任喔!";
		next;
		switch(select("無洞盔甲:1洞盔甲:高級盔甲:下次再說")) {
		case 1:
			setarray .@Items[0],2307,2309,2314,2316,2321,2325,2327,2328,2330,2332,2334,2335,2341,2344,2346,2348,2350,2337,2386,2394,2395,2396;
			set .@j,50;
			break;
		case 2:
			setarray .@Items[0],2311,2318,2319,2320,2308,2310,2315,2317,2322,2324,2326,2331,2333,2336,2342,2345,2347,2349,2351;
			set .@j,55;
			set .@k,1;
			break;
		case 3:
			setarray .@Items[0],2364,2365,2391,2374,2375,2376,2377,2378,2379,2380,2381,2382,2387,2388,2389,2390;
			set .@j,60;
			break;
		case 4:
			mes "[實習打洞師]";
			mes "以後如果有興趣的話，請務必再來找我";
			close;
		}
		set .@menu$,"";
		for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+1)
			set .@menu$, .@menu$+getitemname(.@Items[.@i])+((.@k)?" [1]":"")+":";
		callsub S_EnchantArmor, .@Items[select(.@menu$)-1], .@j;
		end;
	}
	mes "我負責盔甲的鑽洞工作，也就是說我是專";
	mes "門研究如何提高既有盔甲力量的人";
	next;
	mes "[實習打洞師]";
	mes "如果你有興趣的話，請帶著40萬金幣和需";
	mes "要鑽洞的盔甲過來找我吧!";
	close;

S_EnchantArmor:
	set .@itemid,getarg(0);
	set .@failrate,getarg(1);
	mes "[實習打洞師]";
	if (countitem(.@itemid) == 1) {
		mes "插槽鑽洞需要40萬金幣，而且鑽洞效果是隨機的，當然裝備有一定機率會損壞";
		next;
		mes "[實習打洞師]";
		mes "最重要的是，";
		mes "^ff5555既有的精煉值和原來裝備的";
		mes "卡片會全部消失^000000，";
		mes "你確定要鑽洞嗎?";
		next;
		if(select("再考慮一下:已經決定了，請幫我鑽洞") == 1) {
			mes "[實習打洞師]";
			mes "安全第一，祝你旅途愉快!";
			close;
		}
		mes "[實習打洞師]";
		mes "看來你很喜歡冒險，那我們開始吧";
		close2;
		specialeffect2 EF_MAPPILLAR;
		if (Zeny < 400000) {
			mes "[實習打洞師]";
			mes "抱歉，你身上的錢不夠";
			close;
		}
		progressbar "ffff00",7;
		set Zeny, Zeny-400000;
		delitem .@itemid,1;
		switch (rand(1,.@failrate)) {
			case 1: set .@addpart,4702;break;
			case 2: set .@addpart,4712;break;
			case 3: set .@addpart,4722;break;
			case 4: set .@addpart,4732;break;
			case 5: set .@addpart,4742;break;
			case 6: set .@addpart,4752;break;
			case 7:
			case 8: set .@addpart,4701;break;
			case 9:
			case 10: set .@addpart,4711;break;
			case 11:
			case 12: set .@addpart,4721;break;
			case 13:
			case 14: set .@addpart,4731;break;
			case 15:
			case 16: set .@addpart,4741;break;
			case 17:
			case 18: set .@addpart,4751;break;
			case 19:
			case 20:
			case 21: set .@addpart,4700;break;
			case 22:
			case 23:
			case 24: set .@addpart,4710;break;
			case 25:
			case 26:
			case 27: set .@addpart,4720;break;
			case 28:
			case 29:
			case 30: set .@addpart,4730;break;
			case 31:
			case 32:
			case 33: set .@addpart,4740;break;
			case 34:
			case 35:
			case 36: set .@addpart,4750;break;
			default:
				specialeffect2 EF_PHARMACY_FAIL;
				mes "[實習打洞師]";
				mes "真是糟糕!";
				mes "打洞失敗了";
				close;
		}
		getitem2 .@itemid, 1, 1, 0, 0, 0, 0, 0, .@addpart;
	}
	else {
		mes "嗯? 好像什麼都沒有啊?";
		mes "請帶著1個要鑽洞的裝備再來找我吧!";
		close;
	}
}
