//===== rAthena Script ======================================= 
//= Bakonawa Lake
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Defeat Bakonawa before he eats the moon.
//= Part of the "Bakonawa Extermination" quest.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//= 1.1 Added GM management NPC. [Euphy]
//= 1.2 Added VIP features. [Euphy]
//============================================================ 

ma_scene01,174,179,4	script	紹紹	541,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "You are carrying too many items. Come back after taking off some of them.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "You are carrying too much weight. Come back after reducing the weight.";
		close;
	}
	if (BaseLevel < 140) {
		mes "[紹紹]";
		mes "Hey. This is too dangerous a place for you. You cannot keep yourself safe. Get out of here and go to the village right now!";
		close;
	}
	if (malaya_bakona1 < 7 && malaya_bakona2 < 15) {
		mes "[紹紹]";
		mes "We are gonna be killed all by that Bakonawa. Ah, we must slay him but unfortunately we are not prepared.";
		close;
	}

	set .@party_id,getcharid(1);
	set .@md_name$, "Bakonawa Lake";

	set .@baku_time, checkquest(12278,PLAYTIME);
	if (.@baku_time == -1) {
		if (checkquest(12279) == -1) setquest 12279;
	}
	if (!.@party_id) {
		mes "[紹紹]";
		mes "Isn't there any company who can go with you? Or even if you wanna go alone, come after organizing a party.";
		close;
	}
	if (.@baku_time == -1) {
		if (getcharid(0) == getpartyleader(.@party_id,2)) {
			mes "[紹紹]";
			mes "Are you the leader of the Bakonawa slayers? The road is not well so we have to go down the cliff with a rope, is that OK?";
			next;
			set .@i, select("Please weave a rope.:Now I will go down.:Cancel.");
		} else {
			mes "[紹紹]";
			mes "You came for slaying Bakonawa. The leader of your party has to help me weaving the rope for going down.";
			next;
			set .@i, select(":Now I will go down.:Cancel.");
		}
		switch(.@i) {
		case 1:
			if (instance_create(.@md_name$) < 0) {
				mes "[紹紹]";
				mes "Oh, the rope got dropped. I have to make a new one.";
				close;
			}
			mes "[紹紹]";
			mes "Now I'm weaving, so you can go down when I'm done.";
			close;
		case 2:
			switch(instance_enter(.@md_name$)) {
			case 3:
				mes "[紹紹]";
				mes "An unknown error occured.";
				close;
			case 2:
				mes "[紹紹]";
				mes "The rope hasn't been weaved yet. Wait a little.";
				close;
			case 1:
				mes "[紹紹]";
				mes "Civilians cannot enter.";
				close;
			case 0:
				mapannounce "ma_scene01","A party memeber, "+strcharinfo(0)+" of the party "+getpartyname(.@party_id)+" is entering the dungeon, "+.@md_name$+".",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
				setquest 12278;
				//warp "1@ma_b",64,51;
				close;
			}
		case 3:
			close;
		}
	} else if (.@baku_time == 0 || .@baku_time == 1) {
		mes "[紹紹]";
		mes "The rope used for getting to the Lake is already broken. We are now cleaning up the surroundings so come back after some time.";
		close;
	} else if (.@baku_time == 2) {
		mes "[紹紹]";
		mes "It's all cleaned up around here. Now you can go down again after getting a rope.";
		erasequest 12278;
		close;
	}
}

sec_in02,26,26,4	script	Bakonawa's Rage	541,{
	callfunc "F_GM_NPC";
	mes "[紹紹]";
	mes "My name is Minky.";
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		erasequest 12278;
		erasequest 12279;
		set malaya_bakona1,7;
		set malaya_bakona2,15;
	}
	close;
}

1@ma_b,62,52,4	script	紹紹#nf	541,{
	mes "[紹紹]";
	mes "嘿嘿，雖然我也想參與對巴庫暴龍的戰鬥，但我還是選擇躲在那個岩石上替大家加油好了。";
	next;
	mes "[紹紹]";
	mes "有時巴庫暴龍會變成打不死的狀態，這個時候就要找我的話去做就可以了。";
	next;
	if (getcharid(0) == getpartyleader(getcharid(1),2)) {
		mes "[紹紹]";
		mes "我現在來引誘巴庫暴龍浮出水面。";
		next;
		switch(select("等等! 還沒!:好! 來解決牠吧!")) {
		case 1:
			mes "[紹紹]";
			mes "Oh... alright... tell me when you are ready.";
			close;
		case 2:
			mes "[紹紹]";
			mes "我要在這湖面滴一滴靈藥了!";
			next;
			mes "[紹紹]";
			mes "巴庫暴龍嚐到的話很快就會變得兇惡暴躁了";
			donpcevent instance_npcname("#Bakonawan1")+"::OnStart";
			disablenpc instance_npcname("紹紹#nf");
			close;
		}
	} else {
		mes "[紹紹]";
		mes "I am dropping the Alpopal to make Bakonawa mad. Bakonawa can be a good source of protein once we get him.";
		close;
	}
}

1@ma_b,36,111,4	script	#Bakonawan1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan1");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan1");
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"紹紹: 巴庫暴龍浮出水面了，趕快進行攻擊!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	monster .@map$,78,81,"巴庫暴龍",2320,1,instance_npcname("#Bakonawan1")+"::OnMyMobDead"; //original: (78,83)
	initnpctimer;
	end;
OnFail:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	if (mobcount(.@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#BakonawaDeadn1")+"::OnStart";
		killmonster .@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan1");
	}
	end;
OnTimer1000:	callsub OnAnnounce,"10分鐘";
OnTimer60000:	callsub OnAnnounce,"9分鐘";
OnTimer120000:	callsub OnAnnounce,"8分鐘";
OnTimer180000:	callsub OnAnnounce,"7分鐘";
OnTimer240000:	callsub OnAnnounce,"6分鐘";
OnTimer300000:	callsub OnAnnounce,"5分鐘";
OnTimer360000:	callsub OnAnnounce,"4分鐘";
OnTimer420000:	callsub OnAnnounce,"3分鐘";
OnTimer480000:	callsub OnAnnounce,"2分鐘";
OnTimer540000:	callsub OnAnnounce,"1 minute";
OnTimer570000:
	mapannounce instance_mapname("1@ma_b"),"限時: 30秒",bc_map,"0xff4400"; //FW_NORMAL 18 0 0
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
OnTimer600000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"Bakonawa has escaped deep into the lake.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("紹紹#Fail")+"::OnStart";
	donpcevent instance_npcname("#Bakonawan1")+"::OnFail";
	killmonster .@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan1");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"限時: "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,78,81,0	script	#SummonPuppetsn1	139,4,5,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnStart:
	stopnpctimer;
	enablenpc instance_npcname("#SummonPuppetsn1");
	initnpctimer;
	end;
OnTouch:
	stopnpctimer;
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnTimer5000:
	setarray .@x[0],79,71,60,61,57,89,95,96,99;
	setarray .@y[0],71,72,80,90,99,73,82,90,99;
	set .@map$, instance_mapname("1@ma_b");
	for(set .@i,0; .@i<9; set .@i,.@i+1) {
		set .@rand, rand(1,10);
		if (.@rand > 7)
			monster .@map$,.@x[.@i],.@y[.@i],"巴庫暴龍的意志",2337,1,instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
		else if (.@rand < 4)
			monster .@map$,.@x[.@i],.@y[.@i],"巴庫暴龍的意志",2343,1,instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
	}
	end;
OnTimer50000:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnMyMobDead:
	end;
}

1@ma_b,1,5,4	script	#BakonawaDeadn1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#BakonawaDeadn1");
	end;
OnStart:
	enablenpc instance_npcname("#BakonawaDeadn1");
	initnpctimer;
	end;
OnTimer100:
	mapannounce instance_mapname("1@ma_b"),"紹紹: 很好! 巴庫暴龍逃到水底躲起來了! 現在牠想要吞掉月亮呢!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer5000:
	mapannounce instance_mapname("1@ma_b"),"紹紹: 為了不要讓牠把注意力放在月亮上，必須敲鑼打鼓來吵牠!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"紹紹: 就算牠浮出水面也不要攻擊牠，只要專心敲鑼打鼓即可!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer15000:
	mapannounce instance_mapname("1@ma_b"),"目標 - 破壞湖泊左右的鑼和鍋共4個。",bc_map,"0xff3300"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#Bakonawan2")+"::OnStart";
	stopnpctimer;
	disablenpc instance_npcname("#BakonawaDeadn1");
	end;
}

1@ma_b,36,111,4	script	#Bakonawan2	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan2");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan2");
	set .@label$, instance_npcname("#Bakonawan2")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_b");
	monster .@map$,95,98,"鍋",2328,1,.@label$;
	monster .@map$,60,98,"鍋",2328,1,.@label$;
	monster .@map$,97,104,"鑼",2328,1,.@label$;
	monster .@map$,58,104,"鑼",2328,1,.@label$;
	donpcevent instance_npcname("#Bakonawan2-1")+"::OnStart";
	initnpctimer;
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("#Bakonawan2")+"::OnMyMobDead");
	if (.@mob_dead_num < 1) {
		donpcevent instance_npcname("#Bakonawan2-1")+"::OnEnd";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan2");
	} else
		mapannounce .@map$,"紹紹: 很好，只剩 "+.@mob_dead_num+"個了!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	 end;
OnTimer1000:	callsub OnAnnounce,"5分鐘",15,1;
OnTimer60000:	callsub OnAnnounce,"4分鐘",15,1;
OnTimer120000:	callsub OnAnnounce,"3分鐘",15,1;
OnTimer180000:	callsub OnAnnounce,"2分鐘",15,1;
OnTimer240000:	callsub OnAnnounce,"1分鐘",15,1;
OnTimer270000:	callsub OnAnnounce,"30秒",15,0;
OnTimer280000:	callsub OnAnnounce,"20秒",15,0;
OnTimer290000:	callsub OnAnnounce,"10秒",15,0;
OnTimer295000:	callsub OnAnnounce,"5秒",16,0;
OnTimer296000:	callsub OnAnnounce,"4秒",17,0;
OnTimer297000:	callsub OnAnnounce,"3秒",18,0;
OnTimer298000:	callsub OnAnnounce,"2秒",19,0;
OnTimer299000:	callsub OnAnnounce,"1秒",20,0;
OnTimer300000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"Bakonawa has escaped deep into the lake.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("紹紹#Fail")+"::OnStart";
	donpcevent instance_npcname("#Bakonawan2-1")+"::OnFail";
	killmonster .@map$,instance_npcname("#Bakonawan2")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan2");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"限時: "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL getarg(1) 0 0
	if (getarg(2)) donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,36,111,4	script	#Bakonawan2-1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan2-1");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan2-1");
	monster instance_mapname("1@ma_b"),78,81,"覬覦月亮的巴庫暴龍",2321,1,instance_npcname("#Bakonawan2-1")+"::OnMyMobDead"; //original: (78,93)
	end;
OnFail:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	end;
OnEnd:
	enablenpc instance_npcname("#Bakonawan2-1");
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	initnpctimer;
	end;
OnTimer1000:
	mapannounce instance_mapname("1@ma_b"),"紹紹: 巴庫暴龍再次躲到水底了，難道結束了嗎?",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer5000:
	mapannounce instance_mapname("1@ma_b"),"紹紹: 啊! 從水裡快速冒出東西來了，那是巴庫暴龍!!!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"目標 - 消滅變得更加兇暴的巴庫暴龍",bc_map,"0xff3300"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#Bakonawan3")+"::OnStart";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan2-1");
	end;
OnMyMobDead:
	end;
}

1@ma_b,36,111,4	script	#Bakonawan3	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan3");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan3");
	initnpctimer;
	monster instance_mapname("1@ma_b"),78,81,"覬覦月亮的巴庫暴龍",2322,1,instance_npcname("#Bakonawan3")+"::OnMyMobDead"; //original: (78,83)
	donpcevent instance_npcname("#Bakonawan3-1")+"::OnStart";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead");
	if (.@mob_dead_num < 1) {
		mapannounce .@map$,"紹紹: 消滅了巴庫暴龍! 他吐出了遺物箱!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
		donpcevent instance_npcname("紹紹#Completed")+"::OnStart";
		donpcevent instance_npcname("#Bakonawan3-1")+"::OnEnd";
		killmonster .@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan3");
	}
	end;
OnTimer1000:
	callsub OnAnnounce,"10分鐘",15,1;
	end;
OnTimer60000:
OnTimer120000:
OnTimer180000:
OnTimer240000:
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
OnTimer300000:	callsub OnAnnounce,"5分鐘",15,1;
OnTimer360000:	callsub OnAnnounce,"4分鐘",15,1;
OnTimer420000:	callsub OnAnnounce,"3分鐘",15,1;
OnTimer480000:	callsub OnAnnounce,"2分鐘",15,1;
OnTimer540000:	callsub OnAnnounce,"1分鐘",15,1;
OnTimer570000:	callsub OnAnnounce,"30秒",15,0;
OnTimer580000:	callsub OnAnnounce,"20秒",15,0;
OnTimer590000:	callsub OnAnnounce,"10秒",15,0;
OnTimer595000:	callsub OnAnnounce,"5秒",16,0;
OnTimer596000:	callsub OnAnnounce,"4秒",17,0;
OnTimer597000:	callsub OnAnnounce,"3秒",18,0;
OnTimer598000:	callsub OnAnnounce,"2秒",19,0;
OnTimer599000:	callsub OnAnnounce,"1秒",20,0;
OnTimer600000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"Bakonawa has escaped deep into the lake.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("紹紹#Fail")+"::OnStart";
	killmonster .@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan3");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"限時: "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL getarg(1) 0 0
	if (getarg(2)) donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,36,111,4	script	#Bakonawan3-1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan3-1");
	end;
OnStart:
	initnpctimer;
	end;
OnEnd:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	//showdigit 0;
	stopnpctimer;
	end;
OnMyMobDead:
	showdigit mobcount(instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead");
	end;
OnTimer120000:	callsub OnMobSpawn,10;
OnTimer180000:	callsub OnMobSpawn,15;
OnTimer240000:	callsub OnMobSpawn,20;
//OnTimer300000:	callsub OnMobSpawn,25;
OnTimer300000:	callsub OnMobSpawn,30;
OnTimer360000:	callsub OnMobSpawn,35;
OnTimer420000:	callsub OnMobSpawn,40;
OnTimer480000:	callsub OnMobSpawn,45;
OnTimer540000:	callsub OnMobSpawn,50;
OnTimer600000:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	stopnpctimer;
	end;
OnMobSpawn:
	set .@label$, instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_b");
	killmonster .@map$,.@label$;
	set .@mob_dead_num, mobcount(.@map$,.@label$);
	if (.@mob_dead_num < 50) {
		set .@mob_rg, 50 - .@mob_dead_num;
		if (.@mob_rg > getarg(0))
			set .@mob_rg, getarg(0);
		while(1) {
			areamonster .@map$,74,74,82,74,"Bakonawa's Puppet",2334,1,.@label$;
			set .@mob_dead_num, mobcount(.@map$,.@label$);
			if (.@mob_dead_num >= .@mob_rg) {
				break;
			}
		}
	}
	end;
}

1@ma_b,62,52,4	script	紹紹#Completed	541,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "It seems you are carrying too many different kinds of items. Please try again after reducing it.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "It seems you are already carrying items of too much weight. Please try again after reducing the weight.";
		close;
              }
	if (checkquest(12279,HUNTING) == 2) {
		erasequest 12279;
		mes "[紹紹]";
		mes "This was found from Bakonawa's corpse. It would be better if you take this.";
		if (!getserverdef(VAR_VIP_SCRIPT) || vip_status(1))
			getitem 6499,7; //Ancient_Grudge
		else
			getitem 6499,5; //Ancient_Grudge
		getexp 0,100000;
		next;
	}
	mes "[紹紹]";
	mes "We better hurry and get out of this place. This place is still dangerous even without Bakonawa...";
	next;
	mes "[紹紹]";
	mes "Then let's use to the rope to go up!";
	close2;
	warp "ma_scene01",175,176;
	end;
OnInstanceInit:
	disablenpc instance_npcname("紹紹#Completed");
	end;
OnStart:
	enablenpc instance_npcname("紹紹#Completed");
	initnpctimer;
	end;
OnTimer1000:
	monster instance_mapname("1@ma_b"),78,74,"巴庫暴龍的遺物箱",2335,1;
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"紹紹: 開啟遺物箱後再來找我吧! 我有東西要給你。",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	stopnpctimer;
	end;
}

1@ma_b,62,52,4	script	紹紹#Fail	541,{
	mes "[紹紹]";
	mes "HMM. We almost got him but he is one fast creature...";
	next;
	if (getcharid(0) == getpartyleader(getcharid(1),2)) {
		mes "[紹紹]";
		mes "Are we going to try again?";
		next;
		switch(select("Hold on a sec!:Of couse! We cannot stand back now!")) {
		case 1:
			mes "[紹紹]";
			mes "AWW... I almost brought him out.";
			close;
		case 2:
			mes "[紹紹]";
			mes "Then I will use Albopal once again to the lake.";
			next;
			mes "[紹紹]";
			mes "We are about to see Bakonawa with intenser anger...";
			disablenpc instance_npcname("紹紹#Fail");
			donpcevent instance_npcname("#Bakonawan1")+"::OnStart";
			close;
		}
		close;
	} else {
		mes "[紹紹]";
		mes "I am dropping Albopal like I did a while ago. I hope it pisses him off...";
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname("紹紹#Fail");
	end;
OnStart:
	enablenpc instance_npcname("紹紹#Fail");
	end;
}
