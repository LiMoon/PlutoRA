//===== rAthena Script =======================================
//= Eden Group Quests - Mission [41 - 55]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 41 - 55.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//============================================================

moc_para01,40,38,3	script	委託 [41 - 55]	857,{
	if (countitem(6219) < 1) {
		mes "不提供給非伊甸園成員瀏覽";
		close;
	}
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 1000) {
		mes "^008800Wait a sec!!";
		mes "You cannot receive any more items because you're carrying too many items. Please try again after you lighten up.^000000";
		close;
	}
	if (checkquest(12088) == -1) {
		if (BaseLevel < 41 || BaseLevel > 55) {
			mes "這好像是向Lv41~55的冒險家們提出委託的佈告欄";
			close;
		}
		mes "[委託佈告欄]";
		mes "這是記載著來自各地委託內容的佈告欄";
		next;
		if(select("查詢委託內容:取消") == 2) {
			mes "^000077委託登記改到下一次吧!^000000.";
			close;
		}
		switch(select("打突變蛙:打玩具士兵:打庫克雷:打獸人嬰兒:打獸人戰士:打螃蟹:打邪骸食人魚:打烏賊:打卡那多斯:打麥斯特:打貝殼魔靈:打氣泡蟲:打劍魚:打邪骸獸人:打茲諾克:打木乃伊")) {
			case 1: callsub L_Quest,12072,"你有見過紅青蛙嗎?牠是稀有的突變蛙，我們正在調查牠的生活習性，幫我抓20隻吧!";
			case 2: callsub L_Quest,12073,"玩偶會攻擊人類，太荒謬啦! 幫忙去修理牠們，打個15隻看看吧!";
			case 3: callsub L_Quest,12074,"在海洋內部常常發生偷竊事件，疑犯指向庫克雷，請你幫忙收拾 30隻吧!";
			case 4: callsub L_Quest,12075,"獸人的數量漸漸變多了，雖有點慘忍，但必須誅滅牠們以絕後患，請你看著辦吧!";
			case 5: callsub L_Quest,12076,"只靠解決獸人嬰兒力道似乎不夠，麻煩再解決獸人戰士好嗎? 20隻就夠了!";
			case 6: callsub L_Quest,12077,"你見過有厚殼的螃蟹吧? 幫忙抓螃蟹15隻啦!";
			case 7: callsub L_Quest,12078,"魚夫們老是撈到死魚而困擾不已，幫忙解決30隻好嗎?";
			case 8: callsub L_Quest,12079,"這次是烏賊，幫忙解決15隻吧!";
			case 9: callsub L_Quest,12080,"這城市的高官逛海邊時好像因為卡那多斯而受到腳傷，所以下令展開掃蕩，目標是20隻!";
			case 10: callsub L_Quest,12081,"最近因到處出現的幽靈，讓村民受到極度恐懼，幫忙解決麥斯特15隻好嗎?";
			case 11: callsub L_Quest,12082,"有匿名人士要求解決貝殼魔靈30隻，麻煩你了!";
			case 12: callsub L_Quest,12083,"氣泡蟲?你認識氣泡蟲嗎? 有人委託要求解決15隻呢!";
			case 13: callsub L_Quest,12084,"有個老饕高官想吃鮮魚，需要劍魚20隻，你要試試看嗎?";
			case 14: callsub L_Quest,12085,"目前殭屍獸人嚴重騷擾著王城，雖有派軍人去掃蕩，但人手不足，你要試看看嗎? 打15隻即可!";
			case 15: callsub L_Quest,12086,"有人委託掃蕩茲諾克30隻，你好好表現一下吧!";
			case 16: callsub L_Quest,12087,"這次的委託是消滅木乃伊15隻，嗯~很恐怖吧!我不敢想像，你去好好處理吧!";
		}
		end;
	}
	setarray .@Exp[0],9600,7200,15750,7875,11100,8550,17100,9000,12300,9900,20250,10350,13800,11025,22500,11250;
	for(set .@i,0; .@i<16; set .@i,.@i+1)
		if (checkquest(12072+.@i,HUNTING) == 2) {
			mes "[委託佈告欄]";
			mes "紀錄完成的委託任務後領取經驗值獎勵";
			erasequest 12088;
			erasequest 12072+.@i;
			specialeffect2 EF_STEAL;
			getexp .@Exp[.@i],0;
			close;
		}
	if ((checkquest(12072,HUNTING) == 0) && (checkquest(12073,HUNTING) == 0) && (checkquest(12074,HUNTING) == 0) && (checkquest(12075,HUNTING) == 0) && (checkquest(12076,HUNTING) == 0) && (checkquest(12077,HUNTING) == 0) && (checkquest(12078,HUNTING) == 0) && (checkquest(12079,HUNTING) == 0) && (checkquest(12080,HUNTING) == 0) && (checkquest(12081,HUNTING) == 0) && (checkquest(12082,HUNTING) == 0) && (checkquest(12083,HUNTING) == 0) && (checkquest(12084,HUNTING) == 0) && (checkquest(12085,HUNTING) == 0) && (checkquest(12086,HUNTING) == 0) && (checkquest(12087,HUNTING) == 0)) {
		mes "You may now view the bulletin board.";
		erasequest 12088;
		close;
	}
	mes "沒有其他事情了";
	close;
L_Quest:
	mes "[委託任務板的留言]";
	mes getarg(1);
	next;
	if(select("接受委託:取消") == 1) {
		mes "[委託佈告欄]";
		mes "收到了紀錄委託內容的字條，詳細內容請到任務視窗上的委託消息查詢!";
		setquest 12088;
		setquest getarg(0);
	}
	close;
}
