//===== rAthena Script ======================================= 
//= Al De Baran Guides
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Guides for the city of Al De Baran.
//===== Additional Comments: ================================= 
//= 1.0 First version, Renewal guide.
//= 1.1 Navigation system update. [Euphy]
//=     Optimized.
//============================================================ 

aldebaran,139,63,4	script	導覽兵#01aldebaran::GuideAldearan	105,{
	cutin "prt_soldier",2;
	mes "[艾爾帕蘭 衛兵]";
	mes "這裡是運河之都";
	mes "^8B4513艾爾帕蘭^000000，";
	mes "歡迎您的大駕光臨，";
	mes "需要為您介紹嗎?";
	F_Navi("[艾爾帕蘭 衛兵]");
	next;
	set .@str$,"還需要其它服務嗎?";
	while (1) {
		switch(select("[ 主要設施 ]:[ 商人&小幫手 ]:刪除小地圖的標記:結束對話")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ 卡普拉總部 ]:[ 鍊金術師公會 ]:[ 鐘塔 ]:[ 旅館 & 道具店 ]:[ 裝備維修及購買 ]:[ 酒店 ]:返回上一選單")) {
				case 1:
					callsub L_Mark, F_Navi("卡普拉總部","aldebaran,61,229","^B9062F");
					mes .@str$;
					viewpoint 1,61,229,0,0xFF0000;
					next;
					break;
				case 2:
					callsub L_Mark, F_Navi("鍊金術師公會","aldebaran,60,60","^0000FF");
					mes .@str$;
					viewpoint 1,60,60,1,0x0A82FF;
					next;
					break;
				case 3:
					callsub L_Mark, F_Navi("鐘塔","aldebaran,139,133","^FF5400");
					mes .@str$;
					viewpoint 1,139,133,2,0xFFB400;
					next;
					break;
				case 4:
					callsub L_Mark, F_Navi("旅館 & 道具店","aldebaran,197,70","^006400");
					mes .@str$;
					viewpoint 1,197,70,3,0xAAFF00;
					next;
					break;
				case 5:
					mes "[艾爾帕蘭 衛兵]";
					mes F_Navi("武器、防具購買","aldebaran,197,70","^7F3300")+"或";
					mes "裝備維修的地方，";
					mes .@str$;
					viewpoint 1,72,197,4,0xD2691E;
					next;
					break;
				case 6:
					callsub L_Mark, F_Navi("酒店","aldebaran,231,106","^800080");
					viewpoint 1,231,106,5,0xDA70D6;
					next;
					break;
				case 7:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ 伊甸園傳送師 ]:[ 卡普拉職員 ]:[ 傳送聖誕老人 ]:[ 料理材料商人 ]:返回上一選單")) {
				case 1:
					callsub L_Mark, F_Navi("伊甸園傳送師","aldebaran,133,119","^B9062F");
					mes .@str$;
					viewpoint 1,133,119,6,0xFF0000;
					next;
					break;
				case 2:
					callsub L_Mark, "^0000FF卡普拉職員^000000";
					viewpoint 1,143,119,7,0x0A82FF;
					next;
					break;
				case 3:
					mes "[艾爾帕蘭 衛兵]";
					mes "傳送聖誕老人提供傳送到";
					mes F_Navi("薑餅城","aldebaran,168,168","^006400")+"的服務，";
					mes "已經為您標記在小地圖上了，";
					mes .@str$;
					viewpoint 1,168,168,8,0xAAFF00;
					next;
					break;
				case 4:
					callsub L_Mark, F_Navi("料理材料商人","aldebaran,165,107","^7F3300");
					mes .@str$;
					viewpoint 1,165,107,9,0xD2691E;
					next;
					break;
				case 5:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[艾爾帕蘭 衛兵]";
			mes "已刪除小地圖上的標誌了，";
			mes "還需要其它服務嗎?";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			next;
			break;
		case 4:
			mes "[艾爾帕蘭 衛兵]";
			mes "我知道了，";
			mes "再見!";
			close2;
			cutin "prt_soldier",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[艾爾帕蘭 衛兵]";
	mes getarg(0);
	mes "的位置已經標記在";
	mes "小地圖上了，";
	return;
}
aldebaran,243,143,2	duplicate(GuideAldearan)	導覽兵#02aldebaran	105
aldebaran,135,243,4	duplicate(GuideAldearan)	導覽兵#03aldebaran	105
aldebaran,36,135,6	duplicate(GuideAldearan)	導覽兵#04aldebaran	105
