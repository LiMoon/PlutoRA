//===== rAthena Script ======================================= 
//= 3rd Item Seller
//===== By: ================================================== 
//= Masao, Mercurial
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Sells some 3rd Job related items:
//= - Poison Herbs, Rune Stones, Rare Herbs, Points
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Optimized Poison Herb Salesman NPC
//= 1.2 Optimized and standardized. [Euphy]
//= 1.3 Added Malangdo/Mora Points NPC. [Euphy]
//= 1.4 Updated to match the official scripts. [Euphy]
//= 1.5 Added Izlude duplicates. [Euphy]
//============================================================ 

// 毒草銷售員 (Guillotine Cross)
//============================================================
-	script	::phs	-1,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- 請先確認自己的負重量 -";
		close;
	}
	mes "[毒草銷售員]";
	mes "我是專門販售毒草的";
	mes "毒草商人，請問你需要";
	mes "哪一種呢? 毒草的價格是";
	mes "不二價，通通是 4000金幣";
	next;
	set .@i, select("毒夾竹桃:毒馬櫻丹:毒香草:蛇足毒草:毒山莨菪:大王黛粉毒藥:製毒工具組")-1;
	mes "[毒草銷售員]";
	if (.@i < 6) {
		set .@item,7932+.@i;
		set .@price,4000;
		set .@max,2000;
		set .@max$,"2,000";
	} else if (.@i == 6) {
		set .@item,7931;
		set .@price,5000;
		set .@max,500;
		set .@max$,"500";
		mes "Each Poison Kit costs 5,000 zeny.";
	} else {
		mes "Well, I see. Come back again~!";
		close;
	}
	mes "需要多少數量呢 最多可買";
	mes ""+.@max+"個，若想取消交易";
	mes "請輸入0即可";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[毒草銷售員]";
		mes "取消交易了";
		close;
	}
	if (.@amount > .@max) {
		mes "[毒草銷售員]";
		mes "請先確認數量上限後，";
		mes "再重新輸入吧!";
		close;
	}
	set .@total, .@amount * .@price;
	if (Zeny < .@total) {
		mes "[毒草銷售員]";
		mes "你擁有的金幣不足，";
		mes "先摸摸自己的荷包吧";
		close;
	}
	set Zeny, Zeny - .@total;
	getitem .@item, .@amount;
	mes "[毒草銷售員]";
	mes "謝謝~ 歡迎再度光臨";
	close;
}
job3_guil01,79,96,3	duplicate(phs)	毒草銷售員	877
morocc,191,94,4	duplicate(phs)	毒草銷售員#moc	877	//Official: (190,96) -> in a wall
lhz_in02,16,205,4	duplicate(phs)	毒草銷售員#lhz	877

// Rune Stone Merchants (Rune Knight)
//============================================================
-	script	::runesale	-1,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- 負重量不足 -";
		close;
	}
	mes "[盧恩銷售員]";
	mes "你需要盧恩嗎? 跟我買";
	mes "就可以了! 只要告訴我";
	mes "你需要哪一種盧恩!";
	next;
	set .@i, select("購買高級盧恩原石:購買一般盧恩原石");
	mes "[盧恩銷售員]";
	if (.@i == 1) {
		mes "你要買高級的喔!那個!";
		mes "只要2,500金幣!你想買";
		set .@item,12734; //高級盧恩原石
		set .@price,2500;
	} else {
		mes "你要買高級的喔!那個!";
		mes "只要1,000金幣!你想買";
		set .@item,12737; //一般盧恩原石
		set .@price,1000;
		setarray .@rune[0],12737,1000;
	}
	mes "多少個，告訴我一聲";
	mes "就可以囉!";
	mes "最多可以買到2000個!";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[盧恩銷售員]";
		mes "不買就走開!";
		close;
	}
	if (.@amount > 2000) {
		mes "[盧恩銷售員]";
		mes "最多只能買 2,000個!";
		close;
	}
	set .@total, .@amount * .@price;
	if (Zeny < .@total) {
		mes "[盧恩銷售員]";
		mes "金幣! 金幣!";
		mes "您身上的金幣似乎不夠啊";
		close;
	}
	if (!checkweight(.@item,.@amount)) {
		mes "[盧恩銷售員]";
		mes "我不敢保證你身上還有足夠的負重量可以負荷呢..";
		close;
	}
	set Zeny, Zeny - .@total;
	getitem .@item, .@amount;
	mes "[盧恩銷售員]";
	mes "歡迎下次光臨!";
	close;
}
job3_rune01,90,62,3	duplicate(runesale)	盧恩銷售員#job3	853
prontera,168,228,3	duplicate(runesale)	盧恩銷售員#prt	853

// Rare Herb Merchants (Guillotine Cross)
//============================================================
job3_guil01,91,93,3	script	Rare Herb Collector	49,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you lose some weight. -";
		close;
	}
	if (Class == Job_Guillotine_Cross || Class == Job_Guillotine_Cross_T || Class == Job_Baby_Cross) {
		mes "[Rare Poison Herb Collector]";
		mes "I wander around the world and collect rare poison herbs. Recently, I started dealing in the herb called Izidor. If you are interested, you can buy them.";
		next;
		set .@i, select("How can I buy them?:Exchange it for Animal Blood:Exchange it for a Bitter Herb:Exchange it for a Deadly Noxious Herb:Exchange it for a Frozen Rose:Exchange it for Ment:Exchange it for Hinalle")-2;
		if (.@i == -1) {
			mes "[Rare Poison Herb Collector]";
			mes "You can buy them for 10,000 zeny and one of these rare items, Animal Blood, Bitter Herb, Deadly Noxious Herb, Frozen Rose, Ment or an Hinalle.";
			next;
			mes "[Rare Poison Herb Collector]";
			mes "Why do I ask money? That's just to pay a little respect to a collector like me. Ha ha ha...";
			close;
		} else {
			setarray .@exchange[0],702,621,631,749,605,703;
			set .@item, .@exchange[.@i];
			set .@price, 10000;

			mes "[Rare Poison Herb Collector]";
			mes "How many do you want?";
			mes "You can only buy a maximum of '2000' ea.";
			mes "Enter '0' if you want to cancel.";
			next;
			input .@amount;
			if (.@amount == 0) {
				mes "[Rare Poison Herb Collector]";
				mes "You've cancelled the trade.";
				close;
			}
			if (.@amount > 2000) {
				mes "[Rare Poison Herb Collector]";
				mes "The number must be less than 2,000!";
				close;
			}
			set .@total, .@amount * .@price;
			if (countitem(.@item) < .@amount || Zeny < .@total) {
				mes "[Rare Poison Herb Collector]";
				mes "Hey, come back when you have all the requirements for the exchange.";
				close;
			}
			mes "[Rare Poison Herb Collector]";
			mes "Good. I've received the money and the special item.";
			delitem .@item, .@amount;
			set Zeny, Zeny - .@total;
			getitem 709, .@amount; //Izidor
			close;
		}
	}
	mes "[Rare Poison Herb Collector]";
	mes "I wander around the world and collect rare poison herbs. But I don't feel like selling my herbs to a person like you... ha ha ha...";
	close;
}

// Point Merchants (Sorcerer)
//============================================================
-	script	::pss	-1,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- 負重量不足 -";
		close;
	}
	mes "[碎片商人]";
	mes "你好!";
	mes "我是銷售做為各種觸媒的^FF0000碎片^000000，你需要什麼呢?";
	next;
	set .@i, select("火靈碎片 - 200z:地靈碎片 - 200z:水靈碎片 - 200z:風靈碎片 - 200z:結束對話")-1;
	mes "[碎片商人]";
	if (.@i == 4) {
		mes "沒有需要的東西嗎?";
		close;
	}
	setarray .@itemid[0],6360,6363,6361,6362;
	setarray .@color$[0],"火靈","地靈","水靈","風靈";
	mes "你選的是"+.@color$[.@i]+"碎片，";
	mes "你要買多少呢?";
	mes "取消請輸入0。";
	next;
	input .@amount;
	mes "[碎片商人]";
	if (.@amount == 0) {
		mes "已取消!";
		close;
	}
	if (Zeny < .@amount*200) {
		mes "你的錢似乎不夠";
		close;
	}
	if (!checkweight(.@itemid[.@i],.@amount)) {
		mes "負重量不足";
		close;
	}
	set Zeny, Zeny-(.@amount*200);
	getitem .@itemid[.@i],.@amount;
	mes "謝謝光臨!";
	close;
}
gef_tower,105,172,5	duplicate(pss)	碎片商人#Sorcerer	700
prt_in,131,66,0	duplicate(pss)	碎片商人#Prontera	700
alberta,105,52,7	duplicate(pss)	碎片商人#Alberta	700
aldebaran,133,114,5	duplicate(pss)	碎片商人#Aldebaran	700
comodo,193,159,5	duplicate(pss)	碎片商人#Comodo	700
geffen,129,49,5	duplicate(pss)	碎片商人#Geffen	700
izlude,138,163,5	duplicate(pss)	碎片商人#Izlude	700	// Old coordinates: izlude (135,121)
izlude_a,138,163,5	duplicate(pss)	碎片商人#Izlude_a	700
izlude_b,138,163,5	duplicate(pss)	碎片商人#Izlude_b	700
izlude_c,138,163,5	duplicate(pss)	碎片商人#Izlude_c	700
izlude_d,138,163,5	duplicate(pss)	碎片商人#Izlude_d	700
malangdo,214,163,5	duplicate(pss)	碎片商人#Malangdo	700
mora,115,118,3	duplicate(pss)	碎片商人#Mora	700
ra_in01,256,273,3	duplicate(pss)	碎片商人#Rachel	700
veins,202,128,5	duplicate(pss)	碎片商人#Veins	700
dicastes01,207,200,5	duplicate(pss)	碎片商人#Dicastes	700
manuk,261,206,3	duplicate(pss)	碎片商人#Manuk	700
splendide,207,160,5	duplicate(pss)	碎片商人#Splendide	700
mid_camp,224,237,3	duplicate(pss)	碎片商人#Midgard	700
