//===== rAthena Script ======================================= 
//= Alchemist Shop
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Merchants for Alchemist Materials and Manuals.
//===== Additional Comments: ================================= 
//= 1.0 Split Geneticist shop from main file. [Euphy]
//=     Updated dialogue to match the official script.
//============================================================ 

alde_alche,31,186,3	script	製作書銷售員#alde	883,{
	mes "[製作書銷售員]";
	mes "你好，";
	mes "我這裡有販售基因學者的";
	mes "^0000FF特殊製作書^000000";
	next;
	mes "[製作書銷售員]";
	mes "基因學者必須擁有製作書，";
	mes "才能發動技能";
	next;
	mes "[製作書銷售員]";
	mes "擁有的書種類愈多，";
	mes "可以使用技能的種類";
	mes "當然就愈多";
	next;
	mes "[製作書銷售員]";
	mes "有這裡出售的是特殊的書，";
	mes "因此^FF0000不買其它物品^000000，";
	mes "敬請見諒!";
	next;
	mes "[製作書銷售員]";
	mes "你想看看製作書嗎?";
	next;
	if(select("是的:不要") == 2) {
		mes "[製作書銷售員]";
		mes "歡迎再度光臨，";
		mes "拜拜!";
		close;
	}
	if (checkweight(1201,1) == 0) {
		mes "[製作書銷售員]";
		mes "我想你的背包要塞不下了";
		close;
	}
	if (MaxWeight - Weight < 2500) {
		mes "[製作書銷售員]";
		mes "我想你的背包要塞不下了";
		close;
	}
	mes "[製作書銷售員]";
	mes "好的，我給你看看製作書清單";
	next;
	switch(select("[蘋果炸彈製作書] 100,000 zeny:[鳳梨炸彈製作書] 100,000 zeny:[椰子炸彈製作書] 100,000 zeny:[哈密瓜炸彈製作書] 100,000 zeny:[香蕉炸彈製作書] 100,000 zeny:[植物基因栽培法] 100,000 zeny:[高級藥水製作手冊] 100,000 zeny:[混合料理書] 100,000 zeny:[體力爆發研究書] 100,000 zeny:[活力飲料製作書] 100,000 zeny:結束購買")){
	case 1: set .@item,6279; break;
	case 2: set .@item,6280; break;
	case 3: set .@item,6281; break;
	case 4: set .@item,6282; break;
	case 5: set .@item,6283; break;
	case 6: set .@item,6284; break;
	case 7: set .@item,6285; break;
	case 8: set .@item,11022; break;
	case 9: set .@item,11023; break;
	case 10: set .@item,11024; break;
	case 11:
		mes "[製作書銷售員]";
		mes "Thank you for your patronage.";
		mes "Please come again.";
		close;
	}
	mes "[製作書銷售員]";
	mes "^0000FF["+getitemname(.@item)+"] 的價格是";
	mes "100,000 zeny.";
	mes "請告訴我您想要買幾本?";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[製作書銷售員]";
		mes "你還想看其它書嗎?";
		close;
	}
	if (.@amount < 1 || .@amount > 99 ){
		mes "[製作書銷售員]";
		mes "每次最多只能購買100本";
		close;
	}
	mes "[製作書銷售員]";
	mes "購買 "+.@amount+"本^0000FF["+getitemname(.@item)+"]^000000，";
	mes "你要繼續嗎?";
	next;
	if (select("是:不要") == 1) {
		set .@total, .@amount * 100000;
		if (Zeny < .@total) {
			mes "[製作書銷售員]";
			mes "很抱歉，但顧客您的金幣不夠。";
			close;
		}
		if (checkweight(.@item,.@amount) == 0){
			mes "[製作書銷售員]";
			mes "我想你的背包要塞不下了";
			close;
		}
		mes "[製作書銷售員]";
		mes "謝謝!";
		set Zeny,Zeny - .@total;
		getitem .@item,.@amount;
		close;
	}
	mes "[製作書銷售員]";
	mes "購買製作書的時候，應該";
	mes "要慎重一些";
	close;
}
