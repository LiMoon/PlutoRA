//===== rAthena Script ======================================= 
//= Brasilis Town
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Brasilis Town Script
//===== Additional Comments: ================================= 
//= 1.0 First version. Transportation and Basic NPCs.
//= 1.1 Fixed Zeny not being removed for payment. [Kisuka]
//= 1.2 Updated dialog, added Ice Cream Maker. [L0ne_W0lf]
//= 1.3 Moved Alberta NPC to pre-re/re paths. [Euphy]
//= 1.4 Added VIP features. [Euphy]
//============================================================ 

// Brasilis Transportation :: brasilis_trans
//============================================================
/* Pre-Renewal coordinates: alberta,247,115,3 */
alberta,246,82,3	script	布蘭妮號 船員#bra2	100,{
	mes "[布蘭妮號 船員]";
	mes "你有聽過巴西這地方嗎?";
	mes "那裡有點像沙漠都市夢羅克般炎熱，又如";
	mes "斐揚一樣是個茂密的神秘國度";
	next;
	mes "[布蘭妮號 船員]";
	mes "我們買到地圖正要駛往巴西，你只要付船費就能一起去了，";
	if (getserverdef(VAR_VIP_SCRIPT)) {
		set .@cost, vip_status(1)?1000:10000;
		mes "It's just 10,000 zeny for a round trip, and 1,000 for VIP! So do you want to go?"; //custom translation (VIP)
	} else {
		set .@cost,10000;
		mes "來回 10,000zeny! 如何?";
	}
	next;
	switch(select("去巴西!:不去")) {
	case 1:
		if (Zeny < .@cost) {
			mes "[布蘭妮號 船員]";
			mes "我說過了，船費是 "+(.@cost/1000)+",000 zeny";
			close;
		}
		else {
			mes "[布蘭妮號 船員]";
			mes "ok! 走吧";
			set Zeny, Zeny-.@cost;
			close2;
			warp "brasilis",314,60;
			end;
		}
	case 2:
		mes "[布蘭妮號 船員]";
		mes "巴西可能不吸引你吧!";
		close;
	}
}

brasilis,316,57,3	script	布蘭妮號 船員#bra1	100,{
	mes "[布蘭妮號 船員]";
	mes "我們的船要駛回艾爾貝塔，你要搭船嗎?";
	next;
	switch(select("回艾爾貝塔:放棄")) {
	case 1:
		mes "[布蘭妮號 船員]";
		mes "當然是自己的家最好!";
		close2;
		if (checkre(0))
			warp "alberta",243,82;
		else
			warp "alberta",244,115;
		end;
	case 2:
		mes "[布蘭妮號 船員]";
		mes "你還不想回去哦!";
		close;
	}
}

// Generic Brasilis NPCs :: brazil_tre
//============================================================
brasilis,155,165,3	script	Signpost#bra1	858,{
	mes ":: Art Museum ::";
	close;
}

brasilis,195,231,3	script	Signpost#bra2	858,{
	mes ":: Verass Monument ::";
	close;
}

brasilis,240,247,3	script	Signpost#bra3	858,{
	mes ":: Market ::";
	mes " ";
	mes "- For your Potions and Weaponry -";
	close;
}

brasilis,303,309,3	script	Signpost#bra4	858,{
	mes ":: Jungle Cable ::";
	mes "";
	mes "- Not for the faint of heart -";
	close;
}

brasilis,278,137,3	script	Signpost#bra5	858,{
	mes ":: Brasilis Hotel ::";
	close;
}

brasilis,137,77,5	script	Ice-Cream Maker	85,{
	mes "[Ice Cream Maker]";
	mes "Come~come~";
	mes "Ice cream is the perfect snack for a hot day~";
	mes "It's just ^3355FF100 Zeny^000000~";
	mes "Ice Cream~";
	mes "Get 'yer Ice Cream!";
	next;
	switch(select("Give me one!:Ice Cream?:Cancel.")) {
	case 1:
		mes "[Ice Cream Maker]";
		mes "Since there are so many people want to get a cool ice cream you can order only 5 at a time.";
		mes "So how many d'ya want?";
		next;
		while(1) {
			input .@input; //,1,5;
			if (.@input == 0) {
				mes "[Ice Cream Maker]";
				mes "None?";
				mes "Fine get outta the way, I have customers to serve.";
				close;
			}
			else if ((.@input < 0) || (.@input > 5)) {
				mes "[Ice Cream Maker]";
				mes "Wow.";
				mes "You ordered too much.";
				mes "If you eat over 5 you might need to fight with a monster in your stomach. Calm down buddy.";
				next;
			}
			else
				break;
		}
		set .@icecream_hap,.@input*100;
		if (Zeny < .@icecream_hap) {
			mes "[Ice Cream Maker]";
			mes "Dood~! You don't have enough money.";
			mes "It's only ^3355FF100 Zeny^000000~ Seriously!";
			close;
		}
		if (!checkweight(536,.@input)) {
			mes "[Ice Cream Maker]";
			mes "You seem to have too much stuff.";
			mes "Lighten your pack before buying this.";
			close;
		}
		set Zeny, Zeny-.@icecream_hap;
		getitem 536,.@input; //Ice_Cream
		close;
	case 2:
		mes "[Ice Cream Maker]";
		mes "'Ice cream is...";
		mes "Wait, don't you know";
		mes "what Ice Cream is?";
		mes "What rock have you";
		mes "been living under?";
		next;
		mes "[Ice Cream Maker]";
		mes "I'm not going to even start with how weird that sounds.";
		mes "Anyway, get 'yer Ice Cream right here while it's nice and cold.";
		close;
	case 3:
		mes "[Ice Cream Maker]";
		mes "Don't miss your chance to eat the greatest Ice Cream in all the land~!";
		close;
	}
}
