//===== rAthena Script ======================================= 
//= 咒術士轉職任務
//===== By: ================================================== 
//= Muad_Dib, Gepard & L0ne_W0lf
//= 中文化:Yanpo
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Job change Quest from 巫師 / 超魔導師 -> 咒術士.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Masao]
//= 1.1 Fixed some conversion mistakes, replaced numbers with
//=     constants, added item names in comments.
//= 1.2 Fixed the problem where NPC are not enabled and chamber
//=     will never open for the second try or use.
//= 1.2a Fixed a typo. [Euphy]
//= 1.3 Updated script, optimized. [Euphy]
//= 1.3a Added temporary coordinate fix #distorted_space_1-1. [Euphy]
//= 1.3b Added official coordinates. [Euphy]
//= 1.4 Added GM management function. [Euphy]
//= 1.5 Added VIP features. [Euphy]
//============================================================

-	script	#distorted_space_	-1,{
OnTouch:
	if (job_wl > 1) {
		mes " - 有股奇怪的扭曲感 - ";
		if (strnpcinfo(0) == "#distorted_space_1" && (Class == Job_Warlock || Class == Job_Warlock_T || Class == Job_Baby_Warlock)) {
			close2;
			if (countitem(6153) > 0)
				warp "spl_in02",79,102;
			end;
		}
		next;
		switch(select("伸手看看:我看危險，算了")) {
		case 1:
			mes " - 我伸出雙手，同時耳裡響起一陣共鳴、一道冷波環繞著我 - ";
			close2;
			warp "spl_in02",79,102;
			end;
		case 2:
			mes " - You ignore the distortion. - ";
			close;
		}
	}
	if (BaseLevel > 94 && (strnpcinfo(0) == "#distorted_space_2" || BaseJob == Job_Wizard)) {
		mes " - 有股奇怪的扭曲感 - ";
		next;
		switch(select("Search the area.:Ignore it.")) {
		case 1:
			mes " - There appears to be nothing here. But I can definitely feel something around here... - ";
			next;
			switch(select("再仔細觀察一下:不理會")) {
			case 1:
				mes " - I extend my arm as a fierce sound resonates in my ear and a cool wave surrounds me - ";
				close2;
				warp "spl_in02",79,102;
				end;
			case 2:
				mes " - You decide to leave it alone. - ";
				close;
			}
		case 2:
			mes " - You ignore it. - ";
			close;
		}
	}
	end;
}
//morocc,103,144,0	duplicate(#distorted_space_)	#distorted_space_1	139,1,1
morocc,95,132,0	duplicate(#distorted_space_)	#distorted_space_1	139,1,1
splendide,141,284,0	duplicate(#distorted_space_)	#distorted_space_2	139,1,1

function	script	F_Warlock	{
	mes "[助理]";
	mes "哇!!!是人類?!?!";
	mes "你是怎麼進來的呢?";
	mes "這裡只能透過特殊的門才能進來!";
	next;
	mes "[助理]";
	mes "我是不清楚，";
	mes "不過被大師發現之前趕快離開吧!";
	next;
	mes "[???]";
	mes "發生什麼事了嗎?";
	mes "我講過很多次了，當我在冥想的時候要保持安靜啊";
	next;
	mes "[助理]";
	mes "啊!!";
	mes "大..大師...";
	mes "沒事..沒事";
	next;
	mes "[大師]";
	mes "嗯嗯~怎麼會有人類在這裡呢?";
	next;
	mes "[助理]";
	mes "抱歉!";
	mes "我會馬上叫他離開";
	next;
	mes "[大師]";
	mes "儒!!!";
	next;
	mes "[儒]";
	mes "是...!!";
	next;
	mes "[大師]";
	mes "你身後似乎還有尚未關閉的傳送口，難道是我的錯覺嗎?";
	next;
	mes "[儒]";
	mes "對..對不起!!!";
	mes "都是我的錯...";
	next;
	mes "[大師]";
	mes "真是糊塗...";
	mes "等把人類趕走之後在處罰你好了";
	next;
	mes "[大師]";
	mes "MushSartas Ir Cyamar";
	mes "UdenLo";
	if (BaseJob == Job_Wizard) {
		next;
		mes " - 當那位所謂的大師 - ";
		mes " - 擁有綠髮的精靈 - ";
		mes " - 唸完咒語後， - ";
		mes " - 至今從未感受過的 - ";
		mes " - 強烈魔力環繞著全身 - ";
		specialeffect2 EF_DISPELL;
		next;
		mes "[大師]";
		mes "呵，真有趣，";
		mes "身為人類竟能抵抗我的魔法";
		next;
		mes "[大師]";
		mes "嗯嗯...";
		mes "仔細觀察還蠻有趣的人類，";
		mes "由於被肉體束縛而無法解放自己真正的力量";
		next;
		if (BaseLevel > 98 && JobLevel > 49) {
			select("你怎麼會知道呢?");
			mes "[大師]";
			mes "我是^000077咒術士^000000的領袖，";
			mes "叫做'^000077西拉 艾格諾羅里亞^000000'，";
			mes "凡是會用魔法的人，至少都有聽過我的名字";
			next;
			select("何謂^000077咒術士^000000?");
			mes "[大師 艾格諾羅里亞]";
			mes "呵呵.. 有趣的人類哦!";
			next;
			mes "[大師 艾格諾羅里亞]";
			mes "簡單地說，^000077咒術士^000000是指使用居住在優托海音的巨人們的魔力的人。";
			mes "這也不是三言兩語就可以解釋得完的";
			next;
			mes "[大師 艾格諾羅里亞]";
			mes "只有強烈的魔法能代表它的存在，";
			mes "呵呵呵";
			next;
			select("我也能成為咒術士嗎?");
			mes "[大師 艾格諾羅里亞]";
			mes "你嗎?";
			mes "呵呵呵呵呵呵，";
			mes "身為人類的你也想成為咒術士嗎?";
			mes "哈哈哈";
			next;
			mes "[大師 艾格諾羅里亞]";
			mes "你真的很有意思，";
			mes "人類要成為咒術士是有難度的，";
			mes "強大的力量可能會摧毀你羸弱的身體";
			next;
			mes "[大師 艾格諾羅里亞]";
			mes "若抱著必死之心，也許值得一試，";
			mes "呵呵";
			next;
			switch(select("請告訴我成為咒術士的方法:取消")) {
			case 1:
				if (getserverdef(VAR_VIP_SCRIPT)) {
					callfunc "VIP_Third_Class";
					set job_wl,2;
					setquest 11106;
					next;
				}
				mes "[大師 艾格諾羅里亞]";
				mes "你是認真的嗎?";
				mes "You hunger for power don't you human.";
				next;
				mes "[大師 艾格諾羅里亞]";
				mes "想要變強的強烈欲望讓你的靈魂看起來更加美麗呢";
				next;
				mes "[大師 艾格諾羅里亞]";
				mes "不過我要先確認你是否符合資格呢";
				next;
				mes "[大師 艾格諾羅里亞]";
				mes "儒!";
				next;
				mes "[儒]";
				mes "是!!";
				next;
				mes "[大師 艾格諾羅里亞]";
				mes "把'那個東西'拿給這個人類吧";
				next;
				mes "[大師 艾格諾羅里亞]";
				mes "人類，去找那邊那個橘子頭吧!";
				mes "他會告訴你該做什麼";
				set job_wl,2;
				setquest 11106;
				close;
			case 2:
				mes "[大師 艾格諾羅里亞]";
				mes "很好，";
				mes "果真是人類啊";
				set job_wl,1;
				close2;
				warp "splendide",200,100;
				end;
			}
		}
		mes "[大師]";
		mes "You're still quite far too low of a level to even consider this.";
	}
	close2;
	warp "splendide",200,100;
	end;
}

spl_in02,77,107,5	script	助理#Warlock1	446,{
	if (job_wl == 0) {
		callfunc "F_Warlock";
		end;
	} else if (job_wl == 1) {
		mes "[儒]";
		mes "You're... we've met before.";
		mes "Can you imagine how severely I was scolded by my master?!";
		mes "If you have any business with me, just go away!";
		next;
		switch(select("Leave.:Stay.")) {
		case 1:
			mes "[儒]";
			mes "Leave me alone...!";
			close2;
			warp "splendide",200,100;
			end;
		case 2:
			mes "[儒]";
			mes "What?!";
			mes "Just do whatever you want...";
			mes "My master will deal with you then.";
			close;
		}
	} else if (job_wl == 2 || job_wl == 3) {
		mes "[儒]";
		if (job_wl == 2) {
			mes "Dear, master! My pony-tail is funny looking?!";
			mes "Oh, but I spent a lot time on it!";
			next;
			mes "[大師 艾格諾羅里亞]";
			mes "It's noisy.";
			mes "I need to meditate";
			mes "so do as I said.";
			next;
			mes "[儒]";
			mes "Huh..";
			mes "Oh, yeah!";
			next;
			mes "[儒]";
			mes "Hey, human!";
			mes "Would you like to";
			mes "become a 咒術士?";
			next;
		}
		if (SkillPoint != 0) {
			mes "First, use all of your remaining job skill points before continuing.";
			set job_wl,3;
			close;
		}
		mes "This is the Master's";
		mes "decision so let me know.";
		next;
		mes "[儒]";
		mes "We need to test if your body can withstand being a 咒術士.";
		mes "So you need to gather materials to prove yourself.";
		next;
		mes "[儒]";
		mes "到外面收集^000077翡翠水晶^000000和^000077大鈽鐳碎片^000000各1個回來吧!";
		mes "從中邪的怪物身上即可取得";
		next;
		callsub L_Quest,1;
		close;
	} else if (job_wl == 4) {
		if (countitem(6152) == 0 || countitem(6151) == 0)
			callsub L_Quest,0;
		else {
			mes "[儒]";
			mes "I can't conduct the task here. I need to go to my lab.";
			mes "My lab is located on the north western part of Splendide.";
		}
		close;
	} else if (job_wl == 5) {
		mes "[儒]";
		mes "This stone is well made.";
		mes "Please go to my Master.";
		close;
	} else {
		mes "[儒]";
		mes "A Magic Chamber is created from magic materials.";
		next;
		mes "[儒]";
		mes "This combined stone is formed differently according to the raw materials and the way of treating it.";
		next;
		mes "[儒]";
		mes "But most of stones are quite similar!";
		mes "We can't differentiate them easily.";
		next;
		mes "[儒]";
		mes "Even I don't know how it all works.";
		close;
	}

L_Quest:
	mes "[儒]";
	mes "You must be wearing these Magical Stone Rings to get these items while hunting.";
	mes "You need to hunt Naga and Bradium Golems while wearing these rings.";
	next;
	if (getarg(0)) {
		mes "[儒]";
		mes "Show us how strong you are. And don't forget to wear this.";
		set job_wl,4;
		getitem 2796,1; //Magical_Stone
		getitem 2797,1; //Magical_Stone_
		changequest 11106,11107;
		next;
	}
	mes "[儒]";
	mes "Bring these stones with you or else you won't be able to return!";
	mes "Be careful!";
	next;
	mes "[儒]";
	mes "The person next to my Master will let you out if you talk to him.";
	next;
	mes "[儒]";
	mes "我的研究室位於史波浪壯麗前線基地西北的末端";
	return;
}

spl_in02,60,231,5	script	儒#咒術士	446,{
	mes "[儒]";
	if (job_wl < 4) {
		mes "Who are you?";
		mes "Get out of here!";
		close;
	} else if (job_wl == 4) {
		mes "你回來啦!";
		next;
		switch(select("材料帶來了:遺失魔法石了:沒事")) {
		case 1:
			mes "[儒]";
			if (countitem(6152) == 0 || countitem(6151) == 0) {
				mes "......";
				mes "......Where?";
				mes "Don't bother me!";
				next;
				mes "[儒]";
				mes "We need to test if your body can withstand being a 咒術士.";
				mes "So you need to gather materials to prove yourself.";
				next;
				mes "[儒]";
				mes "Please gather ^000077Glittering Crystal^000000s and ^000077Giant Bradium Fragment^000000s.";
				next;
				mes "[儒]";
				mes "You must be wearing these Magical Stone Rings to get these items while hunting.";
				mes "You need to hunt Naga and Bradium Golems while wearing these rings.";
				next;
				mes "[儒]";
				mes "Bring these stones with you or else you won't be able to return!";
				mes "Be careful!";
				close;
			} else if (countitem(6152) && countitem(6151)) {
				if (isequipped(2796) || isequipped(2797)) {
					mes "Bring me back the magical stones.";
					close;
				}
				if (countitem(2796) == 0 || countitem(2797) == 0) {
					mes "It seems that you don't have it?";
					mes "You lost it?";
					next;
					mes "[儒]";
					mes "Please find that stone and come back to me.";
					close;
				}
				mes "很好!材料似乎備齊了!";
				mes "那我在製作魔力石期間，你先去找大師吧! 等我做好隨後就回去";
				delitem 2796,countitem(2796); //Magical_Stone
				delitem 2797,countitem(2797); //Magical_Stone_
				delitem 6152,countitem(6152); //Glittering_Crystal
				delitem 6151,countitem(6151); //Peice_Of_Great_Bradium
				set job_wl,5;
				changequest 11107,11108;
				close;
			}
		case 2:
			mes "[儒]";
			mes "What?!";
			mes "I asked you to double check it and make sure you wore it!";
			next;
			mes "[儒]";
			mes "Please check whether you misplaced it or if you already have it equipped.";
			next;
			switch(select("I found it.:Nah.")) {
			case 1:
				mes "[儒]";
				mes "Don't tease me!";
				mes "It's not hard to tell if you have it or not!";
				close;
			case 2:
				if ((isequipped(2796) == 0 && countitem(2796) == 0) || (isequipped(2797) == 0 && countitem(2797) == 0)) {
					if (countitem(732) > 0) {
						switch(select("Give 3 carat diamond.:Just quit it.")) {
						case 1:
							mes "[儒]";
							mes "I will overlook your fault.";
							mes "Please be careful with it.";
							delitem 732,1; //Crystal_Jewel__
							if (isequipped(2796) == 0 && countitem(2796) == 0)
								getitem 2796,1; //Magical_Stone
							if (isequipped(2797) == 0 && countitem(2797) == 0)
								getitem 2797,1; //Magical_Stone_
							close;
						case 2:
							mes "[儒]";
							mes "Just show me your good faith!";
							close;
						}
					}
					mes "[儒]";
					mes "How can you can say that?";
					mes "^000077Just apologize^000000!";
					mes "I guess I could give you more stones if you give me say a 3 carat diamond...";
					close;
				}
				mes "[儒]";
				if (isequipped(2796) || isequipped(2797)) {
					mes "Please check your equipment window...";
					mes "You sure you don't have it?";
					close;
				}
				mes "Then what's that you are holding now...?";
				close;
			}
		case 3:
			mes "[儒]";
			mes "What...?";
			mes "Don't bother me, I am quite busy.";
			close;
		}
	} else {
		mes "I am focusing on meditating.";
		mes "So I can create magic.";
		next;
		mes "[儒]";
		mes "And... you seem to have more talent on this than regular mages.";
		mes "Maybe if I become an alchemist, I might be better than I am now don't you think?";
		next;
		mes "[儒]";
		mes "I'm studying to better myself.";
		next;
		mes "[儒]";
		mes "If am inside of 'the room of time and magic', I will catch up with your power!";
		mes "Hahahaha...";
		close;
	}
}

spl_in02,84,103,3	script	助理#Warlock2	439,{
	if (job_wl == 0) {
		callfunc "F_Warlock";
		end;
	} else if (job_wl == 1) {
		mes "[助理]";
		mes "Human! What do you want?";
		close;
	} else if (job_wl < 6) {
		mes "[助理]";
		mes "It's not me you are looking for currently.";
		close;
	} else if (job_wl == 6) {
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 3500) {
			mes "You're carring out too many items.";
			mes "You'd better minimize your weight.";
			close;
		}
		mes "[希姿]";
		mes "The test isn't that hard.";
		mes "We can create the magic stone in the magic room.";
		next;
		mes "[希姿]";
		mes "Be careful of the monsters in the test room.";
		mes "These are rainbow Rubies sealed with a 咒術士's magic.";
		mes "Use these during the test.";
		next;
		mes "[希姿]";
		mes "I will guide you into the magic room.";
		set job_wl,7;
		changequest 11109,11110;
		getitem 12384,5; //Rainbow_Ruby_Water
		getitem 12386,5; //Rainbow_Ruby_Wind
		getitem 12387,5; //Rainbow_Ruby_Earth
		getitem 12385,5; //Rainbow_Ruby_Fire
		close2;
		warp "job3_war01",23,20;
		end;
	} else if (job_wl == 7) {
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 3500) {
			mes "You're carring out too many items.";
			mes "You'd better minimize your weight.";
			close;
		}
		mes "[希姿]";
		mes "Would you like to try it again?";
		mes "Let me give you more rubies.";
		next;
		if (countitem(12384)) delitem 12384,countitem(12384); //Rainbow_Ruby_Water
		if (countitem(12386)) delitem 12386,countitem(12386); //Rainbow_Ruby_Wind
		if (countitem(12387)) delitem 12387,countitem(12387); //Rainbow_Ruby_Earth
		if (countitem(12385)) delitem 12385,countitem(12385); //Rainbow_Ruby_Fire
		getitem 12384,5; //Rainbow_Ruby_Water
		getitem 12386,5; //Rainbow_Ruby_Wind
		getitem 12387,5; //Rainbow_Ruby_Earth
		getitem 12385,5; //Rainbow_Ruby_Fire
		close2;
		warp "job3_war01",23,20;
		end;
	} else if (job_wl == 8) {
		mes "[希姿]";
		mes "沒想到你成功了，真厲害!";
		mes "我曾懷疑你的能力，抱歉!";
		if (countitem(12384)) delitem 12384,countitem(12384); //Rainbow_Ruby_Water
		if (countitem(12386)) delitem 12386,countitem(12386); //Rainbow_Ruby_Wind
		if (countitem(12387)) delitem 12387,countitem(12387); //Rainbow_Ruby_Earth
		if (countitem(12385)) delitem 12385,countitem(12385); //Rainbow_Ruby_Fire
		next;
		mes "[希姿]";
		mes "現在所有測驗都結束了，去找大師吧!";
		mes "很開心有你這樣優秀的夥伴";
		set job_wl,9;
		changequest 11111,11112;
		close;
	} else if (job_wl == 9) {
		mes "[希姿]";
		mes "All of the tests are done. Now talk to Master Engoloria.";
		mes "I'm so happy that you have succeeded.";
		close;
	} else {
		mes "[希姿]";
		mes "如今可以感受到強烈的魔力，";
		mes "有點羨慕你呢? 呵呵";
		next;
		mes "[希姿]";
		mes "擁有強烈的魔力當然要付出代價，";
		mes "請你要用在有意義的地方";
		close;
	}
}

spl_in02,78,110,5	script	助理#Warlock3	437,{
	if (job_wl == 0) {
		callfunc "F_Warlock";
		end;
	}
	mes "[助理]";
	mes "......";
	switch(job_wl) {
		case 1: mes "請你離開好嗎?"; break;
		case 2: mes "我可不是橘子頭啊..."; break;
		default: mes "大師正在冥想，請保持安靜"; break;
	}
	next;
	switch(select("離開:取消")) {
	case 1:
		mes "[助理]";
		mes "再見!";
		close2;
		warp "splendide",200,100;
		end;
	case 2:
		mes "[助理]";
		mes "......";
		close;
	}
}

spl_in02,80,108,5	script	公會會長#咒術士	465,{
	if (job_wl == 0) {
		callfunc "F_Warlock";
		end;
	} else if (job_wl == 1) {
		mes "[大師 艾格諾羅里亞]";
		mes "Do you have something to ask me?";
		next;
		switch(select("How can I be a 咒術士?:Cancel.")) {
		case 1:
			if (getserverdef(VAR_VIP_SCRIPT)) {
				callfunc "VIP_Third_Class";
				set job_wl,2;
				setquest 11106;
				next;
			}
			mes "[大師 艾格諾羅里亞]";
			mes "Are you serious?";
			mes "You hunger for power don't you human.";
			next;
			mes "[大師 艾格諾羅里亞]";
			mes "The strong desire to be more powerful makes your soul more beautiful.";
			next;
			mes "[大師 艾格諾羅里亞]";
			mes "But, I need to check that you're qualified.";
			next;
			mes "[大師 艾格諾羅里亞]";
			mes "儒!";
			next;
			mes "[儒]";
			mes "Yeap!!";
			next;
			mes "[大師 艾格諾羅里亞]";
			mes "Give this to the human.";
			next;
			mes "[大師 艾格諾羅里亞]";
			mes "Talk to the guy with the weird hair.";
			mes "He'll let you know what you should do.";
			set job_wl,2;
			setquest 11106;
			close;
		case 2:
			mes "[大師 艾格諾羅里亞]";
			mes "Good.";
			mes "You are a human being so that's for you.";
			close2;
			warp "splendide",200,100;
			end;
		}
	} else if (job_wl == 2) {
		mes "[大師 艾格諾羅里亞]";
		mes "I'm meditating, I said talk to Yoop ok.";
		mes "Yoop has a funny pony-tail.";
		mes "And don't disturb me.";
		close;
	} else if (job_wl == 3 || job_wl == 4) {
		mes "[大師 艾格諾羅里亞]";
		mes "......";
		next;
		mes "[助理]";
		mes "Master is meditating.";
		mes "Be quiet.";
		close;
	} else if (job_wl == 5) {
		mes "[大師 艾格諾羅里亞]";
		mes "I've heard from Yoop that a very useful stone can be created.";
		next;
		mes "[大師 艾格諾羅里亞]";
		mes "It can be used to make magical jewelry that can cast powerful magic.";
		next;
		mes "[大師 艾格諾羅里亞]";
		mes "Yoop.";
		next;
		mes "[儒]";
		mes "Yeap, Master!";
		next;
		mes "[大師 艾格諾羅里亞]";
		mes "Please take him to the magical room.";
		next;
		mes "[儒]";
		mes "Yup, Master.";
		mes "Please follow me.";
		emotion e_ic,0,"助理#Warlock2";
		set job_wl,6;
		changequest 11108,11109;
		close;
	} else if (job_wl < 9) {
		mes "[大師 艾格諾羅里亞]";
		mes "......";
		next;
		mes "[儒]";
		mes "My master is meditating now, please follow me.";
		close;
	} else if (job_wl == 9) {
		if (BaseLevel < 99 || BaseJob != Job_Wizard) {
			mes "[大師 艾格諾羅里亞]";
			mes "Hm...?";
			mes "How did you come this far...";
			close;
		}
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 3500) {
			mes "You've got too many items.";
			mes "Please come back after reducing the number of items you have.";
			close;
		}
		mes "[大師 艾格諾羅里亞]";
		if (SkillPoint != 0) {
			mes "Unless you like your current job, you should use all your skill points.";
			close;
		}
		if (ismounting()) {
			mes "You are on a riding pet,";
			mes "so you cannot change your job.";
			mes "Please unequip your riding pet and try again!";
			close;
		}
		mes "好漂亮的光...";
		mes "你果然沒讓我失望...";
		next;
		mes "[大師 艾格諾羅里亞]";
		mes "恭喜你，你正式成為咒術士的一員了，";
		mes "請你時時刻刻要記住身為咒術士的責任和信念";
		next;
		if (SkillPoint != 0) {
			mes "^0000ffYou still have left the skill points, Please use all the skill points and try to start again!^000000";
			close;
		}
		mes "[大師 艾格諾羅里亞]";
		mes "你製作的那個魔法石會守護你的";
		if (Class == Job_High_Wizard) {
			if (JobLevel > 69) set job_wl,13;
			else if (JobLevel > 59) set job_wl,12;
			else set job_wl,11;
		}
		else set job_wl,10;
		jobchange roclass(eaclass()|EAJL_THIRD);
		completequest 11112;
		getitem 5753,1; //Magic_Stone_Hat
		getitem 2795,1; //Green_Apple_Ring
		close;
	} else if (job_wl > 9) {
		mes "[大師 艾格諾羅里亞]";
		mes "咒術士的力量感覺如何?";
		mes "不可志得意滿，但要持續修行";
		next;
		mes "[大師 艾格諾羅里亞]";
		mes "最後搞不好變成那橘子頭一樣，";
		mes "呵呵呵";
		close;
	}
}

job3_war01,22,22,7	script	儒#WRR	439,{
	mes "[儒]";
	mes "If you ask 希姿 to be sent to the magical room, he would send you there.";
	mes "Wait in the queue to enter the 魔力房.";
	close;
}

job3_war01,29,25,0	script	希姿#WRR	437,{
	mes "[希姿]";
	mes "Would you like to take a test?";
	mes "If you stand by in the waiting room, you'll be able to go into the 魔力房.";
	next;
	mes "[希姿]";
	mes "If there're others inside the room, you just need to wait for a while.";
	next;
	switch(select("Go to the waiting room.:Go outside.")) {
	case 1:
		mes "[希姿]";
		mes "Then please enter the room yourself.";
		mes "Make sure you have enough potions to survive the test as the monsters are very powerful in the Chamber.";
		close;
	case 2:
		mes "[希姿]";
		mes "......";
		mes "Then I will send you outside.";
		mes "Bye...";
		close2;
		warp "spl_in02",79,102;
		end;
	}
}

job3_war01,29,25,5	script	魔力房#1	437,{
	end;
OnInit:
	disablenpc "魔力房#1";
	waitingroom "魔力房",20,"魔力房#1::OnStartArena",1;
	enablewaitingroomevent;
	end;
OnStartArena:
	warpwaitingpc "job3_war02",29,25;
	donpcevent "魔力房#2::OnEnable";
	initnpctimer;
	disablewaitingroomevent;
	end;
OnTimer10000:
	if (getmapusers("job3_war02") == 0) {
		donpcevent "魔力房#2::OnReset";
		donpcevent "魔力房#2::OnDisable";
		stopnpctimer "魔力房#2";
		donpcevent "魔力房#3::OnReset";
		donpcevent "魔力房#3::OnDisable";
		stopnpctimer "魔力房#3";
		donpcevent "魔力房#4::OnReset";
		donpcevent "魔力房#4::OnDisable";
		stopnpctimer "魔力房#4";
		donpcevent "虛石#咒術士::OnReset";
		donpcevent "虛石#咒術士::OnDisable";
		stopnpctimer "虛石#咒術士";
		stopnpctimer;
		enablewaitingroomevent;
		end;
	}
	initnpctimer;
	end;
OnEnable:
	enablewaitingroomevent;
	end;
OnDisable:
	disablewaitingroomevent;
	end;
}

-	script	魔力房#2	-1,{
OnInit:
	disablenpc strnpcinfo(0);
	end;
OnEnable:
	enablenpc strnpcinfo(0);
	monster "job3_war02",23,32,"中邪的怪物",2047,1,strnpcinfo(0)+"::OnMyMobDead";
	monster "job3_war02",23,21,"中邪的怪物",2048,1,strnpcinfo(0)+"::OnMyMobDead";
	monster "job3_war02",34,32,"中邪的怪物",2049,1,strnpcinfo(0)+"::OnMyMobDead";
	monster "job3_war02",34,21,"中邪的怪物",2050,1,strnpcinfo(0)+"::OnMyMobDead";
	monster "job3_war02",23,32,"中邪的怪物",2047,1,strnpcinfo(0)+"::OnMyMobDead";
	monster "job3_war02",23,21,"中邪的怪物",2048,1,strnpcinfo(0)+"::OnMyMobDead";
	monster "job3_war02",34,32,"中邪的怪物",2049,1,strnpcinfo(0)+"::OnMyMobDead";
	monster "job3_war02",34,21,"中邪的怪物",2050,1,strnpcinfo(0)+"::OnMyMobDead";
	initnpctimer;
	end;
OnReset:
	killmonster "job3_war02",strnpcinfo(0)+"::OnMyMobDead";
	end;
OnDisable:
	disablenpc strnpcinfo(0);
	end;
OnMyMobDead:
	if (mobcount("job3_war02",strnpcinfo(0)+"::OnMyMobDead") < 1) {
		stopnpctimer;
		mapannounce "job3_war02","怪物的侵入被阻止了",bc_map;
		set .@str$, "魔力房#"+((strnpcinfo(0) == "魔力房#2")?"3":"4");
		donpcevent .@str$+"::OnEnable";
	}
	end;
OnTimer3000:
	if (strnpcinfo(0) != "魔力房#2") end;
	mapannounce "job3_war02","A Magical Power fragment has been created in the Magic Chamber Room.",bc_map;
	end;
OnTimer4000:
	mapannounce "job3_war02","5分鐘內必須從覬覦魔力碎片的怪物中保護魔力房",bc_map;
	end;
OnTimer5000:
	mapannounce "job3_war02","當解決所有的怪物時，魔力房就進入穩定化狀態",bc_map;
	end;
OnTimer63000:
	mapannounce "job3_war02","距離穩定化還剩4分鐘...",bc_map;
	end;
OnTimer123000:
	mapannounce "job3_war02","距離穩定化還剩3分鐘...",bc_map;
	end;
OnTimer183000:
	mapannounce "job3_war02","距離穩定化還剩2分鐘...",bc_map;
	end;
OnTimer243000:
	mapannounce "job3_war02","距離穩定化還剩1分鐘...",bc_map;
	end;
OnTimer273000:
	mapannounce "job3_war02","距離穩定化還剩30秒...",bc_map;
	end;
OnTimer283000:
	mapannounce "job3_war02","距離穩定化還剩20秒...",bc_map;
	end;
OnTimer293000:
	mapannounce "job3_war02","距離穩定化還剩10秒...",bc_map;
	end;
OnTimer303000:
	mapannounce "job3_war02","魔力穩定化失敗",bc_map;
	donpcevent "魔力房#2::OnReset";
	donpcevent "魔力房#3::OnReset";
	donpcevent "魔力房#4::OnReset";
	end;
OnTimer304000:
	mapwarp "job3_war02","spl_in02",79,102;
	end;
OnTimer305000:
	donpcevent "魔力房#2::OnDisable";
	donpcevent "魔力房#3::OnDisable";
	donpcevent "魔力房#4::OnDisable";
	donpcevent "魔力房#1::OnEnable";
	stopnpctimer;
	end;
}
-	duplicate(魔力房#2)	魔力房#3	-1

-	script	魔力房#4	-1,{
OnInit:
	disablenpc "魔力房#4";
	end;
OnEnable:
	enablenpc "魔力房#4";
	monster "job3_war02",23,32,"中邪的怪物",1002,1,"魔力房#4::OnMyMobDead";
	monster "job3_war02",23,21,"中邪的怪物",1002,1,"魔力房#4::OnMyMobDead";
	monster "job3_war02",34,32,"中邪的怪物",1002,1,"魔力房#4::OnMyMobDead";
	monster "job3_war02",34,21,"中邪的怪物",1002,1,"魔力房#4::OnMyMobDead";
	monster "job3_war02",23,32,"中邪的怪物",1002,1,"魔力房#4::OnMyMobDead";
	monster "job3_war02",23,21,"中邪的怪物",1002,1,"魔力房#4::OnMyMobDead";
	monster "job3_war02",34,32,"中邪的怪物",1002,1,"魔力房#4::OnMyMobDead";
	monster "job3_war02",34,21,"中邪的怪物",1002,1,"魔力房#4::OnMyMobDead";
	initnpctimer;
	end;
OnReset:
	killmonster "job3_war02","魔力房#4::OnMyMobDead";
	end;
OnDisable:
	disablenpc "魔力房#4";
	end;
OnMyMobDead:
	if (mobcount("job3_war02","魔力房#4::OnMyMobDead") < 1) {
		mapannounce "job3_war02","當魔力房穩定化成功後，原本散開的魔力碎片逐漸往中央聚集起來",bc_map;
		stopnpctimer;
		donpcevent "虛石#咒術士::OnEnable";
	}
	end;
OnTimer4000:
	mapannounce "job3_war02","保護魔力房免於怪物的襲擊持續1分鐘",bc_map;
	end;
OnTimer5000:
	mapannounce "job3_war02","當解決所有的怪物時，魔力房就會進入穩定狀態",bc_map;
	end;
OnTimer33000:
	mapannounce "job3_war02","距離穩定化還剩30秒...",bc_map;
	end;
OnTimer43000:
	mapannounce "job3_war02","距離穩定化還剩20秒...",bc_map;
	end;
OnTimer53000:
	mapannounce "job3_war02","距離穩定化還剩10秒...",bc_map;
	end;
OnTimer58000:
	mapannounce "job3_war02","距離穩定化還剩5秒...",bc_map;
	end;
OnTimer63000:
	mapannounce "job3_war02","魔力穩定化失敗，碎片已失去魔力",bc_map;
	donpcevent "魔力房#2::OnReset";
	donpcevent "魔力房#3::OnReset";
	donpcevent "魔力房#4::OnReset";
	end;
OnTimer64000:
	mapwarp "job3_war02","spl_in02",79,102;
	end;
OnTimer65000:
	donpcevent "魔力房#2::OnDisable";
	donpcevent "魔力房#3::OnDisable";
	donpcevent "魔力房#4::OnDisable";
	donpcevent "魔力房#1::OnEnable";
	stopnpctimer;
	end;
}

-	script	虛石#咒術士	-1,{
OnInit:
	disablenpc "虛石#咒術士";
	end;
OnEnable:
	enablenpc "虛石#咒術士";
	donpcevent "魔力房#2::OnReset";
	donpcevent "魔力房#2::OnDisable";
	donpcevent "魔力房#3::OnReset";
	donpcevent "魔力房#3::OnDisable";
	donpcevent "魔力房#4::OnReset";
	donpcevent "魔力房#4::OnDisable";
	monster "job3_war02",29,27,"虛石",2035,1,"虛石#咒術士::OnMyMobDead";
	initnpctimer;
	end;
OnReset:
	killmonster "job3_war02","虛石#咒術士::OnMyMobDead";
	end;
OnDisable:
	disablenpc "虛石#咒術士";
	end;
OnMyMobDead:
	if (mobcount("job3_war02","虛石#咒術士::OnMyMobDead") < 1) {
		mapannounce "job3_war02","虛石上注入了充足的魔力後形成了完整的結晶",bc_map;
		set job_wl,8;
		changequest 11110,11111;
		donpcevent "虛石#咒術士::OnReset";
		donpcevent "虛石#咒術士::OnDisable";
		stopnpctimer;
		donpcevent "希姿#E1::OnEnable";
		donpcevent "希姿#E2::OnEnable";
	}
	end;
OnTimer1000:
	mapannounce "job3_war02","當魔力碎片聚集後形成了虛石",bc_map;
	end;
OnTimer2000:
	mapannounce "job3_war02","虛石擁有吸收魔力的性質",bc_map;
	end;
OnTimer3000:
	mapannounce "job3_war02","必須在虛石上注入魔力後10分鐘內要完全結晶化",bc_map;
	end;
OnTimer303000:
	mapannounce "job3_war02","距離虛石結晶化還剩5分鐘...",bc_map;
	end;
OnTimer363000:
	mapannounce "job3_war02","距離虛石結晶化還剩4分鐘...",bc_map;
	end;
OnTimer423000:
	mapannounce "job3_war02","距離虛石結晶化還剩3分鐘...",bc_map;
	end;
OnTimer483000:
	mapannounce "job3_war02","距離虛石結晶化還剩2分鐘...",bc_map;
	end;
OnTimer543000:
	mapannounce "job3_war02","距離虛石結晶化還剩1分鐘...",bc_map;
	end;
OnTimer573000:
	mapannounce "job3_war02","距離虛石結晶化還剩30秒...",bc_map;
	end;
OnTimer583000:
	mapannounce "job3_war02","距離虛石結晶化還剩20秒...",bc_map;
	end;
OnTimer593000:
	mapannounce "job3_war02","距離虛石結晶化還剩10秒...",bc_map;
	end;
OnTimer603000:
	mapannounce "job3_war02","虛石結晶化失敗",bc_map;
	donpcevent "虛石#咒術士::OnReset";
	end;
OnTimer605000:
	mapwarp "job3_war02","spl_in02",79,102;
	end;
OnTimer607000:
	donpcevent "虛石#咒術士::OnDisable";
	stopnpctimer;
	donpcevent "魔力房#1::OnEnable";
	end;
}

-	script	希姿#E1	-1,{
OnInit:
	disablenpc "希姿#E1";
	end;
OnEnable:
	initnpctimer;
	end;
OnDisable:
	disablenpc "希姿#E1";
	end;
OnTimer2000:
	mapannounce "job3_war01","恭喜! 你已成功把虛石結晶化了",bc_map;
	end;
OnTimer3000:
	mapannounce "job3_war01","魔力房逐漸被新魔力碎片堆積了",bc_map;
	end;
OnTimer6000:
	donpcevent "希姿#E1::OnDisable";
	stopnpctimer;
	donpcevent "魔力房#1::OnEnable";
	end;
}

-	script	希姿#E2	-1,{
OnInit:
	disablenpc "希姿#E2";
	end;
OnEnable:
	initnpctimer;
	end;
OnDisable:
	disablenpc "希姿#E2";
	end;
OnTimer3000:
	mapannounce "job3_war02","恭喜! 你已成功把虛石結晶化了",bc_map;
	end;
OnTimer5000:
	mapannounce "job3_war02","魔力房逐漸被新魔力碎片堆積了",bc_map;
	mapwarp "job3_war02","spl_in02",79,102;
	end;
OnTimer6000:
	donpcevent "希姿#E2::OnDisable";
	stopnpctimer;
	end;
}

job3_war01,1,2,0	script	Button Girl#wl	66,{
	callfunc "F_GM_NPC";
	switch(select("Open Arena:Close Arena:開啟魔力房:關閉魔力房:虛石 On:虛石 Off:取消")) {
	case 1:
		mes "Opening the Arena.";
		donpcevent "魔力房#1::OnEnable";
		close;
	case 2:
		mes "Closing the Arena.";
		donpcevent "魔力房#1::OnDisable";
		close;
	case 3:
		mes "Opening the 魔力房.";
		donpcevent "魔力房#2::OnEnable";
		close;
	case 4:
		mes "Closing the 魔力房.";
		donpcevent "魔力房#2::OnReset";
		donpcevent "魔力房#2::OnDisable";
		donpcevent "魔力房#3::OnReset";
		donpcevent "魔力房#3::OnDisable";
		donpcevent "魔力房#4::OnReset";
		donpcevent "魔力房#4::OnDisable";
		close;
	case 5:
		mes "Enabing 虛石.";
		donpcevent "虛石#咒術士::OnEnable";
		close;
	case 6:
		mes "Disabling 虛石.";
		donpcevent "虛石#咒術士::OnReset";
		donpcevent "虛石#咒術士::OnDisable";
		close;
	case 7:
		close;
	}
}

spl_fild02,0,0,0,0	monster	中邪的鈽鐳巨人	2049,20,5000,0,0
spl_fild02,0,0,0,0	monster	中邪的那迦	2047,20,5000,0,0