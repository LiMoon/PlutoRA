//===== rAthena Script ======================================= 
//= Genetic Job Quest
//===== By: ================================================== 
//= Muad_Dib, Aeomin
//===== 中文化 ================================================
//= 冥王RA-PlutoRA
//= XD人
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job change Quest from Alchemist / Creator -> Genetic.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Masao]
//= 1.1 Fixed Scatter Stuff NPCs [JayPee].
//= 1.2 Updated dialogue. Credits to Muad_Dib. [Joseph]
//= 1.3 Updated script, optimized. [Euphy]
//= 1.3a Moved warps to separate file. [Euphy]
//= 1.4 Added VIP features. [Euphy]
//============================================================

alde_alche,35,186,5	script	鍊金術師公會人員	805,{
	mes "[鍊金術師公會人員]";
	if (BaseLevel > 98 && JobLevel > 49){
		if (BaseJob == Job_Alchemist) {
			if (SkillPoint != 0) {
				mes "- 存在剩餘的技能點數，無法執行任務。 -";
				close;
			}
			if (job_gen == 0) {
				mes "我們鍊金術師公會最近透過各種宣傳和研究，培養了一大批鍊金術晚輩。";
				next;
				mes "[鍊金術師公會人員]";
				mes "他們分散在米德加爾特各地，活躍在生命研究和特殊物品製作方法研究等各個方面，宣傳鍊金術的偉大。並透過發表各種研究論文，促進了學術的發展。";
				next;
				mes "[鍊金術師公會人員]";
				mes "在公會中，這些取得優秀成果的公會成員被統稱為^3131FF'基因學者^000000。";
				next;
				mes "[鍊金術師公會人員]";
				mes "基因學者可以說是達到鍊金頂峰的人，是引領米爾加爾特的學術潮流的一群人。";
				next;
				mes "[鍊金術師公會人員]";
				mes "我看你在這個方面也達到了一定的程度，你想見見其他的基因學者嗎?";
				next;
				mes "[鍊金術師公會人員]";
				mes "剛好在不久前，有個基因學者發表的論文在生命研究領域引起了轟動。";
				mes "像你這種擁有豐富學識的人，應該可以理解她的研究成果，並和她進行討論吧?";
				next;
				mes "[鍊金術師公會人員]";
				mes "說不定她的研究成果會對你的研究起到幫助，你也可以因此成為基因學者!";;
				next;
				mes "[鍊金術師公會人員]";
				mes "怎麼樣?你想去見見那位基因學者嗎?";
				next;
				switch(select("去見她:不去見她")) {
				case 1:
					if (VIP_SCRIPT) {
						callfunc "VIP_Third_Class";
						set job_gen,1;
						setquest 2215;
						next;
					}
					mes "[鍊金術師公會人員]";
					mes "明智的決定!";
					mes "最近風頭最勁的基因學者是'德布梨絲'。";
					next;
					mes "[鍊金術師公會人員]";
					mes "她就在企業都市的研究室裡，那裡還有其他研究武器的基因學者。你應該會很高興見到那些基因學者的。";
					set job_gen,1;
					setquest 2215;
					close;
				case 2:
					mes "[鍊金術師公會人員]";
					mes "你對生命研究不感興趣嗎?";
					close;
				}
			}
			mes "最近在學術界引起轟動的基因學者正在企業都市進行研究。";
			mes "如果你感興趣的話，可以去看一看。";
			close;
		}
	}
	mes "我們鍊金術師公會最近透過各種宣傳和研究，培養了一大批鍊金術晚輩。";
	next;
	mes "[鍊金術師公會人員]";
	mes "他們分散在米德加爾特各地，活躍在生命研究和特殊物品製作方法研究等各個方面，宣傳鍊金術的偉大。並透過發表各種研究論文，促進了學術的發展。";
	next;
	mes "[鍊金術師公會人員]";
	mes "在公會中，這些取得優秀成果的公會成員被統稱為^3131FF'基因學者^000000。";
	next;
	mes "[鍊金術師公會人員]";
	mes "基因學者可以說是達到鍊金頂峰的人，是引領米爾加爾特的學術潮流的一群人。";
	close;
}

job3_gen01,25,58,3	script	德布梨絲#gen	865,{
	if (SkillPoint != 0) {
		mes "- 還有剩餘的技能點數，無法執行任務。 -";
		close;
	}
	if (job_gen == 1) {
		mes "[德布梨絲]";
		mes "啊!吸血植物樣本C又謝掉了。";
		mes "得換一下生長劑的成分嗎?";
		mes "得快點才行啊!";
		next;
		mes "[德布梨絲]";
		mes "哎呀?我好像是第一次見到你吧?";
		mes "看你的穿著，你也是學鍊金術的吧?";
		next;
		mes "[德布梨絲]";
		mes "最近鍊金術師越來越多，但真正能靜下心來研究學問的人卻沒有幾個!";
		next;
		mes "[德布梨絲]";
		mes "你看看這個，這個炸彈的威力非常強喔!";
		mes "我製造的小矮人是不是更漂亮!光說著這類的話!";
		next;
		mes "[德布梨絲]";
		mes "雖然鍊金術師公會說會給研究員很多支持，但我努力研究得到的只有這個<方便種植物的蘑菇>!";
		next;
		mes "[德布梨絲]";
		mes "因此研究室到處都是蘑菇!家裡的各個地方都是孢子!";
		next;
		select("你說話的語調挺特別的。");
		mes "[德布梨絲]";
		mes "我是盧恩.米德加茲人!";
		mes "你去過盧恩.米德加茲嗎?在盧恩.米德加茲，所有人都這麼說話。";
		next;
		switch(select("是嗎?!:我也是盧恩.米德加茲來的。")) {
		case 1:
			mes "[德布梨絲]";
			mes "真的嗎?";
			mes "你一定是從盧恩.米德加茲的哪個小村莊出來的吧?";
			mes "我們村子很大，所有人說話的時候都是這個語調。";
			break;
		case 2:
			emotion e_lv;
			mes "[德布梨絲]";
			mes "哎呀!越看越覺得你很親切。";
			mes "竟然能在秀發茲發德碰到老鄉，真是太讓人高興了!";
			break;
		}
		next;
		mes "- 噹噹噹噹! -";
		next;
		emotion e_ag;
		mes "[德布梨絲]";
		mes "啊啊啊啊!那個大嫂又開始了!";
		next;
		select("那是什麼聲音?");
		mes "[德布梨絲]";
		mes "那是我的同學，和我住在一起的黛米凱柏林做實驗的聲音。";
		mes "黛米是砲彈專家，因此經常製造很多噪音。";
		next;
		emotion e_sob;
		mes "[德布梨絲]";
		mes "你看!";
		mes "我的蘑菇和植物們全都很害怕!";
		mes "我也因為那個聲音而沒辦法集中精力研究。真是煩死人了!!";
		next;
		mes "- 嘎啦 嘎啦 -";
		next;
		mes "[德布梨絲]";
		mes "像這樣心情不好的時候，就必須吃一點歐羅萊昂做的餅乾。";
		next;
		mes "[德布梨絲]";
		mes "那是從普隆德拉運來的高級餅乾，放一塊在嘴裡，就會讓我想起仙宮。";
		mes "吃了一塊甜美的餅乾後，黛米凱柏林的叮叮噹噹的聲音就好像消失得無影無蹤了。";
		next;
		mes "[德布梨絲]";
		mes "你也來一塊嗎?";
		mes "啊，這麼快就全吃完啦。";
		next;
		mes "[德布梨絲]";
		mes "不過沒關係!在我的2樓房間裡，堆著好幾箱子!";
		mes "因為忍受不住誘惑，所以我採購了很多，呵呵呵!";
		next;
		mes "- 嘎啊啊啊啊! -";
		specialeffect EF_ENDURE;
		next;
		specialeffect EF_REPAIRWEAPON,AREA,"#from1to2gen";
		mes "-嘎啊啊! 嘎啊啊啊啊!!! -";
		next;
		specialeffect EF_CRASHEARTH,AREA,"#from1to2gen";
		mes "- 噹!噹!!噹!!!噹!!!! -";
		next;
		mes "[德布梨絲]";
		mes "..................";
		next;
		mes "[德布梨絲]";
		mes "哦哦哦哦呃呃";
		mes "嘎啊啊啊啊啊啊啊啊!!!!";
		next;
		mes "[德布梨絲]";
		mes "黛米!!!!!黛米!!!!!";
		next;
		mes "- 看來必須讓德布梨絲鎮靜下來 -";
		set job_gen,2;
		close;
	} else if (job_gen == 2) {
		mes "[德布梨絲]";
		mes "嘎啊啊啊啊啊啊啊啊!!!!";
		mes "黛米!!!!!黛米!!!!!";
		next;
		mes "- 看來需要用什麼東西讓德布梨絲鎮靜下來 -";
		close;
	} else if (job_gen == 3) {
		mes "[德布梨絲]";
		mes "嘎啊啊啊啊啊啊啊啊!!!!";
		mes "黛米!!!!!黛米!!!!!";
		next;
		select("把餅乾交給德布梨絲。");
		mes "- 德布梨絲像餓狼一樣，一把抓過餅乾，塞進了嘴裡。 -";
		next;
		mes "- 嘎啦 嘎啦 嘎啦 -";
		mes "- 嘎啦 嘎啦 嘎啦 -";
		mes "- 嘎啦 嘎啦 嘎啦 -";
		next;
		mes "[德布梨絲]";
		mes "啊，";
		mes "留在舌尖的甜美的奶油香味，";
		mes "真的能讓人的心情平靜下來。";
		next;
		emotion e_pif;
		mes "[德布梨絲]";
		mes "黛米今天又很用功嘛!";
		mes "呵呵呵，真是個用功的朋友。";
		next;
		mes "- 果然餅乾的效果相當好，也相當快速。 -";
		emotion e_no1,1;
		next;
		mes "[德布梨絲]";
		mes "你也是喜歡鍊金術，所以才會到這裡來的吧?";
		next;
		mes "[德布梨絲]";
		mes "哦，對了，我還沒跟你自我介紹呢。";
		mes "我叫德布梨絲";
		mes "我對創造新的、獨特的生命體非常感興趣。";
		next;
		mes "[德布梨絲]";
		mes "你叫什麼啊，鍊金術師晚輩?";
		next;
		select("我叫"+strcharinfo(0)+"。");
		mes "[德布梨絲]";
		mes "哦，"+strcharinfo(0)+"!";
		mes "名字很獨特嘛!見到你很高興。";
		next;
		mes "[德布梨絲]";//此處老外排版
		mes "但是我記不住名字......";
		mes "我就隨便叫你<泡泡>了。沒關係吧?";
		next;
		//select("I uhh...");//此處沒有出現
		mes "[德布梨絲]";
		mes "泡泡，你聽說過我的研究嗎?";
		mes "你是讀了我的論文之後才來找我的吧?";
		next;
		switch(select("不:是的")) {
		case 1:
			mes "[德布梨絲]";
			mes "哎呀，你的臉上都寫著呢。";
			mes "不要害羞嘛!";
			break;
		case 2:
			mes "[德布梨絲]";
			mes "果然沒錯吧?";
			mes "同一個地方的人，果然就容易溝通。";
			break;
		}
		next;
		mes "[德布梨絲]";
		mes "剛才你看到我怒不可遏之後，用我之前跟你提到的方法讓我恢復了原狀。";
		next;
		mes "[德布梨絲]";
		mes "你這種不放過任何小信息的注意力很讓我滿意。";
		mes "你擁有成為一名基因學者的素質。";
		next;
		mes "[德布梨絲]";
		mes "^FF0000成為基因學者之後，就無法再對鍊金術師和創造者的研究課題進行研究了。^000000";
		mes "如果你可以接受的話，現在就可以挑戰一下。";
		next;
		switch(select("以後再挑戰。:我想馬上成為基因學者!")) {
		case 1:
			mes "[德布梨絲]";
			mes "沒辦法。";
			mes "雖然那些研究課題比基因學者更是下位的階段，但基礎也是非常重要的。";
			mes "泡泡，等你完成了所有的研究之後再來找我吧!一定要來找我喔。";
			set job_gen,4;
			close;
		case 2:
			mes "[德布梨絲]";
			mes "很好!";
			mes "讓我們一起努力吧!";
			set job_gen,5;
			close;
		}
	} else if (job_gen == 4) {
		mes "[德布梨絲]";
		mes "泡泡!";
		mes "你做好挑戰基因學者資格的準備了嗎?";
		next;
		switch(select("還沒有。:是的!")) {
		case 1:
			mes "[德布梨絲]";
			mes "你要不斷努力!學問是沒有捷徑的!";
			close;
		case 2:
			mes "[德布梨絲]";
			mes "很好!";
			mes "讓我們一起努力吧!";
			set job_gen,5;
			close;
		}
	} else if (job_gen == 5) {
		mes "[德布梨絲]";
		mes "泡泡，你對植物有什麼看法嗎?";
		mes "你喜歡植物嗎?你對什麼植物感興趣呢?";
		next;
		mes "- 噹噹噹噹 -";
		next;
		emotion e_swt;
		mes "[德布梨絲]";
		mes "啊，黛米凱柏林該不會又開始了吧?";
		mes "希望這次又不成功...黛米...一定...不能成功...";
		next;
		mes "[德布梨絲]";
		mes "我說到哪裡了?";
		mes "啊，對了。在鍊金術中，植物占據了相當大的比重。";
		mes "鍊金術師們會讓噬人花和氣泡蟲快速生長，幫助自己戰鬥...";
		next;
		mes "- 噹!!噹!!噹!!噹!! -";
		next;
		emotion e_dots;
		mes "[德布梨絲]";
		mes ".......................";
		next;
		emotion e_dots;
		mes "[德布梨絲]";
		mes "和技術一樣，植物比較容易通過...";
		next;
		mes "- 砰!!!! -";
		next;
		emotion e_dots;
		mes "[德布梨絲]";
		mes "…交配…";
		next;
		mes "- 砰!!!! -";
		mes "- 嘎啊啊啊啊啊啊!!!! -";
		mes "- 咚咚咚!!!! -";
		mes "- 噹!!噹!!噹!!噹!! -";
		next;
		emotion e_dots;
		mes "[德布梨絲]";
		mes ".......................";
		next;
		emotion e_dots;
		mes "[德布梨絲]";
		mes "泡泡。";
		next;
		emotion e_dots;
		mes "[德布梨絲]";
		mes "想成為基因學者，你必須通過有關的考試。";
		next;
		mes "[德布梨絲]";
		mes "主題是<完善的隔音系統>!";
		mes "你必須用研究所1樓的植物和實驗工具，";
		mes "把我從黛米凱柏林的可怕的噪音中解救出來。";
		next;
		mes "[德布梨絲]";
		mes "好的，快開始吧!";
		set job_gen,6;
		close;
	} else if (job_gen == 6) {
		mes "[德布梨絲]";
		mes "主題是<完善的隔音系統>!";
		mes "你必須用研究所1樓的植物和實驗工具，";
		mes "把我從黛米凱柏林的可怕的噪音中解救出來。";
		next;
		mes "[德布梨絲]";
		mes "在架子上有相關書籍，可以供你參考。";
		mes "好的，快開始吧!";
		close;
	} else if (job_gen > 6 && job_gen < 60) {
		mes "[德布梨絲]";
		mes "有什麼事嗎?";
		next;
		switch(select("沒什麼。:研究搞砸了。")) {
		case 1:
			mes "[德布梨絲]";
			mes "再不開始的話，我就要尖叫了!!";
			close;
		case 2:
			mes "[德布梨絲]";
			mes "從頭來過不就行了?";
			mes "這種事情來問我幹什麼?";
			set job_gen,7;
			if (checkquest(2209) == 1) erasequest 2209;
			if (checkquest(2210) == 1) erasequest 2210;
			if (checkquest(2211) == 1) erasequest 2211;
			if (checkquest(2212) == 1) erasequest 2212;
			if (checkquest(2213) == 1) erasequest 2213;
			if (checkquest(2214) == 1) erasequest 2214;
			close;
		}
	} else if (job_gen == 60 || job_gen == 61) {
		mes "- 叮叮嘎嘎 -";
		next;
		mes "- 噹噹噹叩叩叩 -";
		next;
		mes "[德布梨絲]";
		mes "黛米凱柏林到底在幹什麼?";
		mes "真是吵死了。";
		next;
		mes "[德布梨絲]";
		mes "研究好像結束了吧，泡泡?";
		mes "把種子給我吧!";
		next;
		if (countitem(6273) == 0 && countitem(6272) == 0) { //疑似官方現行版本任務無法觸發此區塊
			mes "[德布梨絲]";
			mes "發生什麼事了?";
			mes "你怎麼可以說實驗完成後沒有任何結果?";
			mes "再從頭開始吧。";
			set job_gen,7;
		} else {
			mes "[德布梨絲]";
			mes "讓我看看你製造的種子。";
			mes "到底會是什麼結果呢?";
			next;
			mes "[德布梨絲]";
			mes "把種子放進超高速培養機中，調節濕度和日曬量...";
			next;
			mes "[德布梨絲]";
			mes "哎呀!";
			next;
			if (job_gen == 60) {
				specialeffect EF_FLASHER,AREA,"Super Cultivator#gen";
				specialeffect EF_PHARMACY_OK,AREA,"Super Cultivator#gen";
				mes "[德布梨絲]";
				mes "泡泡，你看這個!";
				mes "真是個神奇的植物!";
				next;
				mes "[德布梨絲]";
				mes "培養機內部到處都是帶尖刺的藤!";
				mes "枝條非常密集堅固，就像是牆壁一樣!";
				next;
				mes "[德布梨絲]";
				mes "就是這個，泡泡!";
				mes "利用這種植物的特性，可以用於各種用途!!";
				next;
				mes "[德布梨絲]";
				mes "真是了不起，泡泡。";
				mes "我以為你應該會有一點成果，沒想到比我想像的更好!";
				next;
				mes "[德布梨絲]";
				mes "我得把你的研究成果告訴鍊金術公會。";
				mes "我會寫一封信，說泡泡的實力非常了不起，完全有資格成為基因學者的一員。";
				next;
				mes "[德布梨絲]";
				mes "等待公會的答覆可能需要一點時間，你能稍等一下嗎?";
				mes "你去見見黛米凱柏林吧!";
				next;
				mes "[德布梨絲]";
				mes "她也是基因學者，特長是迷你大砲。";
				mes "你去看看她的研究成果吧!";
				mes "幫我轉告她，讓她研究的時候動作輕一點。";
				delitem 6273,1; //變異種子
				set job_gen,62;
				changequest 2215,2216;
			} else {
				specialeffect EF_SPELLBREAKER,AREA,"Super Cultivator#gen";
				specialeffect EF_PHARMACY_FAIL,AREA,"Super Cultivator#gen";
				mes "[德布梨絲]";
				mes "哎呀!失敗了，泡泡。";
				mes "完全沒有成長，很快就謝掉了。";
				mes "快去重新研究!";
				delitem 6272,1; //實驗用種子
				set job_gen,7;
			}
		}
		if (checkquest(2209) == 1) erasequest 2209;
		if (checkquest(2210) == 1) erasequest 2210;
		if (checkquest(2211) == 1) erasequest 2211;
		if (checkquest(2212) == 1) erasequest 2212;
		if (checkquest(2213) == 1) erasequest 2213;
		if (checkquest(2214) == 1) erasequest 2214;
		close;
	} else if (job_gen > 61 && job_gen < 76) {
		if (checkquest(2223,HUNTING) == 2) {
			mes "[德布梨絲]";
			mes "哎呀，等等。";
			mes "郵件好像已經來了。";
			set job_gen,76;
			completequest 2223;
			close;
		}
		mes "[德布梨絲]";
		mes "在收到公會的答覆之前，你先去看看黛米凱柏林的研究成果吧!";
		close;
	} else if (job_gen == 76) {
		mes "[德布梨絲]";
		mes "泡泡!";
		mes "有好消息!";
		next;
		if (checkweight(1201,2) == 0 || MaxWeight - Weight < 2000) {
			mes "- 請在這裡這裡暫停一下!! -";
			mes "- 您所攜帶的物品太多了 -";
			mes "- 所以不能獲得物品 -";
			mes "- 請減輕重量之後 -";
			mes "- 再來挑戰吧 -";
			close;
		}
		mes "[德布梨絲]";
		if (BaseJob != Job_Alchemist || BaseLevel < 99 || JobLevel < 50) {
			mes "哦，但是你好像還沒做好聽消息的準備。";
			close;
		}
		if (checkcart()){
			mes "先把那個沉重、骯髒的手推車放掉。在這種開心的時刻，那個東西太煞風景了。";
			close;
		}
		if (SkillPoint != 0) {
			mes "- 還有剩餘的技能點數，無法執行任務。 -";
			close;
		}
		mes "收到鍊金術師公會的信了。";
		mes "我來讀給你聽，泡泡!";
		next;
		mes "<親愛的從事基因學研究的德布梨絲小姐>";
		mes "<最近我們看到了你的活耀，聽到了很多你的消息。>";
		next;
		mes "[德布梨絲]";
		mes "這位是我的老鄉。";
		next;
		mes "<傳達你的很多研究成果都進入了可上市的階段，我們對此感到非常高興。>";
		mes "<這一定會給其他鍊金術師造成很大的激勵作用。>";
		next;
		mes "<另外你還隨信附了一篇論文。>";
		mes "<內容是名叫"+strcharinfo(0)+"的鍊金術師發現的，可以高速成長形成各種隔離斷膜的新型植物。>";
		mes "<我和鍊金術師公會的各位鍊金術師看了這篇論文，覺得很有意思。>";
		next;
		mes "<聽說了這一消息的其他基因學者對該種植物的應用也非常關心，希望今後能有更多相關的研究論文。>";
		next;
		mes "<德布梨絲>";
		mes "<我想"+strcharinfo(0)+"具備了作為一名基因學者的能力和素質。>";
		mes "<請你盡快轉告他，希望他在成為基因學者之後，能夠繼續有好的表現。>";
		next;
		mes "<鍊金術師公會對於能有德布梨絲小姐、黛米凱柏林小姐和"+strcharinfo(0)+"這樣優秀的人才而感到驕傲。>";
		mes "<期待你們今後有更好的研究成果發布。>";
		next;
		mes "[德布梨絲]";
		if (ismounting()) {
			mes "請你先從那兇惡的東西上下來，牠不適合如此開心的場合。";
			close;
		}
		mes "祝賀你，泡泡!";
		mes "從現在開始，你就是基因學者了!!";
		jobchange roclass(eaclass()|EAJL_THIRD);
		set job_gen,77;
		getitem 5752,1; //Midas_Whisper
		getitem 2795,1; //Green_Apple_Ring
		next;
		mes "[德布梨絲]";
		mes "就像公會信中說的那樣，我也對你充滿了期待。";
		mes "我們一起努力吧!爭取成為最好的基因學者!";
		close;
	} else if (job_gen == 77) {
		mes "[德布梨絲]";
		mes "泡泡，你想加入我愛萊昂嗎?那是喜愛餅乾的人組成的同好會。當然，我們對歐羅萊昂完全沒有不純潔的想法。";
		close;
	}
	mes "[德布梨絲]";
	mes "我那麼漂亮嗎?幹嘛總是盯著我看?";
	close;
}

job3_gen01,83,72,3	script	送貨箱#generic	111,{
	mes "- 有個巨大的箱子。 -";
	if (job_gen == 2) {
		next;
		switch(select("打開箱子查看:確認地址:什麼都不做。")) {
		case 1:
			mes "- 箱子裡裝著包裝精美的幾袋餅乾。 -";
			next;
			switch(select("拿出餅乾:什麼都不做。")) {
			case 1:
				mes "- 你從箱子裡拿出一袋餅乾。 -";
				set job_gen,3;
				close;
			case 2:
				mes "你什麼都沒做。";
				close;
			}
		case 2:
			mes "[寄件人：盧恩.米德加茲王國普隆德拉歐羅萊昂]";
			mes "[收件人：秀發茲發德共和國企業都市德布梨絲]";
			close;
		case 3:
			mes "你什麼都沒做。";
			close;
		}
	}
	close;
}

job3_gen01,12,56,3	script	植物圖鑒#generic	111,{
	mes "<世界上最容易的練金術>";
	mes "包勃羅斯著";
	next;
	switch(select("動物篇:植物篇:礦物篇:其他")) {
	case 1:
		mes "...利用動物進行的鍊金術非常簡單，誰都可以學會。";
		mes "把用來施展鍊金術的動物放在白色的托盤上，在頭腦中想像變化的目標三次，然後使用鍊金術。";
		mes "非常簡單。";
		next;
		mes "...鍊金術中主要使用的動物，如果遭到鍊金術師們的濫捕，可能會面臨滅絕，因此在本書中不進行討論。";
		close;
	case 2:
		mes "...利用植物進行的鍊金術連小孩子都很容易學會。";
		mes "把用來施展鍊金術的植物放在桌子上，用簡單的工具把它們折斷或切斷，接在一起，然後使用鍊金術即可。";
		mes "真是了不起的方法。";
		next;
		mes "...鍊金術中主要使用的植物有體型超大的美枝草、中間柔軟但是長著非常鋒利的刺的摩卡樹、生長速度非常快但必須寄生在其他生物身上的補氣藤。";
		if (job_gen == 6)
			set job_gen,7;
		close;
	case 3:
		mes "...使用礦物的鍊金術是新手很容易掌握的技術。";
		mes "將礦物放進精緻的箱子裡，隨便唱兩次想到的歌，然後施展鍊金術就行了。";
		mes "真是非常容易。";
		next;
		mes "...鍊金術中使用的礦物主要是強化武器金屬等級1，所以對資金的要求也不高。";
		close;
	case 4:
		mes "...鍊金術是世界上最容易的東西。";
		mes "筆者每週都會在學生們面前用發出藍光的礦物進行鍊金術示範，每次總能得到熱烈的歡迎。";
		mes "相信大家看了本書之後，也會被鍊金術給迷住的。";
		close;
	}
}

// QuestID,PlantName,Description{,Part?}
function	script	Genetic_Job_Plant	{
	if (job_gen == 7) {
		if (checkquest(getarg(0)) == -1 || checkquest(getarg(0)) == 0) {
			mes "- "+getarg(2)+" -";
			next;
			switch(select("用作材料:不使用")) {
			case 1:
				if (getarg(3,0)) set .@str$,"some of the ";
				mes "- 你獲得了"+.@str$+getarg(1)+"。 -";
				setquest getarg(0);
				close;
			case 2:
				mes "- 這種植物好像不適合用在研究。 -";
				close;
			}
		}
		mes "- 你已經有"+getarg(1)+"了。 -";
		close;
	} else if (job_gen == 6) {
		mes "好像需要有關植物的信息，在房間的某個地方應該可以找到。";
		close;
	}
	mes "- "+getarg(2)+" -";
	close;
}

job3_gen01,36,55,3	script	美枝草#gen	844,{
	callfunc "Genetic_Job_Plant",2209,"美枝草","雖然是個草，但體積非常巨大，被稱為美枝草。";
	end;
}
job3_gen01,36,45,3	script	摩卡樹#gen	844,{
	callfunc "Genetic_Job_Plant",2210,"摩卡樹","- 長有保護脆弱身體的尖刺的摩卡樹。 -",1;
	end;
}
job3_gen01,42,55,3	script	補氣藤#gen	844,{
	callfunc "Genetic_Job_Plant",2211,"補氣藤","- 成長速度非常快的補氣藤。 -",1;
	end;
}
job3_gen01,30,58,3	script	阿蘭杜拉#gen	844,{
	callfunc "Genetic_Job_Plant",2212,"阿蘭杜拉","- 開花時，會全伸長滿花的植物。 -";
	end;
}
job3_gen01,30,63,3	script	昆格拉#gen	844,{
	callfunc "Genetic_Job_Plant",2213,"昆格拉","- 擁有巨大而堅固的樹幹和非常大的葉子的植物。 -";
	end;
}
job3_gen01,32,38,3	script	黏稠草#gen	844,{
	callfunc "Genetic_Job_Plant",2214,"黏稠草","- 雖然不知道名字，但擁有粘性，可以粘在任何地方。 -";
	end;
}

job3_gen01,7,50,3	script	實驗台#gen	844,{
	if (job_gen == 7) {
		mes "- 放著很多工具的實驗台，好像可以把植物放上去進行組合研究。 -";
		next;
		if(select("進行實驗:不進行實驗") == 2) {
			mes "- 還沒到實驗的時候。 -";
			close;
		}
		if ((checkquest(2209) == -1 || checkquest(2209) == 0) &&
		    (checkquest(2210) == -1 || checkquest(2210) == 0) &&
		    (checkquest(2211) == -1 || checkquest(2211) == 0) &&
		    (checkquest(2212) == -1 || checkquest(2212) == 0) &&
		    (checkquest(2213) == -1 || checkquest(2213) == 0) &&
		    (checkquest(2214) == -1 || checkquest(2214) == 0)) {
			mes "["+strcharinfo(0)+"]";
			mes "哎呀，應該把要做實驗用的植物帶過來。";
			close;
		}
		mes "要怎麼處理準備好的植物呢?";
		next;
		select("折斷:粉碎:搗碎");
		while (1) {
			mes "接著做什麼呢?";
			next;
			if (rand(2))
				select("燃燒做成灰:泡在蒸餾水裡:用蒸汽蒸");
			else
				select("放在實驗用的燒瓶裡搖晃:冷凍:加熱");
			if (!rand(7)) break;
		}
		mes "接著做什麼呢?";
		next;
		select("注入到實驗用的種子中。");
		mes "你想選擇哪個種子?";
		next;
		select("實驗用種子-A型:實驗用種子-B型:實驗用種子-C型:實驗用種子-D型");
		mes "把選擇好的種子中放進了準備好的物質內";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "好的，現在把種子拿去給德布梨絲看看吧!";
		if ((checkquest(2209) == -1 || checkquest(2209) == 0) &&
		    (checkquest(2210) == -1 || checkquest(2210) == 0) &&
		    (checkquest(2211) == -1 || checkquest(2211) == 0)){
			set job_gen,61;
			getitem 6272,1; //實驗用種子
			close;
		}
		if (checkquest(2209) == 1) set job_gen,job_gen+10;
		if (checkquest(2210) == 1) set job_gen,job_gen+10;
		if (checkquest(2211) == 1) set job_gen,job_gen+10;
		set .@score, job_gen + JobLevel - 20;
		if (rand(1,100) < .@score) {
			set job_gen,60;
			getitem 6273,1; //變異種子
			close;
		} else {
			set job_gen,61;
			getitem 6272,1; //實驗用種子
			close;
		}
	} else if (job_gen > 7 && job_gen < 60) {
		mes "- 看起來實驗好像失敗了，我最好重新來過。 -";
		set job_gen,7;
		if (countitem(6273)) delitem 6273,1; //變異種子
		if (countitem(6272)) delitem 6272,1; //實驗用種子
		if (checkquest(2209) == 1) erasequest 2209;
		if (checkquest(2210) == 1) erasequest 2210;
		if (checkquest(2211) == 1) erasequest 2211;
		if (checkquest(2212) == 1) erasequest 2212;
		if (checkquest(2213) == 1) erasequest 2213;
		if (checkquest(2214) == 1) erasequest 2214;
		close;
	} else if (job_gen == 60 || job_gen == 61) {
		mes " - 好像把實驗得到的的種子交給德布梨絲就可以了。 -";
		end;
	}
	mes "- 放著很多工具的實驗台，好像可以把植物放上去進行組合研究。 -";
	close;
}

job3_gen01,21,67,3	script	超高速培養機#gen	844,{
	end;
}

job3_gen01,91,48,3	script	黛米凱柏林#gen	982,{
	mes "[黛米凱柏林]";
	if (job_gen < 62) {
		mes "啊~螺栓去哪兒了?";
		close;
	} else if (job_gen == 62) {
		mes "我想坐在藍色的手推車上~";
		mes "裝上美麗如畫的大砲~";
		mes "和我心愛的人一起~";
		mes "開一輩子的砲~~";
		next;
		mes "[黛米凱柏林]";
		mes "啊~螺栓去哪兒了?";
		mes "螺絲也怎麼不夠了?昨天我不是剛訂購了一批嗎?";
		next;
		select("你好");
		mes "[黛米凱柏林]";
		mes "哎呀~?";
		mes "什麼時候...你是誰啊?";
		next;
		mes "[黛米凱柏林]";
		mes "啊!不，現在不是說話的時候!";
		mes "喂!你來幫幫我!";
		next;
		mes "[黛米凱柏林]";
		mes "客人訂購的手推車用迷你砲必須在今天完成，但不知道零件跑哪裡去了!";
		next;
		mes "[黛米凱柏林]";
		mes "哎呀~我想是不是掉在這周圍......";
		mes "只要有10個就行了!我得組裝其他零件......拜託你了!";
		changequest 2216,2217;
		set job_gen,63;
		close;
	} else if (job_gen > 62 && job_gen < 74) {
		mes "啊，糟了，糟了~~~";
		mes "零件找到了嗎?還沒有嗎?既然答應幫我，就請你把握時間!";
		close;
	} else if (job_gen == 74) {
		mes "這個手推車的火力還不夠，把這個裝在這裡的話...";
		next;
		select("零件拿來了");
		mes "[黛米凱柏林]";
		mes "哎呀，嚇我一跳!!!";
		mes "你怎麼總是喜歡嚇人啊???";
		mes "這不是我丟失的螺絲和螺栓嗎?";
		mes "你是在哪裡找到的?";
		next;
		select("你不是讓我去找的嗎?");
		mes "[黛米凱柏林]";
		mes "哎呀?哎呀?";
		mes "真的嗎?呵呵呵呵呵!";
		mes "看我的記性。";
		next;
		mes "[黛米凱柏林]";
		mes "請稍等一下。";
		mes "只要有這些零件，馬上就能結束了。";
		next;
		mes "- 嘎嘎嘎嘎 -";
		next;
		mes "[黛米凱柏林]";
		mes "很好，砲已經組裝到了手推車上......接下來測試一下性能怎麼樣?";
		next;
		mes "- 嘁... -";
		next;
		mes "- 砰!砰!砰!砰! -";
		next;
		mes "[黛米凱柏林]";
		mes "很好!成功了!";
		mes "呼，多虧了你，訂購的東西終於按時完成了。";
		mes "真是太感謝了。";
		next;
		mes "[黛米凱柏林]";
		mes "啊?你來找我有什麼事嗎?";
		mes "你也是來訂做東西的嗎?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我是基因學者志願生。";
		mes "聽說黛米凱柏林在攜帶式迷你加農砲方面有著獨到的見解。";
		next;
		mes "[黛米凱柏林]";
		mes "是的，我可以製作很多種加農砲。";
		mes "最近有很多人請我幫他們改造手推車，我給他們安裝了包含迷你加農砲在內很多附加功能。";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "改造手推車?";
		next;
		mes "[黛米凱柏林]";
		mes "是的，你在下坡的時候，難道不想坐著手推車飛奔嗎?";
		mes "或你不覺得拖著手推車到處走很無趣嗎?";
		next;
		mes "[黛米凱柏林]";
		mes "手推車不再只是裝載東西的推車。";
		mes "在手推車上裝上加農砲，可以讓它成為攻擊時的好幫手，可以讓你的力量獲得爆發性的提高。";
		next;
		mes "[黛米凱柏林]";
		mes "在下面德布梨絲好像正在做某種像扮家家酒一樣的研究。";
		mes "我完全不能理解那種論文怎麼能賺到那麼多人氣的，哼。";
		emotion e_pif;
		next;
		mes "[黛米凱柏林]";
		mes "請看看這個超級迷你加農砲。";
		mes "只有手掌大小，是世界上最輕、最小的加農砲。";
		mes "你不覺得它很可愛嘛?啊-";
		emotion e_awsm;
		next;
		select("我的手推車也能進行改造嗎?");
		mes "[黛米凱柏林]";
		mes "很遺憾，手推車改造只針對基因學者。";
		mes "這是鍊金術公會的規定，我也沒辦法。";
		next;
		mes "[黛米凱柏林]";
		mes "但是請不要失望。";
		mes "只要成為基因學者不就行了?呵呵呵呵!";
		next;
		mes "[黛米凱柏林]";
		mes "而且不是所有手推車都可以進行改造。";
		mes "必須是經過長時間內磨練的、品質經過檢驗的手推車才行。";
		next;
		mes "[黛米凱柏林]";
		mes "在成為基因學者之前，請你平時好好修練修練手推車技能。";
		mes "要使你的肌肉和手推車的運動達到完美的平衡才行。";
		next;
		mes "[黛米凱柏林]";
		mes "如果你需要的話，^FF0000我可以給你介紹一個不錯的修練場所^000000。";
		mes "^FF0000雖然她應該不怎麼喜歡^000000，但那裡真的是很棒的場所，呵呵!";
		next;
		mes "[黛米凱柏林]";
		mes "趁這次機會，你去盡情讓你的手推車飛奔吧!";
		mes "你去用手推車打飛";
		mes "^FF0000100個左右的怪物^000000緩解一下疲勞，熟悉一下手推車。";
		next;
		mes "[黛米凱柏林]";
		mes "好了，我得開始準備下一個訂單的製作了。";
		mes "需要修練的時候，請隨時來找我。";
		mes "我會隨時幫你進行手推車改造的。";
		next;
		mes "- 咯 -";
		next;
		mes "- 怎麼回事?感覺地面好像突然空蕩蕩的。 -";
		set job_gen,75;
		changequest 2217,2223;
		close2;
		warp "job3_gen01",23,32;
		end;
	} else if (job_gen == 75) {
		if (checkquest(2223,HUNTING) == 2) {
			mes "德布梨絲好像發現是我把你送進圍欄的了。";
			mes "看著德布梨絲的那個表情，感覺真的很不錯。真可惜。";
			close;
		}
		mes "哎呀，修練還沒結束嗎?";
		mes "你想到魔菇圍欄裡去嗎?";
		next;
		switch(select("是的:不")) {
		case 1:
			mes "[黛米凱柏林]";
			mes "盡情地!盡情地揮舞你的手推車吧~";
			close2;
			warp "job3_gen01",23,32;
			end;
		case 2:
			mes "[黛米凱柏林]";
			mes "哎呀，真可惜。我真想看看德布梨絲火冒三丈的樣子。";
			close;
		}
	} else if (job_gen > 75) {
		mes "德布梨絲好像發現是我把你送進圍欄的了。";
		mes "看著德布梨絲的那個表情，感覺真的很不錯。真可惜。";
		close;
	}
}

-	script	散亂的東西#gen	-1,{
	if (job_gen > 62 && job_gen < 74) {
		mes "- 地上散落著各種東西，實驗室一片狼藉 -";
		next;
		mes "- 在裡面你找到了一些小金屬碎片 -";
		set job_gen, job_gen+1;
		specialeffect EF_BASH;
		disablenpc strnpcinfo(0);
		initnpctimer;
		close;
	} else if (job_gen == 74) {
		mes "- 零件好像都已收齊了 -";
		close;
	}
	end;
OnTimer7000:
	enablenpc strnpcinfo(0);
	stopnpctimer;
	end;
}
job3_gen01,84,45,3	duplicate(散亂的東西#gen)	散亂的東西#gen1	111
job3_gen01,77,41,3	duplicate(散亂的東西#gen)	散亂的東西#gen2	111
job3_gen01,78,57,3	duplicate(散亂的東西#gen)	散亂的東西#gen3	111
job3_gen01,89,59,3	duplicate(散亂的東西#gen)	散亂的東西#gen4	111
job3_gen01,90,51,3	duplicate(散亂的東西#gen)	散亂的東西#gen5	111
job3_gen01,83,27,3	duplicate(散亂的東西#gen)	散亂的東西#gen6	111
job3_gen01,74,45,3	duplicate(散亂的東西#gen)	散亂的東西#gen7	111

job3_gen01,18,39,3	script	警告#gen	837,{
	mes "= 警告 =";
	mes "如果你碰了我的蘑菇，我會對著你的耳朵尖叫的!!!";
	if (job_gen < 75) close;
	next;
	switch(select("到圍欄外面去:待著不動")) {
	case 1:
		mes "- 在被德布梨絲抓住之前，最好還是出去 -";
		close2;
		warp "job3_gen01",21,38;
		end;
	case 2:
		mes "- 行動已取消。 -";
		close;
	}
}

job3_gen01,80,12,1	script	堆著的書#gen	111,{
	mes "- <月刊BooM!>的過期雜誌亂糟糟地堆在那裡。 -";
	close;
}

job3_gen01,83,20,1	script	床#gen	111,{
	mes "- 方格花紋的床單組好像經過了精心挑選，但看上去髒兮兮的，幾乎喪失了休息的功能。 -";
	close;
}

job3_gen01,85,25,1	script	被打開的書#gen	111,{
	mes "< 你也想要這麼漂亮的房間嗎?你的錢夠嗎?你有足夠大的房間嗎?請馬上到企業都市百貨商店去，把最高級的壁紙和裝飾用品全部買回來吧! >";
	next;
	mes "< 在夜裡照亮你的房間的高級蠟燭，貴婦人必須擁有的黃金材質的大鏡子。兩著缺一不可! >";
	close;
}

job3_gen01,81,81,0	script	#dbroom	139,2,2,{
OnTouch:
	mes "- 房間的一邊堆滿著快遞箱子，看上去像倉庫一樣。 -";
	close;
}

job3_gen01,24,32,0	script	The gaze of Devries	139,2,2,{
OnTouch:
	if (checkquest(2223,HUNTING) == 2) {
		mapannounce "job3_gen01","德布梨絲：哎呀?魔菇圍欄裡有貓跑進去了嗎?",bc_map,"0x99CC00"; //FW_NORMAL 12 0 0
		emotion e_omg,1;
		warp "job3_gen01",21,38;
	}
	end;
}

job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0