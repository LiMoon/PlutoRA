//===== rAthena Script ======================================= 
//= Genetic Job Quest
//===== By: ================================================== 
//= Muad_Dib, Aeomin
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= [Official Conversion]
//= Job change Quest from Alchemist / Creator -> Genetic.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Masao]
//= 1.1 Fixed Scatter Stuff NPCs [JayPee].
//= 1.2 Updated dialogue. Credits to Muad_Dib. [Joseph]
//= 1.3 Updated script, optimized. [Euphy]
//= 1.3a Moved warps to separate file. [Euphy]
//= 1.4 Added VIP features. [Euphy]
//============================================================

alde_alche,35,186,5	script	鍊金術師公會人員	805,{
	mes "[鍊金術師公會人員]";
	if (BaseLevel > 98 && JobLevel > 49){
		if (BaseJob == Job_Alchemist) {
			if (SkillPoint != 0) {
				mes "- You still have unused skill points. Come back when you have used all of them. -";
				close;
			}
			if (job_gen == 0) {
				mes "Our Alchemist Union brings up many junior alchemists with various promotions and investigations.";
				next;
				mes "[鍊金術師公會人員]";
				mes "They have spread to all corners of Rune-Midgart and were active in many fields like sciences or special item production methods.";
				next;
				mes "[鍊金術師公會人員]";
				mes "We call the members of our new union ^3131FF'Geneticists'^000000.";
				next;
				mes "[鍊金術師公會人員]";
				mes "Geneticists are the ones who have reached their peak potential in the world of Alchemy. They are the ones who currently spearhead alchemy throughout all of Rune-Midgart.";
				next;
				mes "[鍊金術師公會人員]";
				mes "It looks like you are an expert in this field, how about meeting other geneticists?";
				next;
				mes "[鍊金術師公會人員]";
				mes "Not long ago there was a Geneticist who announced a sensational new life studying research.";
				mes "You are very knowledgeable. Do you think you might understand her work?";
				next;
				mes "[鍊金術師公會人員]";
				mes "Her study may help you gain the knowledge to help you make a Geneticist!";;
				next;
				mes "[鍊金術師公會人員]";
				mes "How's that sound? Do you want to meet a Geneticist?";
				next;
				switch(select("Of course, I want to meet a Geneticist.:I don't want to meet a Geneticist.")) {
				case 1:
					if (getserverdef(VAR_VIP_SCRIPT)) {
						callfunc "VIP_Third_Class";
						set job_gen,1;
						setquest 2215;
						next;
					}
					mes "[鍊金術師公會人員]";
					mes "明智的決定!";
					mes "最近風頭最勁的基因學者是'德布梨絲'。";
					next;
					mes "[鍊金術師公會人員]";
					mes "You can find her in the Alchemy Lab at the western part of Lighthalzen. There is also another Geneticist studying weaponry there, you might want to pay her a visit as well.";
					set job_gen,1;
					setquest 2215;
					close;
				case 2:
					mes "[鍊金術師公會人員]";
					mes "Aren't you interested in the study of life?";
					close;
				}
			}
			mes "The infamous Geneticist is studying at the Alchemy Lab in the western part of Lighthalzen.";
			mes "If you are interested, go meet with her.";
			close;
		}
	}
	mes "我們鍊金術師工會最近透過各種宣傳和研究，培養了一大批鍊金術師晚輩。";
	next;
	mes "[鍊金術師公會人員]";
	mes "他們分散在米德加爾特各地，活躍在生命研究和特殊物品製造方法研究等各個方面，宣傳煉金術的偉大。並透過發表各種研究論文，促進了學術的發展。";
	next;
	mes "[鍊金術師公會人員]";
	mes "在公會中，這些取得優秀成果的公會員被統稱為^3131FF'基因學者'^000000。";
	next;
	mes "[鍊金術師公會人員]";
	mes "基因學者可以說是達到煉金術頂峰的人，是引領米德加爾特的學術潮流的一群人。";
	close;
}

job3_gen01,25,58,3	script	德布梨絲#gen	865,{
	if (SkillPoint != 0) {
		mes "- You still have some unused skill points. -";
		close;
	}
	if (job_gen == 1) {
		mes "[德布梨絲]";
		mes "Oh! The bloodsucking plant sample C is withering again.";
		mes "Should I change the ingredient of the growth care?";
		mes "Hey please cheer up soon!";
		next;
		mes "[德布梨絲]";
		mes "Oh? A stranger?";
		mes "Your look tells me that you are doing Alchemy as well right?";
		next;
		mes "[德布梨絲]";
		mes "People say the number of alchemists is increasing nowadays though there aren't may who study this field properly!";
		next;
		mes "[德布梨絲]";
		mes "They're all too worried about saying things like, ''Look at my bomb damage!'' or ''isn?t my homunculus prettier?''";
		next;
		mes "[德布梨絲]";
		mes "The Alchemist Union was saying they'd support the researchers but actually what you get from the studying is just 'Spores Which Can Be Easily Raised'!";
		next;
		mes "[德布梨絲]";
		mes "Thanks to that, there are spores everywhere in my lab! There are spores everywhere!";
		next;
		select("Why do you talk like that?");
		mes "[德布梨絲]";
		mes "我是盧恩-米德加茲人!";
		mes "你去過盧恩-米德加茲嗎? 在盧恩-米德加茲，所有人都這麼說話。";
		next;
		switch(select("是嗎?!:我也是盧恩-米德加茲來的。")) {
		case 1:
			mes "[德布梨絲]";
			mes "Impossible?";
			mes "Do you come from a Rune-Midgart village?";
			mes "My village is very big, everyone there speaks like this.";
			break;
		case 2:
			emotion e_lv;
			mes "[德布梨絲]";
			mes "Really! That's why I felt some kind of familiar atmosphere around you.";
			mes "Meeting a hometown person in Schwaltzvalt, it's my pleasure to meet you!";
			break;
		}
		next;
		mes "- Thump!! -";
		next;
		emotion e_ag;
		mes "[德布梨絲]";
		mes "Ughhhhh! That woman started again!";
		next;
		select("What's that sound?");
		mes "[德布梨絲]";
		mes "That's the sound of an experiment being carried out by my roommate, 黛米凱伯林..";
		mes "Demi's a fire shell technician so she's always noisy like that.";
		next;
		emotion e_sob;
		mes "[德布梨絲]";
		mes "Look at that!";
		mes "My spores and plants are all freaked out!";
		mes "I can't do my work with those noisy sounds. It gives me so much stress!!";
		next;
		mes "- Munch Crunch -";
		next;
		mes "[德布梨絲]";
		mes "When I don't feel good, I savor Mr. Orlean's handmade cookies little by little.";
		next;
		mes "[德布梨絲]";
		mes "It's a high-quality cookie that I got from Prontera. When I get one bite, it seems like I'm floating to Asgard.";
		mes "The softness of the sweet cookie drowns out the clashing sounds from 黛米凱伯林.";
		next;
		mes "[德布梨絲]";
		mes "Wanna try one?";
		mes "Ah, I've eaten all of it.";
		next;
		mes "[德布梨絲]";
		mes "But it's okay! On the 2nd floor of my room, there are boxes of those!";
		mes "I couldn't stop buying it all in bulk. Ho ho ho.";
		next;
		mes "- Zooooooooooooooooing -";
		specialeffect EF_ENDURE;
		next;
		specialeffect EF_REPAIRWEAPON,AREA,"#from1to2gen";
		mes "- (Screeching Sounds)!!! -";
		mes "- (Screeching)!!! -";
		next;
		specialeffect EF_CRASHEARTH,AREA,"#from1to2gen";
		mes "- Bump! Bump! Bump! -";
		next;
		mes "[德布梨絲]";
		mes "..................";
		next;
		mes "[德布梨絲]";
		mes "Yoooooooooooow";
		mes "Yipeeeeeeeeee!!!";
		next;
		mes "[德布梨絲]";
		mes "Demiiiiiiiiiiiiiiiiiiiiiiii!!!";
		mes "Demiiiiiiiiiiiiiiiiiiiii!!!";
		next;
		mes "-";
		mes "看來需要用什麼東西讓德布梨絲鎮靜下來";
		mes "-";
		set job_gen,2;
		close;
	} else if (job_gen == 2) {
		mes "[德布梨絲]";
		mes "Yipeeeeeeeeee!!!";
		mes "Demiiiiii!!!";
		mes "Demiiiiiiiii!!!";
		next;
		mes "- Hmm. -";
		mes "- What'll make her calm? -";
		close;
	} else if (job_gen == 3) {
		mes "[德布梨絲]";
		mes "Yipeeeeeeeeee!!!";
		mes "Demiiiiii!!!";
		mes "Demiiiiiiiii!!!";
		next;
		select("把餅乾交給德布梨絲。");
		mes "- 德布梨絲 quickly snatches -";
		mes "- away the cookie and stuffs -";
		mes "- it all in her mouth. -";
		next;
		mes "- Crunch Crunch Crunch -";
		mes "- Crunch Crunch Crunch -";
		mes "- Crunch Crunch Crunch -";
		next;
		mes "[德布梨絲]";
		mes "Ah, The sweet buttery taste remains at the end of my tongue.";
		mes "This really gives me peace of mind.";
		next;
		emotion e_pif;
		mes "[德布梨絲]";
		mes "Demi is studying hard today as well";
		mes "Huhuhu. She's really a hard worker.";
		next;
		mes "- That cookie changed her mood quick. -";
		emotion e_no1,1;
		next;
		mes "[德布梨絲]";
		mes "You came here with a love for Alchemy?";
		next;
		mes "[德布梨絲]";
		mes "Oh wait, We haven't introduced ourselves yet.";
		mes "My name is 德布梨絲.";
		mes "I'm very interested in making living things that show new and peculiar growth.";
		next;
		mes "[德布梨絲]";
		mes "What's your name, young alchemist?";
		next;
		select("I'm "+strcharinfo(0)+".");
		mes "[德布梨絲]";
		mes "Oh, "+strcharinfo(0)+"!";
		mes "That's a very unusual name. Pleased to meet you.";
		next;
		mes "[德布梨絲]";
		mes "But I can't memorize the name well.";
		mes "So I'll just call you <Popo>. Would that be ok?";
		next;
		select("I uhh...");
		mes "[德布梨絲]";
		mes "Great...";
		mes "Popo, have you heard of my research?";
		mes "You came here because of that, am I right?";
		next;
		switch(select("No.:Yes.")) {
		case 1:
			mes "[德布梨絲]";
			mes "Well, it's all over your face.";
			mes "You don't have to be shy.";
			break;
		case 2:
			mes "[德布梨絲]";
			mes "That's right huh?";
			mes "Well, as I expected, there really is something that makes homeland people get each other";
			break;
		}
		next;
		mes "[德布梨絲]";
		mes "When you saw me in that feverish state, you remembered what I said before and that calmed me down.";
		next;
		mes "[德布梨絲]";
		mes "I like the way you manage yourself and take care to remember small details.";
		mes "You have the talent to become a Geneticist.";
		next;
		mes "[德布梨絲]";
		mes "^FF0000If you become a Geneticist, you can't take the Alchemist's or Biochemist study course that you haven't investigated before.^000000";
		mes "If that's all right for you, how about giving it a shot right now?.";
		next;
		switch(select("I'll try later.:I want to become a Geneticist!")) {
		case 1:
			mes "[德布梨絲]";
			mes "Alright.";
			mes "Even though your knowledge is not that great as that of Geneticists, a fundamental knowledge is also important.";
			mes "Popo, come back to me once you've finished your studies.";
			set job_gen,4;
			close;
		case 2:
			mes "[德布梨絲]";
			mes "Good!";
			mes "Let's do it!";
			set job_gen,5;
			close;
		}
	} else if (job_gen == 4) {
		mes "[德布梨絲]";
		mes "泡泡，你聽說過我的研究嗎?";
		mes "Are you ready to give it a shot?";
		next;
		switch(select("No.:Yes!")) {
		case 1:
			mes "[德布梨絲]";
			mes "You have to do your best! There is no easy way when looking for knowledge!";
			close;
		case 2:
			mes "[德布梨絲]";
			mes "Very good!";
			mes "Let's do it!";
			set job_gen,5;
			close;
		}
	} else if (job_gen == 5) {
		mes "[德布梨絲]";
		mes "What do you think of plants, Popo";
		mes "Do you like plants? What kind of plants are you interested in?";
		next;
		mes "- Boom, Boom, Boom -";
		next;
		emotion e_swt;
		mes "[德布梨絲]";
		mes "Ah, please tell me 黛米凱伯林 is not starting again?";
		mes "Please let my expectation be wrong ... Please... Demi, please...";
		next;
		mes "[德布梨絲]";
		mes "Where did I stop talking";
		mes "Oh, right. Plants are very important in Alchemy.";
		next;
		mes "[德布梨絲]";
		mes "Alchemy can make Flora's and Geographers grow immensely fast, so they can help in battle, like those Alchemists...";
		next;
		mes "- Boom!! Boom!! Boom!! Boom!! -";
		next;
		emotion e_dots;
		mes "[德布梨絲]";
		mes ".......................";
		next;
		emotion e_dots;
		mes "[德布梨絲]";
		mes "Techniques, plants are relatively easy to...";
		next;
		mes "- Boom!!!!!! -";
		next;
		emotion e_dots;
		mes "[德布梨絲]";
		mes "Crossbreed...";
		next;
		mes "- Pop!!!!!! -";
		mes "- Keeeeeeeeeeeeeh!!!! -";
		mes "- Boooooooooooooom!!!! -";
		mes "- Boom!! Boom!! Boom!! -";
		next;
		emotion e_dots;
		mes "[德布梨絲]";
		mes ".......................";
		next;
		emotion e_dots;
		mes "[德布梨絲]";
		mes "Popo.";
		next;
		emotion e_dots;
		mes "[德布梨絲]";
		mes "I'll give you the test to become a Geneticist.";
		next;
		mes "[德布梨絲]";
		mes "主題是<完美的隔音系統>!";
		mes "你必須用研究所1樓的植物和實驗工具，把我從黛米凱伯林的可怕的噪音中解救出來。";
		next;
		mes "[德布梨絲]";
		mes "Well, you better get started!";
		set job_gen,6;
		close;
	} else if (job_gen == 6) {
		mes "[德布梨絲]";
		mes "主題是<完美的隔音系統>!";
		mes "你必須用研究所1樓的植物和實驗工具，把我從黛米凱伯林的可怕的噪音中解救出來。";
		next;
		mes "[德布梨絲]";
		mes "There are some guides in the cabinet that you may use.";
		mes "Well, you better get started!";
		close;
	} else if (job_gen > 6 && job_gen < 60) {
		mes "[德布梨絲]";
		mes "Is there a problem?";
		next;
		switch(select("Nothing.:The experiment failed.")) {
		case 1:
			mes "[德布梨絲]";
			mes "Better get started, or I'll start to scream!!!";
			close;
		case 2:
			mes "[德布梨絲]";
			mes "You can start from the beginning then?";
			mes "Why are you asking me such things?";
			set job_gen,7;
			if (checkquest(2209) == 1) erasequest 2209;
			if (checkquest(2210) == 1) erasequest 2210;
			if (checkquest(2211) == 1) erasequest 2211;
			if (checkquest(2212) == 1) erasequest 2212;
			if (checkquest(2213) == 1) erasequest 2213;
			if (checkquest(2214) == 1) erasequest 2214;
			close;
		}
	} else if (job_gen == 60 || job_gen == 61) {
		mes "- Zing Zing Zigi Zigi -";
		next;
		mes "- Zing Zing Zigi Zigi -";
		next;
		mes "[德布梨絲]";
		mes "What the hell is 黛米凱伯林 doing?";
		mes "That's really a noisy one.";
		next;
		mes "[德布梨絲]";
		mes "Your study is done, 泡泡?";
		mes "Give me those seeds.";
		next;
		if (countitem(6273) == 0 && countitem(6272) == 0) {
			mes "[德布梨絲]";
			mes "What happened?";
			mes "How could you say the study is completed without any result?";
			mes "Start again.";
			set job_gen,7;
		} else {
			mes "[德布梨絲]";
			mes "Well, I'll take a look at the seeds Popo made.";
			mes "What's the result?";
			next;
			mes "[德布梨絲]";
			mes "When you put a seed in a Super Cultivator, and adjust the temperature...";
			next;
			mes "[德布梨絲]";
			mes "Damn!";
			next;
			if (job_gen == 60) {
				specialeffect EF_FLASHER,AREA,"Super Cultivator#gen";
				specialeffect EF_PHARMACY_OK,AREA,"Super Cultivator#gen";
				mes "[德布梨絲]";
				mes "Popo, look at that!";
				mes "That's really a mysterious plant!";
				next;
				mes "[德布梨絲]";
				mes "The inside of the Cultivator is now full of thorny stems!";
				mes "It's as if the stems are making a wall and growing very sturdily and those are getting tangled!";
				next;
				mes "[德布梨絲]";
				mes "This is it, Popo!";
				mes "This plant's properties can be used for many applications!";
				next;
				mes "[德布梨絲]";
				mes "This is really great, Popo.";
				mes "I knew that you were knowledgeable but your ability is more than that!";
				next;
				mes "[德布梨絲]";
				mes "I will tell the outcome to the Alchemist Union.";
				mes "I will send a recommendation letter saying that your ability is really great and that you could be a brilliant member.";
				next;
				mes "[德布梨絲]";
				mes "Getting an answer from the Union will take some time, so could you wait a moment?";
				mes "Why don't you go up to 黛米凱伯林?";
				next;
				mes "[德布梨絲]";
				mes "She is a fellow Geneticist, specializing in mini cannons.";
				mes "Go take a look at her experiment results.";
				mes "Could you give this message to her? Tell her to conduct her experiments quietly.";
				delitem 6273,1; //Altered_Seed
				set job_gen,62;
				changequest 2215,2216;
			} else {
				specialeffect EF_SPELLBREAKER,AREA,"Super Cultivator#gen";
				specialeffect EF_PHARMACY_FAIL,AREA,"Super Cultivator#gen";
				mes "[德布梨絲]";
				mes "Uh oh! You failed, Popo.";
				mes "It didn't grow at all, it's withered already.";
				mes "Go and start over!";
				delitem 6272,1; //Experiment_Seed
				set job_gen,7;
			}
		}
		if (checkquest(2209) == 1) erasequest 2209;
		if (checkquest(2210) == 1) erasequest 2210;
		if (checkquest(2211) == 1) erasequest 2211;
		if (checkquest(2212) == 1) erasequest 2212;
		if (checkquest(2213) == 1) erasequest 2213;
		if (checkquest(2214) == 1) erasequest 2214;
		close;
	} else if (job_gen > 61 && job_gen < 76) {
		if (checkquest(2223,HUNTING) == 2) {
			mes "[德布梨絲]";
			mes "哎呀，等等。";
			mes "郵件好像已經來了。";
			set job_gen,76;
			completequest 2223;
			close;
		}
		mes "[德布梨絲]";
		mes "在收到公會的答覆之前，你先去看看黛米凱伯林的研究成果吧!";
		close;
	} else if (job_gen == 76) {
		mes "[德布梨絲]";
		mes "泡泡!";
		mes "有好消息!";
		next;
		if (checkweight(1201,2) == 0 || MaxWeight - Weight < 2000) {
			mes "- Wait a minute !! -";
			mes "- Currently you're carrying -";
			mes "- too many items with you. -";
			mes "- Please try again -";
			mes "- after you lose some weight. -";
			close;
		}
		mes "[德布梨絲]";
		if (BaseJob != Job_Alchemist || BaseLevel < 99 || JobLevel < 50) {
			mes "Oh, it looks like you're not ready to hear it.";
			close;
		}
		if (checkcart()){
			mes "First take off that heavy and dirty cart, that doesn't suit you on this happy moment.";
			close;
		}
		if (SkillPoint != 0) {
			mes "But you look like you have a lingering desire for previous studies.";
			close;
		}
		mes "A letter has arrived from the Alchemist Union.";
		mes "I'll read it to you, Popo!";
		next;
		mes "<Dearest Miss 德布梨絲,>";
		mes "<We are very aware of the brilliant activities you've shown lately.>";
		next;
		mes "<Miss. 德布梨絲>";
		mes "This guy is also from my hometown.";
		next;
		mes "<I heard very good news that your research results are taking the step of commercial use.>";
		mes "<I'm sure it will be a great inspiration for other Alchemists.>";
		next;
		mes "<Come to think of it, you've attached a paper?>";
		mes "<It was a new plant which makes plants grow super-fast, discovered by an Alchemist named, "+strcharinfo(0)+">";
		next;
		mes "<Myself and many other Alchemists from the Alchemist union are taking great interest in this.>";
		next;
		mes "<We are looking forward to more volumes of study papers and outcomes, especially from other Geneticists who?ve heard this news.>";
		next;
		mes "<Miss. 德布梨絲>";
		mes "<I think that "+strcharinfo(0)+" has enough ability and talent to be a Geneticist.>";
		next;
		mes "<Please tell "+strcharinfo(0)+" as soon as possible that we are expecting remarkable activities as a Geneticist from now on.>";
		next;
		mes "<The Alchemist Union is very proud of Miss 德布梨絲, Miss 黛米凱伯林 and "+strcharinfo(0)+" who are excellent talented people.>";
		mes "<We hope that you yield good fruits in the future as well.>";
		next;
		mes "[德布梨絲]";
		if (ismounting()) {
			mes "You are on a riding pet,";
			mes "so you cannot change your job.";
			mes "Please unequip your riding pet and try again!";
			close;
		}
		mes "祝賀你，泡泡!";
		mes "從現在開始，你就是基因學者了!!";
		jobchange roclass(eaclass()|EAJL_THIRD);
		set job_gen,77;
		getitem 5752,1; //Midas_Whisper
		getitem 2795,1; //Green_Apple_Ring
		next;
		mes "[德布梨絲]";
		mes "Just like the word from the Union, I'm also looking forward to your awesome activities.";
		mes "Let's try hard together! To become the best Geneticist!";
		close;
	} else if (job_gen == 77) {
		mes "[德布梨絲]";
		mes "Popo, do you want to join my Love Orleans club? It's a club for those that love cookies. Of course, we don't think of doing weird things with Charles Orleans.";
		close;
	}
	mes "[德布梨絲]";
	mes "Am I that pretty to you? Why are you staring at me like that?";
	close;
}

job3_gen01,83,72,3	script	送貨箱#generic	111,{
	mes "- 有個巨大的箱子。 -";
	if (job_gen == 2) {
		next;
		switch(select("打開箱子查看:確認地址:什麼也不做。")) {
		case 1:
			mes "- 箱子裡裝著包裝精美的幾袋餅乾。 -";
			next;
			switch(select("拿出餅乾:什麼也不做。")) {
			case 1:
				mes "- 你從箱子裡拿出了一袋餅乾。-";
				set job_gen,3;
				close;
			case 2:
				mes "You do nothing.";
				close;
			}
		case 2:
			mes "[From: 盧恩-米德加茲王國, 普隆德拉, Charles Orleans]";
			mes "[Recipient: 秀發茲發德共和國, 里希塔樂鎮, 德布梨絲]";
			close;
		case 3:
			mes "You do nothing.";
			close;
		}
	}
	close;
}

job3_gen01,12,56,3	script	植物圖鑑#generic	111,{
	mes "<世界上最容易的煉金術>";
	mes "包勃羅斯著";
	next;
	switch(select("動物篇:植物篇:礦物篇:其他")) {
	case 1:
		mes "...Using animals in Alchemy can easily be practiced by anyone.";
		mes "You can put the animals that you want to use on the white tray, imagine the goal you want to make and then practice the alchemy.";
		mes "It's very easy.";
		next;
		mes "...The animals that are used in Alchemy might in the danger of getting extinct because of being captured by thoughtless alchemists.";
		mes "So I won't mention them.";
		close;
	case 2:
		mes "...The guide of Alchemy using plants is easy to follow, even for a little kid.";
		mes "You can put the plants on the table, and break or cut them, connect them using a simple tool, then do alchemy.";
		mes "That's really an extraordinary method.";
		next;
		mes "...The plants usually used for Alchemy are 美枝草 and Muka Trees that have sharp thorns but are delicate inside and Bogi Creeper whose growth speed is really fast but parasitic on living things.";
		if (job_gen == 6)
			set job_gen,7;
		close;
	case 3:
		mes "...Using minerals in Alchemy is a way that beginners find pretty easy.";
		mes "You can put the minerals you want to in the beautifully crafted box, and sing any song that you can think of twice and then practice the alchemy.";
		mes "That's really an easy way.";
		next;
		mes "...A mineral that's usually used for Alchemy is 'Phracon' which doesn't cost a lot.";
		close;
	case 4:
		mes "...Alchemy is the easiest thing in the world.";
		mes "This writer shows a cobalt blue Alchemy demonstration in front of students every week, and always get a big applaud from them.";
		mes "I'm sure that you would be into the Alchemy world at the moment you read this book.";
		close;
	}
}

// QuestID,PlantName,Description{,Part?}
function	script	Genetic_Job_Plant	{
	if (job_gen == 7) {
		if (checkquest(getarg(0)) == -1 || checkquest(getarg(0)) == 0) {
			mes "- "+getarg(2)+" -";
			next;
			switch(select("用作材料:不使用")) {
			case 1:
				if (getarg(3,0)) set .@str$,"some of the ";
				mes "- You've acquired "+.@str$+getarg(1)+". -";
				setquest getarg(0);
				close;
			case 2:
				mes "- This plant doesn't look suitable as an experiment sample. -";
				close;
			}
		}
		mes "- You've already acquired "+getarg(1)+". -";
		close;
	} else if (job_gen == 6) {
		mes "Information related to this plant can be found in the Plant Guide in the cabinet.";
		close;
	}
	mes "- "+getarg(2)+" -";
	close;
}

job3_gen01,36,55,3	script	美枝草#gen	844,{
	callfunc "Genetic_Job_Plant",2209,"美枝草","雖然是個草，但體積非常巨大，被稱為美枝草。";
	end;
}
job3_gen01,36,45,3	script	Muka Tree#gen	844,{
	callfunc "Genetic_Job_Plant",2210,"Muka Tree","This is the Muka Tree which protects its delicate inside with sharp thorns.",1;
	end;
}
job3_gen01,42,55,3	script	Bogi Vine#gen	844,{
	callfunc "Genetic_Job_Plant",2211,"Bogi Vine","This is a Bogi Vine which has a very fast growth speed.",1;
	end;
}
job3_gen01,30,58,3	script	Aolatura#gen	844,{
	callfunc "Genetic_Job_Plant",2212,"Aolatura","This is known to blossom then thousand flowers as it only blooms once per lifetime.";
	end;
}
job3_gen01,30,63,3	script	Congra#gen	844,{
	callfunc "Genetic_Job_Plant",2213,"Congra","It's a plant that has a big and solid stem with enormous leaves.";
	end;
}
job3_gen01,32,38,3	script	Sticky Grass#gen	844,{
	callfunc "Genetic_Job_Plant",2214,"Sticky Grass","I may not know the name but it's sticky and can stick to just about anything.";
	end;
}

job3_gen01,7,50,3	script	實驗台#gen	844,{
	if (job_gen == 7) {
		mes "-";
		mes "放著很多工具的實驗台，好像可以把植物放上去進行組合研究。 -";
		next;
		if(select("進行實驗:不進行實驗") == 2) {
			mes "- It's not the time to start an experiment. -";
			close;
		}
		if ((checkquest(2209) == -1 || checkquest(2209) == 0) &&
		    (checkquest(2210) == -1 || checkquest(2210) == 0) &&
		    (checkquest(2211) == -1 || checkquest(2211) == 0) &&
		    (checkquest(2212) == -1 || checkquest(2212) == 0) &&
		    (checkquest(2213) == -1 || checkquest(2213) == 0) &&
		    (checkquest(2214) == -1 || checkquest(2214) == 0)) {
			mes "["+strcharinfo(0)+"]";
			mes "Hmm, I need to gather plant materials first.";
			close;
		}
		mes "要怎麼處理準備好的植物呢?";
		next;
		select("折斷:粉碎:搗碎");
		while (1) {
			mes "What shall I do now?";
			next;
			if (rand(2))
				select("燃燒做成灰:泡在蒸餾水裡:用蒸氣蒸");
			else
				select("放在實驗用的燒瓶裡搖晃:冷凍:加熱");
			if (!rand(7)) break;
		}
		mes "What shall I do now?";
		next;
		select("注入到實驗用的種子中。");
		mes "你想選擇哪個種子?";
		next;
		select("實驗用種子-A型:實驗用種子-B型:實驗用種子-C型:實驗用種子-D型");
		mes "I've put the prepared materials into the chosen testing seed.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Good. Now I can let Miss 德布梨絲 take a look at this seed!";
		if ((checkquest(2209) == -1 || checkquest(2209) == 0) &&
		    (checkquest(2210) == -1 || checkquest(2210) == 0) &&
		    (checkquest(2211) == -1 || checkquest(2211) == 0)){
			set job_gen,61;
			getitem 6272,1; //Experiment_Seed
			close;
		}
		if (checkquest(2209) == 1) set job_gen,job_gen+10;
		if (checkquest(2210) == 1) set job_gen,job_gen+10;
		if (checkquest(2211) == 1) set job_gen,job_gen+10;
		set .@score, job_gen + JobLevel - 20;
		if (rand(1,100) < .@score) {
			set job_gen,60;
			getitem 6273,1; //Altered_Seed
			close;
		} else {
			set job_gen,61;
			getitem 6272,1; //Experiment_Seed
			close;
		}
	} else if (job_gen > 7 && job_gen < 60) {
		mes "- It looks like the experiment has failed. I better start over. -";
		set job_gen,7;
		if (countitem(6273)) delitem 6273,1; //Altered_Seed
		if (countitem(6272)) delitem 6272,1; //Experiment_Seed
		if (checkquest(2209) == 1) erasequest 2209;
		if (checkquest(2210) == 1) erasequest 2210;
		if (checkquest(2211) == 1) erasequest 2211;
		if (checkquest(2212) == 1) erasequest 2212;
		if (checkquest(2213) == 1) erasequest 2213;
		if (checkquest(2214) == 1) erasequest 2214;
		close;
	} else if (job_gen == 60 || job_gen == 61) {
		mes " - It looks like I need to let Miss 德布梨絲 take a look at this seed! -";
		end;
	}
	mes "- This is a 實驗台 with a lot of equipment. I think I can study the combinations here with plants. -";
	close;
}

job3_gen01,21,67,3	script	Super Cultivator#gen	844,{
	end;
}

job3_gen01,91,48,3	script	黛米凱伯林#gen	982,{
	mes "[黛米凱伯林]";
	if (job_gen < 62) {
		mes "Hmm~ Where'd that bolt go?";
		close;
	} else if (job_gen == 62) {
		mes "我想坐在藍色的手推車上面~";
		mes "裝上美麗如畫的大砲~";
		mes "和我心愛的人一起~";
		mes "開心一輩子的砲~?";
		next;
		mes "[黛米凱伯林]";
		mes "Er~ Where's the bolt?";
		mes "Why aren't their enough screws? Didn't I order new ones just recently?";
		next;
		select("Hello~?");
		mes "[黛米凱伯林]";
		mes "Oh, my~?";
		mes "When did you... Who are you?";
		next;
		mes "[黛米凱伯林]";
		mes "Oh, my god!";
		mes "Hey! Please help me!";
		next;
		mes "[黛米凱伯林]";
		mes "I need to complete and ordered mini cannon today, but I can't find the parts!";
		next;
		mes "[黛米凱伯林]";
		mes "Ugh~ I think they dropped around here somewhere.";
		mes "I only need 10! I need to put the other parts together first... Please could you help me?!";
		changequest 2216,2217;
		set job_gen,63;
		close;
	} else if (job_gen > 62 && job_gen < 74) {
		mes "Ah, oh no, oh no~~~";
		mes "Did you find the parts? No, not yet? You promised, please hurry up!";
		close;
	} else if (job_gen == 74) {
		mes "這個手推車的火力還不夠，把這個裝在這裡的話...";
		next;
		select("零件拿來了");
		mes "[黛米凱伯林]";
		mes "Oh, gosh!";
		mes "You scared me to death!!";
		mes "Oh, are these my lost screws and bolts?";
		mes "Where did you find these?";
		next;
		select("You asked me to find them!");
		mes "[黛米凱伯林]";
		mes "Oh?, really?";
		mes "Did I? Hohohohoho.";
		mes "I don't even know where my mind is.";
		next;
		mes "[黛米凱伯林]";
		mes "Please wait a moment.";
		mes "I will be done quickly with these.";
		next;
		mes "- Snap Snap Snap -";
		next;
		mes "[黛米凱伯林]";
		mes "Okay, I've connected the cannon to the cart... Now shall we test the performance?";
		next;
		mes "- Sizlle... -";
		next;
		mes "- Pop! Pop! Pop! Pop! -";
		next;
		mes "[黛米凱伯林]";
		mes "Great! Success!";
		mes "Whew. You've been a lot of help to me.";
		mes "Thank you very much.";
		next;
		mes "[黛米凱伯林]";
		mes "Oh, by the way what made you come here?";
		mes "Did you come here to order something";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "I'm a Geneticist applicant.";
		mes "I heard that you were very knowledgeable about portable mini cannons and stuff.";
		next;
		mes "[黛米凱伯林]";
		mes "Yep, I deal with many kinds of cannons.";
		mes "Nowadays, there are those who want to remodel their carts, so I've been installing several additional functions such as mini cannons to their carts.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Cart remodeling?";
		next;
		mes "[黛米凱伯林]";
		mes "Yes, didn't you want to race out when you went downhill?";
		mes "Or haven't you ever thought that it was so boring dragging around that plain old cart?";
		next;
		mes "[黛米凱伯林]";
		mes "Carts are no longer just a wagon for storage.";
		mes "You could hang a cannon on a cart to make it help you boost your attack power.";
		next;
		mes "[黛米凱伯林]";
		mes "Miss 德布梨絲 seems to be studying childish experiments downstairs.";
		mes "I don't understand why those research papers get so popular.";
		emotion e_pif;
		next;
		mes "[黛米凱伯林]";
		mes "請看看這個超級迷你加農炮。";
		mes "只有手掌大小，是世界上最輕、最小的加農炮。";
		mes "你不覺得它很可愛嗎? 啊-";
		emotion e_awsm;
		next;
		select("我的手推車也能進行改造嗎?");
		mes "[黛米凱伯林]";
		mes "Unfortunately, the cart remodeling is only allowed for Geneticists.";
		mes "It's a rule from the Alchemist Union, so I can't help it.";
		next;
		mes "[黛米凱伯林]";
		mes "But don't get disappointed.";
		mes "You can be a Geneticist too!";
		mes "Hohoho.";
		next;
		mes "[黛米凱伯林]";
		mes "And what's more, you can?t so the cart remodeling to just any cart.";
		mes "It can only be done on quality certified carts that've been polished for a long time.";
		next;
		mes "[黛米凱伯林]";
		mes "You should practice your cart skills whenever you have time until you become a Geneticist.";
		mes "Till your muscles are dealing with your cart in powerful harmony and your cart moves beautifully,";
		next;
		mes "[黛米凱伯林]";
		mes "If you want, ^FF0000I'll show you a good training spot.^000000";
		mes "^FF0000德布梨絲 won't like it much^000000, but it's the best place. Haha.";
		next;
		mes "[黛米凱伯林]";
		mes "趁這次機會，你去盡情讓你的手推車飛奔吧!";
		mes "你去用手推車打飛";
		mes "^FF0000100個左右的怪物^000000，緩解一下疲勞，熟悉一下手推車。";
		next;
		mes "[黛米凱伯林]";
		mes "Well, I need to get to work for the next order.";
		mes "Come and find me whenever you need training.";
		mes "I'll help you out whenever you need your cart remodeled.";
		next;
		mes "- Rattle -";
		next;
		mes "- What's this? I feel like the floor is missing somewhere. -";
		set job_gen,75;
		changequest 2217,2223;
		close2;
		warp "job3_gen01",23,32;
		end;
	} else if (job_gen == 75) {
		if (checkquest(2223,HUNTING) == 2) {
			mes "德布梨絲好像發現是我把你送進圍欄的了。看著她的那個表情，感覺真的很不錯。真可惜。";
			close;
		}
		mes "Sigh, haven't you finished practicing?";
		mes "Do you want to enter the spore pen?";
		next;
		switch(select("Yes.:No.")) {
		case 1:
			mes "[黛米凱伯林]";
			mes "Have fun swinging your cart around~";
			close2;
			warp "job3_gen01",23,32;
			end;
		case 2:
			mes "[黛米凱伯林]";
			mes "What a shame! I wanted to see Miss 德布梨絲's angry face.";
			close;
		}
	} else if (job_gen > 75) {
		mes "It seem Miss 德布梨絲 found out that I put you in her spore pen.";
		mes "It's funny when you look at her face. After you've killed all those spores.";
		close;
	}
}

-	script	散亂的東西#gen	-1,{
	if (job_gen > 62 && job_gen < 74) {
		mes "- 地上散亂著各種東西，實驗室裡一片狼藉 -";
		next;
		mes "- You found a little metal fragment. -";
		set job_gen, job_gen+1;
		specialeffect EF_BASH;
		disablenpc strnpcinfo(0);
		initnpctimer;
		close;
	} else if (job_gen == 74) {
		mes "- I think I've gathered all of the parts. -";
		close;
	}
	end;
OnTimer7000:
	enablenpc strnpcinfo(0);
	stopnpctimer;
	end;
}
job3_gen01,84,45,3	duplicate(散亂的東西#gen)	散亂的東西#gen1	111
job3_gen01,77,41,3	duplicate(散亂的東西#gen)	散亂的東西#gen2	111
job3_gen01,78,57,3	duplicate(散亂的東西#gen)	散亂的東西#gen3	111
job3_gen01,89,59,3	duplicate(散亂的東西#gen)	散亂的東西#gen4	111
job3_gen01,90,51,3	duplicate(散亂的東西#gen)	散亂的東西#gen5	111
job3_gen01,83,27,3	duplicate(散亂的東西#gen)	散亂的東西#gen6	111
job3_gen01,74,45,3	duplicate(散亂的東西#gen)	散亂的東西#gen7	111

job3_gen01,18,39,3	script	警告#gen	837,{
	mes "= 警告 =";
	mes "如果你碰了我的蘑菇，我會對著你的耳朵尖叫的!!!";
	if (job_gen < 75) close;
	next;
	switch(select("到圍欄外面去:待著不動")) {
	case 1:
		mes "- I'd better get out before I get caught by Miss 德布梨絲. -";
		close2;
		warp "job3_gen01",21,38;
		end;
	case 2:
		mes "- I decide to stay still. -";
		close;
	}
}

job3_gen01,80,12,1	script	Stacked Magazines#gen	111,{
	mes "- 'Monthly Bang!' magazines are piled up in disorder. -";
	close;
}

job3_gen01,83,20,1	script	Bed#gen	111,{
	mes "- A checker patterned blanket set which looks like it was picked carefully is too good for this dirty bed. -";
	close;
}

job3_gen01,85,25,1	script	Opened Book#gen	111,{
	mes "< Do you want to have my pretty room? Do you have enough zeny? Is there enough room? Run to the Lighthalzen Department Store right away and get the most high-end wall papers and interior accessories! >";
	next;
	mes "< Luxurious candles that'll lighten up your room, a huge golden mirror that every lady owns, you cannot miss any of them! >";
	close;
}

job3_gen01,81,81,0	script	#dbroom	139,2,2,{
OnTouch:
	mes "- It's a room that looks like a storehouse, thanks to so many parcels piled up in the room. -";
	close;
}

job3_gen01,24,32,0	script	The gaze of 德布梨絲	139,2,2,{
OnTouch:
	if (checkquest(2223,HUNTING) == 2) {
		mapannounce "job3_gen01","德布梨絲: Whoa? Was there a lost kitty in the spore pen?",bc_map,"0x99CC00"; //FW_NORMAL 12 0 0
		emotion e_omg,1;
		warp "job3_gen01",21,38;
	}
	end;
}

job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,26,34,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,23,33,0,0	monster	毒魔菇	1428,1,7000,0,0
job3_gen01,21,30,0,0	monster	毒魔菇	1428,1,7000,0,0