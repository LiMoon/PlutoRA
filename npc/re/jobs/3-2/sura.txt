//===== rAthena Script ======================================= 
//= Sura Job Quest
//===== By: ================================================== 
//= Muad_Dib, Gepard
//===== 中文化 ================================================
//= 冥王RA-PlutoRA
//= XD人
//===== Current Version: ===================================== 
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job change Quest from Monk / Champion -> Sura.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Masao]
//= 1.1 Removed disablenpc which causes players stucked in waitingroom. [Joseph]
//= 1.2 Updated script, optimized. [Euphy]
//= 1.3 Added GM management function. [Euphy]
//= 1.4 Added VIP features. [Euphy]
//============================================================

ve_in,237,125,0	script	金克雷#job_shu	107,{
	if (job_shu == 0) {
		if (BaseJob == Job_Monk && BaseLevel >= 99 && JobLevel >= 50) {
			set .@job_check,1;
			if (VIP_SCRIPT) {
				callfunc "VIP_Third_Class";
				set job_shu,1;
				setquest 11155;
				next;
			}
		}
		mes "[金克雷]";
		mes "哈哈哈哈哈哈。";
		mes "那個小傢伙一拳就躺下了!";
		next;
		mes "[斯拉吉沃]";
		mes "那種沒禮貌的東西就應該好好教訓一下!";
		mes "竟敢惹我們大哥!";
		next;
		if (!.@job_check) {
			mes "[金克雷]";
			mes "哈哈哈哈哈哈!!!!";
			mes "再來一杯啤酒!!!";
			mes "今天一定要喝得盡興!!!";
			next;
			mes "[斯拉吉沃]";
			mes "金克雷大哥!!!我會一輩子跟著你的!!!!";
			close;
		}
		mes "[金克雷]";
		mes "哈哈哈哈哈哈!!!!";
		next;
		mes "[斯拉吉沃]";
		mes "喂!服務員!!!再來一杯啤酒!!!";
		mes "我們大哥的杯子空了，還不趕快端過來!";
		next;
		mes "[酒店女服務生]";
		mes "好，馬上就來。";
		next;
		mes "[斯拉吉沃]";
		mes "太晚了!!!你知道我們大哥是什麼人嗎?嗯!?";
		next;
		mes "[酒店女服務生]";
		mes "對...對不起...";
		next;
		mes "[金克雷]";
		mes "呵呵呵，算了算了。";
		mes "你這麼凶神惡煞的，把可愛的小姐都嚇著了，";
		mes "小姐~我們不是那麼可怕的人~";
		next;
		mes "[酒店女服務生]";
		mes "啊!請別這樣!!";
		next;
		mes "- 噹 -";
		donpcevent "???#bcmd::OnEnable";
		next;
		mes "[???]";
		mes "為。";
		mes "店裡面到處都是豬叫的聲音，我都快受不了了";
		next;
		mes "[???]";
		mes "吵死了。那種廢話，你們到外面去說就好了。";
		next;
		mes "[斯拉吉沃]";
		mes "什麼?!";
		mes "你是什麼東西?竟敢讓我們出去!";
		mes "你知道我們大哥是誰嗎!!";
		next;
		mes "[斯拉吉沃]";
		mes "他就是大名鼎鼎的!!!";
		mes "孩子都嚇到不敢哭的山賊金克雷!!!";
		next;
		mes "[???]";
		mes "長得那麼骯髒，做的也是骯髒的事情，倒是滿相配的嘛!";
		mes "在世界面臨危機的時候，卻只顧著自己填飽肚子，那就是所謂的山賊。";
		next;
		mes "[???]";
		mes "也是...如果有點頭腦的話，就不會在這裡地方說這種話了。";
		next;
		mes "[金克雷]";
		mes "什麼!";
		mes "你這賤人還真敢說，";
		mes "我可不會因為對手是女人而手下留情的。";
		next;
		mes "[斯拉吉沃]";
		mes "哼!大哥真的發火了!";
		mes "像你這種女人，一拳頭就......";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "喂!你們幾個男人，怎麼對小姐這麼沒禮貌!!";
		next;
		mes "[???]";
		mes "讓開，小孩......";
		next;
		mes "[???]";
		mes "你能這麼說，我挺高興的。";
		mes "我正在修行，不能先發動攻擊。";
		next;
		mes "[金克雷&斯拉吉沃]";
		mes "嗯?";
		next;
		mes "[金克雷&斯拉吉沃]";
		mes "哦?!";
		next;
		mes "[金克雷&斯拉吉沃]";
		mes "呃啊啊啊啊啊啊啊!!!!!!";
		specialeffect EF_FIRESPLASHHIT,AREA,"金克雷#job_shu";
		specialeffect EF_FIRESPLASHHIT,AREA,"斯拉吉沃#job_shu";
		next;
		mes "- 怎麼回事?那種力量?? -";
		mes "- 那麼高大的人一下子就打飛了......? -";
		mes "- 好像是從來沒看過的 -";
		mes "- 技能......? -";
		next;
		mes "[???]";
		mes "真掃興...";
		next;
		mes "[酒店女服務生]";
		mes "真...真是謝謝你...";
		mes "真不知道該怎麼報答你。";
		next;
		mes "[???]";
		mes "區區小事何足掛齒。";
		next;
		mes "[酒店女服務生]";
		mes "你要回去了嗎?";
		mes "要不要我把菜給你端上來?";
		next;
		mes "[???]";
		mes "不用太在意。";
		mes "給我來個麵包，來碗熱湯就行了，";
		mes "再見!";
		donpcevent "???#bcmd::OnDisable";
		next;
		mes "[酒店女服務生]";
		mes "好的......";
		mes "......";
		mes "......";
		mes "......(暖烘烘~)";
		set job_shu,1;
		setquest 11155;
		close;
	} else {
		if (strnpcinfo(0) == "金克雷#job_shu") {
			mes "[金克雷]";
			mes "竟然一下子就把我打飛了......";
			next;
			mes "[金克雷]";
			mes "不行，我被她迷住了......";
		} else {
			mes "[斯拉吉沃]";
			mes "大哥...現在我們混不下去了嗎?";
			mes "嗚嗚。";
		}
		close;
	}
}
ve_in,241,128,4	duplicate(金克雷#job_shu)	斯拉吉沃#job_shu	110

ve_in,244,126,3	script	酒店女服務生#job_shu	69,{
	mes "[酒店女服務生]";
	if (job_shu == 0) {
		mes "唉~";
		mes "最討厭那種吵吵鬧鬧、沒禮貌的客人了!";
		mes "要是有人能教訓他們就好了。";
		close;
	} else if (job_shu == 1) {
		if (BaseJob == Job_Monk) {
			mes "哎呀~";
			mes ""+jobname(Class)+"先生";
			mes "謝謝你剛才幫我。";
			next;
		}
		mes "[酒店女服務生]";
		mes "啊...你是說剛才那個人嗎?";
		mes "幾天前，她就來到了這裡。";
		mes "很帥吧?";
		next;
		mes "[酒店女服務生]";
		mes "雖然都是女人，但我都快被她迷住了。";
		mes "啊~那美妙的身姿...";
		next;
		mes "[酒店女服務生]";
		mes "啊!!";
		mes "現在不是胡思亂想的時候!!!";
		mes "我得趕快做好料理，給她送過去才行~";
		next;
		mes "[酒店女服務生]";
		mes "如果你還沒找到住的地方，就住到^f57d7d樓上的旅館^000000裡好了。";
		close;
	}
	mes "那些客人這下終於安靜了。";
	mes "哎呀。這下好多了!!";
	close;
}

ve_in,240,131,0	script	???#bcmd	484,{
	end;
OnInit:
	disablenpc "???#bcmd";
	end;
OnEnable:
	enablenpc "???#bcmd";
	initnpctimer;
	end;
OnDisable:
	disablenpc "???#bcmd";
	stopnpctimer;
	end;
OnTimer600000:
	donpcevent "???#bcmd::OnDisable";
	stopnpctimer;
	end;
}

ve_in,97,149,0	script	#Suraryokan	139,3,3,{
OnTouch:
	if (job_shu == 1) {
		set .@n$, "["+strcharinfo(0)+"]";
		mes "[???]";
		mes "誰啊!";
		next;
		mes .@n$;
		mes "我是剛才在酒店裡的人";
		mes "我想問你點事情。";
		next;
		mes "[???]";
		mes "煩死了，走開。";
		next;
		mes .@n$;
		mes "別...別這樣嘛。跟我...(喀?)";
		next;
		mes "[???]";
		mes "......";
		next;
		mes .@n$;
		mes "不要看不起我，跟我說說話嘛。";
		next;
		mes .@n$;
		mes "一下下就好了。";
		next;
		mes .@n$;
		mes "我不會害你";
		next;
		mes .@n$;
		mes "怎麼能這麼對待找上門的客人呢...";
		next;
		mes .@n$;
		mes "真是太過分了。";
		next;
		mes .@n$;
		mes "看了剛才你的格鬥";
		next;
		mes .@n$;
		mes "我看你好像身懷絕技，所以才特地來找你的。";
		next;
		mes .@n$;
		mes "這樣拒絕人，";
		next;
		mes .@n$;
		mes "好像和強調禮節的武術精神不太相符吧。";
		next;
		mes "[???]";
		mes "真是個頑固的傢伙......";
		next;
		mes "[???]";
		mes "你想得到什麼啊?";
		set job_shu,2;
		close;
	}
	end;
}

ve_in,98,159,0	script	格鬥家#job_shu	484,{
	if (job_shu < 2) {
		mes "[格鬥家]";
		mes "啊~好累。";
		mes "修行之路是遙遠又艱辛的!";
		close;
	} else if (job_shu == 2) {
		mes "["+strcharinfo(0)+"]";
		mes "我想知道你剛才用的是什麼技能。";
		next;
		mes "[???]";
		mes "...";
		mes "...";
		mes "...";
		mes "...你這種小朋友知道了又能怎麼樣?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我不是小朋友!";
		mes "我已經一直在刻苦修行武藝。";
		mes "我有自信不會輸給任何人。";
		next;
		mes "[???]";
		mes "也不會輸給我?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "...不...";
		mes "那可能有點難...";
		mes "老實說我挺震驚的。";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我一直認為自己很強。但看了你的戰鬥之後，我知道我之前太高估我自己了。";
		next;
		mes "[???]";
		mes "嘿~你能強到什麼地方去啊?";
		mes "還不是到達了極限，只能在原地徘徊。";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "...我果然沒有看錯";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我想你一定知道。";
		mes "突破界限的方法。";
		next;
		mes "[???]";
		mes "夠了。";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "嗯? 為什麼?";
		next;
		mes "[???]";
		mes "你是人類嗎?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "嗯?";
		mes "怎麼突然說出這種莫名其妙的話......";
		mes "你也看到了，我當然是人類。";
		next;
		mes "[???]";
		mes "人類的話，要比你現在變得更強是很難的。";
		mes "如果你想變得更強，就去買更好的裝備吧。";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "那你是什麼?";
		mes "你不是人類嗎?";
		next;
		mes "[???]";
		mes "超越界限......";
		mes "那不是人類的道路，";
		mes "而是鬼神的道路......";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "鬼神?";
		next;
		mes "[???]";
		mes "選擇這條道路之後，我就放棄身為人類的身分。";
		mes "我放棄了神的庇護，踏上了修羅之路。";
		next;
		mes "[???]";
		mes "選擇這條道路之後，就不再有片刻的安逸。";
		mes "只是成為單純的戰爭機器......";
		next;
		switch(select("我還是想知道:那就算了")) {
		case 1:
			mes "[???]";
			mes "你是傻瓜嗎?";
			mes "成為鬼神有什麼好的?";
			next;
			mes "[???]";
			mes "你100%會後悔的，還是好好過你現在的生活吧。";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "如果不知道就算了。但是看到你之後，我就再也忘不掉了!";
			next;
			mes "[???]";
			mes "啊...真是個煩人的傢伙。";
			next;
			mes "[???]";
			mes "好吧，^f57d7d你先去捕捉100之沙漠之狼^000000，";
			mes "這樣我就考慮一下。";
			set job_shu,3;
			changequest 11155,11156;
			close;
		case 2:
			mes "[???]";
			mes "明智的決定。";
			mes "沒有必要自找苦吃。";
			close;
		}
	} else if (job_shu == 3) {
		if (checkquest(11156,HUNTING) == 2) {
			mes "[???]";
			mes "啊~";
			mes "怎麼?你真的去抓了嗎?";
			mes "我本來是想為難你一下。";
			next;
			mes "[???]";
			mes "一言抵千金!!";
			mes "雖然我沒想到你真的會去做，所以才答應你。但約定就是約定。";
			next;
			mes "[???]";
			mes "你到^f57d7d朱諾南部的爾邁斯峽谷的山谷^000000中去";
			mes "我的師弟^f57d7d博迪^000000應該在那邊修行";
			next;
			mes "[布魯諾]";
			mes "你說是'布魯諾'讓你去找他的。接下去的事情他會看著辦。";
			set job_shu,4;
			changequest 11156,11157;
			close;
		}
		mes "[???]";
		mes "Hah... you're really making me tired.";
		next;
		mes "[???]";
		mes "Okay, then^f57d7d go and kill 100 Desert Wolves.^000000";
		mes "Then I'll think about it again.";
		close;
	} else if (job_shu == 4) {
		mes "[布魯諾]";
		mes "你到^f57d7d朱諾南部的爾邁斯峽谷的山谷^000000中去";
		mes "我的師弟^f57d7d博迪^000000應該在那邊修行";
		next;
		mes "[布魯諾]";
		mes "你說是'布魯諾'讓你去找他的。接下去的事情他會看著辦。";
		close;
	} else if (job_shu == 100) {
		mes "[布魯諾]";
		mes "喂~師弟!";
		mes "最近怎麼樣?";
		next;
		mes "[布魯諾]";
		mes "沒什麼禮物要送給我這個師兄嗎?";
		close;
	}
	mes "[布魯諾]";
	mes "I recommend you to reconsider being a Sura.";
	mes "Sura is strong and cool, but that's not all.";
	next;
	mes "[布魯諾]";
	mes "Me?";
	mes "If I need to choose a job again, I will definitely choose Sura.";
	close;
}

yuno_fild07,254,176,7	script	博迪#job_shu	753,{
	if (job_shu < 4) {
		mes "[博迪]";
		mes "哈!!哈!!哈!!哈!!";
		next;
		mes "[博迪]";
		mes "雖然不知道你來找我是為了什麼，但如果你想留住自己的性命，就不要待在我身邊。";
		close;
	} else if (job_shu == 4) {
		mes "[博迪]";
		mes "哈!!哈!!哈!!哈!!";
		next;
		mes "[博迪]";
		mes "雖然不知道你來找我是為了什麼，但如果你想留住自己的性命，就不要待在我身邊。";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "是'布魯諾'叫我來的。";
		next;
		mes "[博迪]";
		mes "啊，是師兄讓你來的啊。";
		mes "找我有什麼事情嗎?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我問布魯諾她的力量是怎麼回事，她讓我來找你。";
		next;
		mes "[博迪]";
		mes "那個麻煩的女人...啊，不，呵呵。";
		mes "既然是難伺候的布魯諾師兄介紹的，看來你應該不是普通人。";
		next;
		mes "[博迪]";
		mes "我也正在修行，無法回答你的問題。我會把你介紹給我們的師父。";
		next;
		mes "[博迪]";
		mes "師父不喜歡吵鬧^f57d7d請你先在接待室裡等一下^000000";
		next;
		mes "[博迪]";
		mes "請跟我來......";
		set job_shu,5;
		changequest 11157,11158;
		close2;
		warp "sword_1-1",215,244;
		end;
	} else if (job_shu == 100) {
		mes "[博迪]";
		mes "啊，"+strcharinfo(0)+"，好久不見";
		mes "你是來見師父的嗎?";
		next;
		switch(select("是的:不")) {
		case 1:
			mes "[博迪]";
			mes "我馬上帶你過去";
			close2;
			warp "sword_1-1",216,168;
			end;
		case 2:
			mes "[博迪]";
			mes "那你來這裡有什麼事嗎?";
			mes "難...難道是...來見我的?!";
			close;
		}
	}
	mes "[博迪]";
	mes strcharinfo(0)+"你還沒見到師父嗎?";
	next;
	mes "[博迪]";
	mes "師父不喜歡吵鬧^f57d7d請你先在接待室裡等一下^000000";
	next;
	mes "[博迪]";
	mes "請跟我來......";
	close2;
	warp "sword_1-1",215,244;
	end;
}

sword_1-1,223,243,4	script	接待室	753,{
	end;
OnInit:
	waitingroom "接待室",20,"接待室::OnStartArena",1;
	enablewaitingroomevent;
	end;
OnStartArena:
	warpwaitingpc "sword_2-1",223,205;
	donpcevent "博迪#Sura_Salon::OnEnable";
	disablewaitingroomevent;
	end;
OnEnable:
	enablewaitingroomevent;
	end;
OnDisable:
	disablewaitingroomevent;
	end;
}

sword_1-1,223,243,4	script	博迪#job_shu_Salong	753,{
	mes "[博迪]";
	mes "師父不喜歡吵鬧^f57d7d請你先在接待室裡等一下^000000";
	next;
	switch(select("去接待室:離開")) {
	case 1:
		mes "[博迪]";
		mes "好的，你去上面的的接待室等一下。";
		close;
	case 2:
		mes "[博迪]";
		mes "再見!";
		close2;
		warp "yuno_fild07",255,178;
		end;
	}
}

sword_2-1,1,1,0	script	博迪#Sura_Salon	66,{
	end;
OnInit:
	disablenpc "博迪#Sura_Salon";
	end;
OnEnable:
	enablenpc "博迪#Sura_Salon";
	initnpctimer;
	end;
OnReset:
	killmonster "sword_2-1","博迪#Sura_Salon::OnMyMobDead";
	end;
OnDisable:
	disablenpc "博迪#Sura_Salon";
	stopnpctimer;
	end;
OnMyMobDead:
	if (mobcount("sword_2-1","博迪#Sura_Salon::OnMyMobDead") < 1) {
		monster "sword_2-1",219,210,"幻覺",1479,1,"博迪#Sura_Salon::OnMyMobDead";
		monster "sword_2-1",228,209,"幻覺",1483,1,"博迪#Sura_Salon::OnMyMobDead";
		monster "sword_2-1",228,201,"幻覺",1479,1,"博迪#Sura_Salon::OnMyMobDead";
		monster "sword_2-1",219,201,"幻覺",1483,1,"博迪#Sura_Salon::OnMyMobDead";
	}
	end;
OnTimer3000:
	mapannounce "sword_2-1","博迪: 師父正在修行，您可能要再等5分鐘左右。",bc_map;
	end;
OnTimer4000:
	mapannounce "sword_2-1","博迪: 啊!在接待室中可能會看到幻覺，請你小心。",bc_map;
	end;
OnTimer5000:
	mapannounce "sword_2-1","博迪: 這是師父設置的，請不要覺得不便。",bc_map;
	end;
OnTimer33000:
	monster "sword_2-1",219,210,"幻覺",1480,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"唉，難道",1479,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"幻覺",1446,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"錯覺",1483,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"幻覺",1480,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"幻象",1479,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"幻覺",1446,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"錯覺",1483,1,"博迪#Sura_Salon::OnMyMobDead";
	end;
OnTimer93000:
	monster "sword_2-1",219,210,"幻覺",1480,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"錯覺",1479,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"幻覺",1446,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"幻覺",1483,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"幻覺",1480,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"幻覺",1446,1,"博迪#Sura_Salon::OnMyMobDead";
	end;
OnTimer153000:
	mapannounce "sword_2-1","博迪: 等得太無聊的話，要我給你一本雜誌嗎?",bc_map;
	monster "sword_2-1",219,210,"雜誌",1478,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"雜誌",1478,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"雜誌",1478,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"雜誌",1478,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"雜誌",1478,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"雜誌",1478,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"雜誌",1478,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"雜誌",1478,1,"博迪#Sura_Salon::OnMyMobDead";
	end;
OnTimer213000:
	monster "sword_2-1",219,210,"幻覺",1480,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"別在意",1479,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"幻覺",1483,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"幻覺",1480,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"你看到我了?",1479,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"幻覺",1483,1,"博迪#Sura_Salon::OnMyMobDead";
	end;
OnTimer273000:
	monster "sword_2-1",219,210,"幻覺",1480,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"幻覺",1446,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"幻覺",1480,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"幻覺",1479,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"幻覺",1446,1,"博迪#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"幻覺",1483,1,"博迪#Sura_Salon::OnMyMobDead";
	end;
OnTimer300000:
	mapannounce "sword_2-1","博迪: 師父到了，我馬上把你帶到師父的房裡去。",bc_map;
	end;
OnTimer303000:
	donpcevent "博迪#job_shuaneh::OnEnable";
	end;
OnTimer305000:
	donpcevent "博迪#Sura_Salon::OnReset";
	end;
OnTimer315000:
	mapannounce "sword_2-1","博迪: 請快到這邊來。",bc_map;
	donpcevent "博迪#job_shuaneh::OnDisable";
	donpcevent "#Sura_garajjom::OnEnable";
	end;
OnTimer320000:
	mapannounce "sword_2-1","博迪: I guess you're not ready to meet the 師父 yet...",bc_map;
	donpcevent "#Sura_garajjom::OnDisable";
	donpcevent "接待室::OnEnable";
	mapwarp "sword_2-1","yuno_fild07",255,178;
	stopnpctimer;
	end;
}

sword_2-1,223,205,7	script	博迪#job_shuaneh	753,{
	if (job_shu > 4) {
		mes "I'll show you the way. This way.";
		close2;
		warp "sword_1-1",216,168;
		donpcevent "博迪#job_shuaneh::OnDisable";
		end;
	}
	mes "Why are you here?";
	close2;
	warp "yuno_fild07",248,179;
	end;
OnInit:
OnDisable:
	disablenpc "博迪#job_shuaneh";
	end;
OnEnable:
	enablenpc "博迪#job_shuaneh";
	end;
}

sword_2-1,223,205,0	script	#Sura_garajjom	139,10,10,{
OnTouch:
	warp "sword_1-1",216,168;
	end;
OnInit:
OnDisable:
	disablenpc "#Sura_garajjom";
	end;
OnEnable:
	enablenpc "#Sura_garajjom";
	end;
}

sword_1-1,222,169,5	script	布魯諾#job_shu	484,{
	if (job_shu > 4) {
		mes "[布魯諾]";
		mes "嗨~"+strcharinfo(0)+"，有什麼事嗎?";
		next;
		switch(select("對話:離開")) {
		case 1:
			if (job_shu == 100) {
				mes "[布魯諾]";
				mes "我們師父?";
				mes "雖然他說自己已經放棄俗世，成為鬼神，但其他還是對人世充滿了迷戀。";
				next;
				mes "[布魯諾]";
				mes "就算成為了修羅，仍需要不斷地修行，不斷和其他人交流~";
				next;
				mes "[布魯諾]";
				mes "師父在成為修羅之前有個非常要好的朋友，決心成為修羅之後，";
				next;
				mes "[布魯諾]";
				mes "他給那們朋友寫了一封信，說自己已經'死了'。";
				mes "但是他偶爾會偷偷地到'充滿回憶之處'去。";
				next;
				mes "[布魯諾]";
				mes "其實他可以告訴朋友自己沒有死，然後去見他。";
				mes "雖然是我們的師父，我不應該這麼說，但確實固執得要死。";
				next;
				mes "[布魯諾]";
				mes "不過他倒是挺帥氣的就是。";
				mes "呵呵";
				next;
				mes "[布魯諾]";
				mes "我一直在各地旅行，其實也是為了尋找師父的'那個好朋友'，而不是純粹地為了修行。";
				next;
				mes "[布魯諾]";
				mes "我只是覺得太無聊，所以才會這樣做。";
				mes "絕...絕不是為了師父!";
				close;
			}
			mes "[布魯諾]";
			mes "喂~我還以為你沒見到師父你就會離開，沒想到居然讓你來到了這裡。";
			next;
			mes "[布魯諾]";
			mes "既然讓你來到了這裡，就是我的師弟了，快叫我師兄。";
			next;
			mes "[布魯諾]";
			mes "在我們的世界哩，年齡只不過是個數字!";
			close;
		case 2:
			mes "[布魯諾]";
			mes "你可以隨時過來~";
			mes "我會讓你看看我的死亡訓練課程的~";
			close2;
			warp "yuno_fild07",255,178;
			end;
		}
	}
	mes "[布魯諾]";
	mes "What are you?";
	mes "How did you find this place!";
	mes "Go away!!!";
	close2;
	warp "yuno_fild07",255,178;
	end;
}

sword_1-1,223,167,2	script	師父#job_shu	483,{
	mes "[師父]";
	if (job_shu == 5) {
		mes "居然被你找到我的房間，看來你還不錯嘛。";
		next;
		mes "[師父]";
		if (SkillPoint != 0) {
			mes "但是不放棄過去的技能點數，是無法成為修羅的。";
			close;
		}
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 3500) {
			mes "You seem to be heavily burdened. Please remove some of it.";
			close;
		}
		if (BaseLevel < 99 || JobLevel < 50) {
			mes "You don't seem to have reached the limit of a human, you still have far to go in order to become Sura.";
			mes "Please go practice some more.";
			close;
		}
		if (BaseJob != Job_Monk) {
			mes "Hmm?";
			close;
		}
		mes "[布魯諾]";
		mes "那當然~";
		mes "我的眼睛一向很準。";
		next;
		mes "[布魯諾]";
		mes "還有師父!這是狼毛做成的大衣。";
		mes "請你天冷的時候穿上。";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "啊，那是我抓......呃。";
		next;
		mes "- 砰 -";
		next;
		mes "[布魯諾]";
		mes "呵呵呵~";
		mes ""+strcharinfo(0)+"師弟，你有什麼話要說嗎?";
		mes "以後大家要和睦相處哦~";
		next;
		mes "[師父]";
		mes "好吧，其他的弟子好像都已經認同你，我就收你為徒吧!";
		next;
		mes "[師父]";
		mes "既然你能來到這個地方，就應該知道，我們修羅的道路充滿了艱辛。";
		next;
		mes "[師父]";
		mes "我已經切斷了和俗世的聯繫，當世界陷入戰爭而需要鬼神的時候，我們才會重新回到俗世。";
		next;
		mes "[師父]";
		mes "希望那樣的時刻永遠不要到來...但是要是真的有那麼一天，我們絕不能有一絲猶豫，一定要變成鬼神，消滅敵人。";
		next;
		if (ismounting()) {
			mes "[師父]";
			mes "So... please get down from your riding pet!";
			mes "It is a loss of etiquette on this situation.";
			close;
		}
		mes "[師父]";
		mes "為此，一定要勤於修練。";
		set job_shu,100;
		jobchange roclass(eaclass()|EAJL_THIRD);
		completequest 11158;
		getitem 5754,1; //Blazing_Soul
		getitem 2795,1; //Green_Apple_Ring
		close;
	} else if (job_shu > 5) {
		mes "我也曾經是一個平凡的青年，在世界各地旅行，結識各路好友。";
		next;
		mes "[師父]";
		mes "但是就像你一樣，我也會有瓶頸。";
		next;
		mes "[師父]";
		mes "偶爾我也會想，有時你不得不放棄所有的東西，來讓自己變得更強。";
		next;
		mes "[師父]";
		mes "當然，現在後悔也來不及了...";
		close;
	}
	mes "This is not a place where you can stay.";
	mes "Please go back.";
	close2;
	warp "yuno_fild07",255,178;
	end;
}
//GM用NPC，大概是要測試用的
sword_2-1,1,2,0	script	Button Girl#sura	66,{
	callfunc "F_GM_NPC";
	switch(select("Turn on arena.:Turn off arena.:Turn on the living room.:Turn on 博迪.:Turn on Gara.:Turn off all.:Close")) {
	case 1:
		mes "Turn on arena.";
		donpcevent "Drawing Room::OnEnable";
		close;
	case 2:
		mes "Turn off arena.";
		donpcevent "Drawing Room::OnDisable";
		close;
	case 3:
		mes "Turn on the living room.";
		donpcevent "博迪#Sura_Salon::OnEnable";
		close;
	case 4:
		mes "Turn on 博迪.";
		donpcevent "博迪#job_shuaneh::OnEnable";
		close;
	case 5:
		mes "Turn on Gara.";
		donpcevent "#Sura_garajjom::OnEnable";
		close;
	case 6:
		mes "Turn off all.";
		donpcevent "Drawing Room::OnDisable";
		donpcevent "博迪#Sura_Salon::OnReset";
		donpcevent "博迪#Sura_Salon::OnDisable";
		donpcevent "博迪#job_shuaneh::OnDisable";
		donpcevent "#Sura_garajjom::OnDisable";
		close;
	case 7:
		close;
	}
}
