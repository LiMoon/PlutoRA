//===== rAthena Script ======================================= 
//= Sorcerer Job Quest
//===== By: ================================================== 
//= Muad_Dib
//===== 中文化 ================================================
//= 冥王RA-PlutoRA
//= XD人
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job change Quest from Sage / Professor -> Sorcerer.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Masao]
//= 1.1 Cleaning. [Euphy]
//= 1.2 Updated script, optimized. [Euphy]
//= 1.3 Added VIP features. [Euphy]
//============================================================
gef_tower,102,34,5	script	梅利托	742,{
	mes "[梅利托]";
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "您持有的物品太重而無法繼續進行，請減輕重量之後再來挑戰吧!";
		close;
	}
	if (Class == Job_Sorcerer || Class == Job_Sorcerer_T || Class == Job_Baby_Sorcerer) {
		mes "不要和元素精靈打架，要好好的相處~知道了吧~！";
		close;
	}
	if (BaseLevel < 99 || JobLevel < 50 || SkillPoint != 0) {
		mes "啊~你還是血氣方剛的年輕人啊!真是羨慕你。";
		close;
	}
	if (BaseJob != Job_Sage) {
		mes "啊~你還是血氣方剛的年輕人啊!真是羨慕你。";
		close;
	}
	if (job_soc == 0) {
		mes "歡迎光臨!我可以幫你什麼嗎?";
		next;
		select("聽說能獲得妖術師的訊息，所以我才來找你。");
		mes "[梅利托]";
		mes "作一個學者，難道不是更好嗎?";
		mes "妖術師要和元素精靈培養感情，所以沒什麼時間看書......";
		next;
		mes "[梅利托]";
		mes "是什麼事情讓你決定成為妖術師的呢?";
		next;
		if(select("火元素精靈引導了我:水元素精靈引導了我:地元素精靈引導了我:肉元素精靈引導了我") == 4) {
			mes "[梅利托]";
			mes "有那種元素精靈嗎?";
			close;
		}
		mes "[梅利托]";
		mes "是嗎?";
		mes "還沒開始，就能跟元素精靈交流?要不你就擁有過人的資質，要不就是世紀大騙子。";
		next;
		mes "[梅利托]";
		mes "嗯......雖然不是很重要，但你能先把名字寫在這邊的名冊上嗎?";
		next;
		if(select("寫下名字:不寫名字") == 2) {
			mes "[梅利托]";
			mes "那就算了，下次有機會再見!";
			close;
		}
		mes "[梅利托]";
		mes "你的名字是[" + strcharinfo(0) + "]啊!";
		next;
		mes "[梅利托]";
		mes "我要為你進行登記，請稍候再跟我說話。";
		set job_soc,1;
		close;
	} else if (job_soc == 1) {
		mes "好的，讓我來跟你說明一下吧。";
		next;
		mes "[梅利托]";
		mes "[" + strcharinfo(0) + "]，你應該可以熟練的使用魔法吧?";
		next;
		if(select("是的:不，我不熟魔法") == 2) {
			mes "[梅利托]";
			mes "嗯?!你不知道怎麼使用魔法?請你馬上出去，馬上！";
			close;
		}
		mes "[梅利托]";
		mes "好的，這些其實都不用問。";
		mes "妖術師職業當然必須要會使用魔法，但是和其他魔法系職業存在一些差異。你知道是什麼嗎?";
		next;
		switch(select("可以製造水和麵包:在90歲之前不能談戀愛:可以操控元素精靈")) {
		case 1:
			mes "[梅利托]";
			mes "嗯......那個好像是別的世界的其他職業。";
			close;
		case 2:
			mes "[梅利托]";
			mes "噓！......那種話怎麼可以隨便說呢！你想被抓走嗎?";
			close;
		case 3:
			break;
		}
		mes "[梅利托]";
		mes "回答正確，妖術師最大的特點，就是可以和元素精靈交流。";
		next;
		mes "[梅利托]";
		mes "我們需要確認希望成為妖術師的人是否可以和元素精靈交流，才會發給他轉職證。";
		next;
		mes "[梅利托]";
		mes "基本的登記程序和介紹已經完成了，接下來請你去必須決定是否參加妖術師資格考試。";
		next;
		mes "[梅利托]";
		mes "參不參加是你的自由。你的決定是什麼呢?";
		next;
		switch(select("參加考試:下次再挑戰")) {
		case 1:
			if (VIP_SCRIPT) {
				callfunc "VIP_Third_Class";
				set job_soc,2;
				setquest 12096;
				next;
			}
			mes "[梅利托]";
			mes "好的。";
			mes "登記的內容，我會轉交給1樓的卡拉卡斯。他會引導你進行實際的考試。";
			set job_soc,2;
			setquest 12096;
			close;
		case 2:
			mes "[梅利托]";
			mes "噓！......那種話怎麼可以隨便說呢！你想被抓走嗎?";
			close;
		}
	} else {
		mes "你做好參加考試的準備了嗎?卡拉卡斯有點懶，我想你可能會比較痛苦......呵呵";
		close;
	}
}

gef_tower,113,161,5	script	卡拉卡斯	754,{
	mes "[卡拉卡斯]";
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "你看上去怎麼那麼重啊?請清理掉一些行李後再來吧!";
		close;
	}
	if (Class == Job_Sorcerer || Class == Job_Sorcerer_T || Class == Job_Baby_Sorcerer) {
		mes "你看上去很高興嘛。祝你旅途愉快。";
		close;
	}
	if (BaseLevel < 99 || JobLevel < 50 || SkillPoint != 0) {
		mes "找我有什麼事嗎?";
		close;
	}
	if (BaseJob != Job_Sage) {
		mes "找我有什麼事嗎?";
		close;
	}
	if (job_soc < 2) {
		mes "最近來王宮的人越來越多了。";
		close;
	} else if (job_soc == 2) {
		mes "......";
		next;
		select("喂......喂~");
		mes "[卡拉卡斯]";
		mes "......(睡夢中~)";
		next;
		select("哎嘿嘿！！~");
		mes "[卡拉卡斯]";
		mes "哈！！";
		mes "嚇了我一跳...... ";
		next;
		mes "[卡拉卡斯]";
		mes "(哈欠~)你......從哪裡來啊?";
		next;
		select("是梅利托讓我來的");
		mes "[卡拉卡斯]";
		mes "啊，等等。";
		mes "名冊......我已經收到了。";
		next;
		mes "[卡拉卡斯]";
		mes "[" + strcharinfo(0) + "]......對吧?";
		next;
		if(select("是的，沒錯:不是啊?") == 2) {
			mes "[卡拉卡斯]";
			mes "哎呀，看來名冊搞錯了。你能以後再來找我嗎?";
			close;
		}
		mes "[卡拉卡斯]";
		mes "既然你想參加妖術師轉職考試，我先來跟你說明一下吧。";
		next;
		mes "[卡拉卡斯]";
		mes "獲得妖術師轉職證的方法非常簡單，只要找到散布在世界各地的元素精靈，把它們的力量轉換成物質型態就行了~";
		next;
		mes "[卡拉卡斯]";
		mes "很容易吧?";
		next;
		select("老......老師，這真的可能嗎?");
		mes "[卡拉卡斯]";
		mes "當然，你不可能見到所有的元素精靈。要想讓元素精靈實體化，在你眼前出現，你還有很長的路要走。";
		next;
		mes "[卡拉卡斯]";
		mes "一般來說，只要在可以感覺到元素精靈的氣息的地方舉行特定的儀式就可以了。";
		next;
		mes "[卡拉卡斯]";
		mes "下面我來把方法告訴你，請仔細聽著。";
		next;
		mes "[卡拉卡斯]";
		mes "普通人的眼精，是無法識別元素精靈的。";
		mes "但是通過特殊的媒介，可以讓他們的一部分形象實體化。";
		next;
		mes "[卡拉卡斯]";
		mes "例如，帶著一定量以上的風靈礦石到可能有風元素精靈的地方去，元素精靈的共鳴就會大大增強。";
		next;
		mes "[卡拉卡斯]";
		mes "帶著水靈礦石到可能有火元素精靈的地方去，就可能產生出現強烈的排斥反應。";
		next;
		mes "[卡拉卡斯]";
		mes "妖術師把這種利用基本的相成相剋的關係來接近元素精靈的能力，視作是最基本的素養。";
		next;
		select("看來跟元素精靈的交流很重要");
		mes "[卡拉卡斯]";
		mes "那當然~";
		mes "因為妖術師的力量是以元素精靈的強大力量為泉源的。";
		next;
		mes "[卡拉卡斯]";
		mes "但是不知道只會讀書的人行不行......";
		next;
		mes "[卡拉卡斯]";
		mes "在我告訴你考試的方法之前，請你先去搜集考試所需的各種物品。";
		next;
		mes "[卡拉卡斯]";
		mes "風靈礦石3個、";
		mes "水靈礦石3個、";
		mes "火靈礦石3個、";
		mes "地靈礦石3個。";
		next;
		mes "[卡拉卡斯]";
		mes "如果你能準備好這些東西，我就告訴你接下來該怎麼做。";
		set job_soc,3;
		changequest 12096,12097;
		close;
	} else if (job_soc == 3) {
		if (countitem(992) > 2 && countitem(991) > 2 && countitem(990) > 2 && countitem(993) > 2) {
			mes "東西全都準備好啦。.";
			next;
			select("接下來我該怎麼做?");
			mes "[卡拉卡斯]";
			mes "我來跟你說明一下。首先我來告訴你做為考試舞台的地方。你最好找個地方記下來。";
			next;
			mes "[卡拉卡斯]";
			mes "1. 冰洞穴1樓、";
			mes "2. 拖爾火山1樓、";
			mes "3. 妙勒尼 廢棄礦坑3樓、";
			mes "4. 崑崙迷宮2樓";
			next;
			mes "[卡拉卡斯]";
			mes "在這4個地方都設置了探測棒，讓普通人也可以和元素精靈交流。";
			next;
			mes "[卡拉卡斯]";
			mes "當然，必須持有一定數量的和元素精靈相符的屬性礦石，探測棒才會啟動，因此光找到探測棒是沒用的。";
			next;
			mes "[卡拉卡斯]";
			mes "每種屬性至少需要有3個以上的屬性礦石才行。不是風靈原石之類的大型屬性石，而是風靈礦石這樣的小型屬性石。";
			next;
			select("所以才要我帶著屬性石去啊?");
			mes "[卡拉卡斯]";
			mes "是的，沒錯。理解能力很不錯嘛！";
			next;
			mes "[卡拉卡斯]";
			mes "你帶著屬性石過去，把它放到探測棒的屬性分析器裡。那樣探測棒就會啟動，開始與元素精靈交流。";
			next;
			mes "[卡拉卡斯]";
			mes "如果交流成功，就可以採集到物質化的元素精靈的一部分作為試劑。如果失敗，屬性石會碎裂。";
			next;
			mes "[卡拉卡斯]";
			mes "如果運氣好的話，失敗的時候，風靈礦石會變成風靈原石......但是這樣的情況並不多見，所以在此不作過多的討論，哈哈哈。";
			next;
			select("如果身上的屬性石用光了怎麼辦?");
			mes "[卡拉卡斯]";
			mes "啊哈哈......沒有屬性石的話，就比較麻煩了，必須重新去搜集才行。";
			next;
			mes "[卡拉卡斯]";
			mes "因此一般參加考試的人都會帶進可能多的屬性石。你搜集到的，只不過是最基本的數量。";
			next;
			mes "[卡拉卡斯]";
			mes "高級妖術師不需要這樣的過程，就可以和元素精靈對話。但對於參加轉職考試的初心者來說，必須借助媒介，才可以和元素精靈對話。";
			next;
			select("和元素精靈對話後獲得的試劑是固定的嗎?");
			mes "[卡拉卡斯]";
			mes "你無法知道可以從元素精靈那裡得到什麼試劑，因為那會隨著元素精靈狀態的不同而不同。";
			next;
			mes "[卡拉卡斯]";
			mes "如果你能成功獲得3種試劑，就回到我這裡來。";
			next;
			mes "[卡拉卡斯]";
			mes "一個元素精靈可以製造的試劑最多只有2種，因此要想製造出3種試劑，最好是多到幾個地方去。";
			next;
			mes "[卡拉卡斯]";
			mes "還有什麼不明白的?";
			next;
			if(select("沒有了:請再從頭說明一遍") == 2) {
				mes "[卡拉卡斯]";
				mes "哈哈那我先去喝杯水，你稍後再來找我吧!";
				next;
			}
			mes "[卡拉卡斯]";
			mes "我就簡單的給你寫一下，稍等......";
			next;
			mes "^000099從卡拉卡斯那裡得到了寫有和元素精靈交流的方法的文件。如果想查看詳細信息，請打開任務視窗。^000000.";
			set job_soc,4;
			changequest 12097,12098;
			close;
		}
		mes "Before I let you know the progressing way, you'll need to bring some required material.";
		next;
		mes "[卡拉卡斯]";
		mes "風靈礦石3個、";
		mes "水靈礦石3個、";
		mes "火靈礦石3個、";
		mes "地靈礦石3個。";
		next;
		mes "[卡拉卡斯]";
		mes "如果你能準備好這些東西，我就告訴你接下來該怎麼做。";
		close;
	} else if (job_soc == 4) {
		if (countitem(6276) && countitem(6278) && countitem(6277)) {
			mes "喔~做的不錯嘛!";
			mes "我還以為你連元素精靈的影子也找不到呢。沒想到居然讓你成功地搜集到了秘藥！";
			next;
			select("這樣就結束了嗎?");
			mes "[卡拉卡斯]";
			mes "辛苦你了。我要先來整理一下你帶回來的秘藥。請稍等一下。";
			delitem 6276,1; //Clear_Reagent
			delitem 6278,1; //Black_Reagent
			delitem 6277,1; //Red_Reagent
			set job_soc,5;
			completequest 12098;
			close;
		}
		mes "和元素精靈交流，獲得希望的結果，並不是一件容易的事情。";
		next;
		mes "[卡拉卡斯]";
		mes "不要放棄。加油~";
		close;
	} else if (job_soc == 5) {
		mes "本來我的老師會親自查看妖術師轉職考試的結果的，但自從他去尋找魚元素精靈之後，就再也沒有回來，所以由我來對你進行評價。";
		next;
		mes "[卡拉卡斯]";
		mes "[" + strcharinfo(0) + "]，";
		mes "為了打開通往妖術師之路，你克服了艱難的險阻，達到了我們學會要求的所有條件。";
		next;
		mes "[卡拉卡斯]";
		mes "當然這只是開始。現在還無法知道，在今後的道路上，你將需要和哪些元素精靈交流，有哪些苦難正在等待著你。";
		next;
		mes "[卡拉卡斯]";
		mes "但是有一件事情可以確定。";
		mes "那就是你表現出來的能力，足以幫助你克服任何的困難，[" + strcharinfo(0) + "]";
		next;
		mes "[卡拉卡斯]";
		if (BaseLevel < 99 || JobLevel < 50 || SkillPoint) {
			mes "Before I appointed you the need job, please check on your own body status. ";
			next;
			mes "[卡拉卡斯]";
			mes "You must have the etiquette of your job now before you accept the new job! ";
			close;
		}
		if (ismounting()) {
			mes "好的，在進行儀式前";
			mes "請先從坐騎下來~";
			next;
			mes "[卡拉卡斯]";
			mes "在接受新職業的場合中，你的行為應該要符合禮儀吧?";
			close;
		}
		mes "因此，我宣布你通過了妖術師學會的所有轉職考試。";
		next;
		mes "[卡拉卡斯]";
		mes "祝賀你，[" + strcharinfo(0) + "]。";
		jobchange roclass(eaclass()|EAJL_THIRD);
		getitem 5756,1; //Wind_Whisper
		getitem 2795,1; //Green_Apple_Ring
		next;
		mes "[卡拉卡斯]";
		mes "希望今後你能和元素精靈進行更多交流。";
		close;
	}
}
-	script	Spirit Detecting Staff#0	-1,{

	if (strnpcinfo(4) == "thor_v01") setarray .@id[0],990,994,6276,6278;
	else if (strnpcinfo(4) == "ice_dun01") setarray .@id[0],991,995,6278,6276;
	else if (strnpcinfo(4) == "mjo_dun03") setarray .@id[0],993,997,6277,6276;
	else if (strnpcinfo(4) == "gon_dun02") setarray .@id[0],992,996,6277,6278;

	if (job_soc == 4 && countitem(.@id[0]) > 2) {
		progressbar "ffff00",5;
		delitem .@id[0],3; //Boody_Red,Crystal_Blue,Yellow_Live,Wind_Of_Verdure
		set .@soc_ran, rand(1,70);
		if (.@soc_ran < 10) {
			if (countitem(.@id[2]) == 0) {
				mes "通過和看不見的元素精靈對話，獲得了試劑";
				getitem .@id[2],1; //Clear_Reagent,Black_Reagent,Red_Reagent
				close;
			}
			mes "從元素精靈那裡好像沒有得到什麼東西";
			close;
		} else if (.@soc_ran > 9 && .@soc_ran < 16) {
			if (countitem(.@id[3]) == 0) {
				mes "通過和看不見的元素精靈對話，獲得了試劑";
				getitem .@id[3],1; //Clear_Reagent,Black_Reagent,Red_Reagent
				close;
			}
			mes "從元素精靈那裡好像沒有得到什麼東西";
			close;
		} else if (.@soc_ran == 56) {
			mes "元素精靈幫你把屬性石結合成了巨大的屬性石";
			getitem .@id[1],1; //Flame_Heart,Mistic_Frozen,Great_Nature,Rough_Wind
			close;
		} else {
			mes "使用了屬性石，但和元素精靈的交流好像失敗了";
			close;
		}
	}
	mes "看到了妖術師協會設置的，用於和元素精靈對化的探測棒";
	if (job_soc == 4 && countitem(.@id[0]) < 3) {
		next;
		mes "持有和這裡的屬性相符的屬性石的話，應該就能和元素精靈交流";
	}
	close;
}
thor_v01,64,252,0	duplicate(Spirit Detecting Staff#0)	元素精靈探測棒#1	836
ice_dun01,274,274,0	duplicate(Spirit Detecting Staff#0)	元素精靈探測棒#2	836
mjo_dun03,200,141,0	duplicate(Spirit Detecting Staff#0)	元素精靈探測棒#3	836
gon_dun02,195,190,0	duplicate(Spirit Detecting Staff#0)	元素精靈探測棒#4	836