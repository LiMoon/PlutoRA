//==================================
// [冥] The PlutoEA Project [王]
// http://plutoea.googlecode.com/svn/
// ---------------------------------
// - 弓箭手任務技能 腳本 - 
//===================================
// 版本: 1.7
//===================================

moc_ruins,118,99,5	script	爵 納利#qsk_ac	88,{
	if (BaseClass == Job_Archer) {
		if (getskilllv("AC_MAKINGARROW") == 1) {
			mes "[爵 納利]";
			mes "噢噢，是我家鄉人啊!";
			mes "很高興見到你!";
			mes "過的還不錯吧?";
			mes "啊! 這箭矢!";
			mes "是自己做的啊!";
			next;
			mes "[爵 納利]";
			mes "哈哈...!";
			mes "做的不錯嗎?";
			mes "哈哈... 不管怎麼樣，我已經很高興了";
			mes "下一次再來找我吧，也多帶點故鄉消息.";
			mes "那麼再見囉~";
			close;
		}
		else if (JobLevel >= 30 || (BaseJob == Job_Hunter || BaseJob == Job_Bard || BaseJob == Job_Dancer)) {
			mes "[爵 納利]";
			mes "啊!"; 
			mes " ";
			mes "你是...";
			next;
			if ((countitem(907) > 19) && (countitem(921) > 6) && (countitem(906) > 40) && (countitem(1019) > 12) && (countitem(501) > 0)) {
				mes "[爵 納利]";
				mes "你把東西帶來了!"; 
				mes "非常謝謝你.";
				mes "那就按照約定, 我來教你技能吧.";
				next;
				delitem 907,20; //枯木汁
				delitem 921,7; //香菇芽孢
				delitem 906,41; //鱗片梗
				delitem 1019,13; //瑪哪樹枝
				delitem 501,1; //紅色藥水
				skill "AC_MAKINGARROW",1,0;
				mes "[爵 納利]";
				mes "那麼，從現在開始，就不用擔心箭矢了";
				mes "對啦, 你在斐揚的時候，有沒有看到";
				mes "卡浩這傢伙啊?";
				mes "請你小心啊. 那傢伙是";
				mes "非常殘暴的傢伙啊";
				next;
				mes "[爵 納利]";
				mes "去斐揚的時候，記得小心葛豪就好";
				mes "請你記得.";
				mes "那就再見囉~";
				close;
			}
			else {
				mes "[爵 納利]";
				mes "噢，在斐揚用箭的人!?";
				mes "很高興見到你! 在這種地方";
				mes "見到同胞! 真是太高興了!";
				mes "我獨自一個人來到夢羅克";
				mes "這裡很排斥外人... 嗚嗚嗚~";
				mes "我很孤獨啊~";
				next;
				if (select("很累嗎. 還好吧:繼續加油啊") == 1) {
					mes "[爵 納利]";
					mes "是的. 謝謝你...";
					mes "你也在這外地";
					mes "也真的辛苦啊.";
					mes "找到箭矢非常難吧?";
					mes "所以說我都親自，自己動手做啊.";
					next;
					select("啊，真的嗎?!");
					mes "[爵 納利]";
					mes "是的! 我把雜項物品，搜集起來之後";
					mes "可以製作箭矢.";
					mes "活在這辛苦的世界";
					mes "算是一個不錯的手藝啊.";
					mes "若你想學的話，我可以教你啊.";
					next;
					select("那就拜託你了.");
					mes "[爵 納利]";
					mes "不過可不是免費呀.";
					mes "天下沒有白吃的午餐啊~";
					mes "嗯... 那就這樣好了?"; 
					mes "請你幫我尋獲，我所指定的物品的話.";
					mes "我就教你這技能.";
					next; 
					mes "[爵 納利]";
					mes "我離開故鄉之後，感覺到非常孤單.";
					mes "想藉著故鄉的物品";
					mes "來安慰我孤單的心情啊.";
					mes "我需要在斐揚森林，橡膠樹上流下來的枯木汁20個";
					mes "在斐揚商店賣的紅色藥水1瓶";
					mes "請你拿給我好嗎."; 
					next; 
					mes "[爵 納利]";
					mes "還需要，活在斐揚森林附近";
					mes "從樹精魔物身上掉下來的，瑪哪樹枝13個";
					mes "森靈的鱗片梗41個";
					mes "魔菇的香菇芽孢7個";
					mes "你可以拿給我嗎";
					next;
					mes "[爵 納利]";
					mes "等到到時後，拿到之後我再教你技能好了";
					mes "那... 我就等你囉.";
					mes "等待你帶來故鄉的消息.";
					close;
				}
				mes "[爵 納利]";
				if (Sex)
					mes "...嘿..小姐";
				else
					mes "...嘿..先生.";
				mes "...晚上走路小心啊";
				close;
			}
		}
		else {
			mes "[爵 納利]";
			mes "什麼... 第一次看到弓箭手嗎?";
			mes "繼續走你的路吧.";
			mes "若是沒碰到，有水準的弓箭手的話"; 
			mes "我是不想開口的人啊!";
			close;
		}
	}
	else {
		mes "[?]";
		mes "哼... 什麼?";
		mes "還有話要說嗎?";
		mes "我可沒有話要說啊.";
		mes "有一位賢能者，把我的對話"; 
		mes "給中斷了.";
		close;
	}
}

payon,103,63,3	script	葛豪#qsk_ac	88,3,3,{
	if (BaseClass == Job_Archer) {
		if (getskilllv("AC_CHARGEARROW") == 1) {
			mes "[葛豪]";
			mes "又見面了."; 
			mes "還真奇怪啊.";
			mes "為什麼我見到，曾經見過的人";
			mes "就會有撒嬌的口語出現呢."; 
			mes "啊... 不管怎麼樣，很高興又見到你.";
			mes "啊... 以後再不要來喔，啊啊...";
			close;
		}
		else if (JobLevel >= 35 || (BaseJob == Job_Hunter || BaseJob == Job_Bard || BaseJob == Job_Dancer)) {
			mes "[葛豪]";
			mes "真是的... 受傷的地方很不容易好啊.";
			mes "我變成這副德行... 都是";
			mes "我太大意啦. 唉...";
			mes "雖然是這樣, 可是還給我造成";
			mes "這麼大的傷勢...";
			next;
			mes "爵 納利，這傢伙要怎麼辦.";
			mes "恩嗯... 啊!";
			mes "你? 什麼時候待在那裡的啊?"; 
			mes "嗯... 真是高手啊.";
			mes "像你這樣的高手";
			mes "當然一定會使用衝鋒箭囉.";
			next;
			switch(select("請問那是什麼啊?:現在可以教我嗎?")) {
			case 1:
				mes "[葛豪]";
				mes "...你是在說笑啊?"; 
				mes "你已經到這樣子的等級了";
				mes "還沒有修練到衝鋒箭啊?";
				mes "真是特別的人啊.";
				next;
				mes "(葛豪開始進入發呆的狀態)";
				next;
				mes "[葛豪]";
				mes "好吧，就這樣子吧. 我來教你";
				mes "什麼叫做衝鋒箭.";
				next;
				mes "[葛豪]"; 
				mes "所謂衝鋒箭是指, 當攻擊的同時";
				mes "把敵人往後退移的技能";
				mes "稍為花點時間瞄準，才能使用";
				mes "和魔法不一樣的是"; 
				mes "施法時間不會被中斷.";
				next;
				mes "[葛豪]";
				mes "在近距離會給較大的傷害力";
				mes "對弓箭手來說，是一個不錯的技能."; 
				mes "若你想學的話，我可以教你啊.";
				next;
				mes "[葛豪]";
				mes "首先要改造弓";
				mes "請你尋找一個不用的十字弓給我";
				mes "還有觸鬚10個 和鳥嘴10個";
				mes "猴子尾巴3個, 這彈力不錯.";
				mes "還有綠寶石2個. 最後還需要...";
				mes "我最喜歡的香蕉汁36瓶!";
				next; 
				mes "[葛豪]"; 
				mes "......咳咳!";
				mes "這樣子準備好之後，給我的話";
				mes "我會傳授給你衝鋒箭技能.";
				mes "那就, 請你出發吧.";
				mes "(假如沒有帶香蕉汁的話，我要痛扁他.)";
				close;
			case 2:
				if (countitem(721) > 1 && countitem(942) > 2 && countitem(962) > 9 && countitem(925) > 9 && countitem(532) > 35) {
					mes "[葛豪]";
					mes "不錯! 很標準!";
					mes "我要把全套的弓箭技能";
					mes "衝鋒箭傳授給你!";
					next;
					delitem 721,2; //Azure_Jewel
					delitem 942,3; //Yoyo_Tail
					delitem 962,10; //Tentacle
					delitem 925,10; //Bill_Of_Birds
					delitem 532,36; //Banana_Juice
					skill "AC_CHARGEARROW",1,0;
					mes "[葛豪]";
					mes "噢, 比我想像中做的更好!";
					mes "更不需要改良弓了!"; 
					mes "這請你再帶回去吧~";
					mes "從現在開始，請你盡情的享受";
					mes "在洞穴和原野上的表現了!";
					mes "哈~哈~!";
					close;
				}
				else {
					mes "[葛豪]";
					mes "嗯... 有點可惜啊.";
					mes "有個地方不足啊.";
					mes "我在重新說一邊, 我需要綠寶石2個";
					mes "猴子尾巴3個, 觸鬚10個"; 
					mes "鳥嘴10個, 最後需要"; 
					mes "香蕉汁36瓶!";
					next;
					mes "[葛豪]";
					mes "一定要找到之後，重新再來找我啊!";
					close;
				}
			}
		}
		else {
			mes "[葛豪]";
			mes "噢噢... 你是弓箭手啊."; 
			mes "稍為再努力一點的話"; 
			mes "會得到不錯的名聲!";
			mes "請你繼續加油啊!"; 
			close;
		}
	}
	else {
		mes "[?]";
		mes "對我這人生，孤獨的人來說";
		mes "還有什麼事情來找我呢?";
		close;
	}

OnTouch:
	mes "[???]";
	mes "咳嗚嗚嗚嗯....";
	next;
	mes "[???]";
	mes "咳嗚嗚嗚嗯....";
	close;
}
