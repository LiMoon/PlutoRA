//===== rAthena Script ======================================= 
//= The Airship System Script
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= The Airship System used in the official servers.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

//============================================================ 
//= 國內線路線
//= ... -> 朱諾 -> 艾音布羅克 -> 里希塔樂鎮 -> 艾音布羅克
//= -> 朱諾 -> 毀葛 -> ...
//============================================================ 

airplane,243,73,0	script	#AirshipWarp-1	45,1,1,{
	end;

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;

OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;

OnTouch:
	switch ($@airplanelocation) {
	case 0: warp "yuno",92,260; end;
	case 1: warp "einbroch",92,278; end;
	case 2: warp "lighthalzen",302,75; end;
	case 3: warp "hugel",181,146; end;
	}
}

airplane,243,29,0	duplicate(#AirshipWarp-1)	#AirshipWarp-2	45,1,1

airplane,1,1,0	script	Domestic_Airship	-1,{
	end;
OnInit:
	initnpctimer;
	end;
OnTimer20000:
	mapannounce "airplane","目前本飛空艇正飛往艾音布羅克途中",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane","等一下飛空艇就要降落在艾音布羅克了",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","我們已經到達艾音布羅克了，下機的旅客再會了",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane","我們目前正停靠在艾音布羅克中，等一下即將要出發囉",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","飛空艇要起飛了，下一個目的是里希塔樂鎮",bc_map,"0x70dbdb";
	end;
OnTimer100000:
	mapannounce "airplane","目前本飛空艇正飛往里希塔樂鎮",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane","等一下飛空艇就要降落在里希塔樂鎮了",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation,2;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","我們已經到達里希塔樂鎮了，下機的旅客再會了",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane","我們目前正停靠在里希塔樂鎮中，等一下即將要出發囉",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","飛空艇要起飛了，下一個目的艾音布羅克",bc_map,"0x00ff00";
	end;
OnTimer180000:
	mapannounce "airplane","目前本飛空艇正飛往艾音布羅克",bc_map,"0x00ff00";
	end;
OnTimer210000:
	mapannounce "airplane","等一下飛空艇就要降落在艾音布羅克了",bc_map,"0x00FF00";
	end;
OnTimer220000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","我們已經到達艾音布羅克了，下機的旅客再會了",bc_map,"0x00ff00";
	end;
OnTimer230000:
	mapannounce "airplane","我們目前正停靠在艾音布羅克中，等一下即將要出發囉",bc_map,"0x00ff00";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","飛空艇要起飛了，下一個目的是朱諾",bc_map,"0xff8200";
	end;
OnTimer260000:
	mapannounce "airplane","目前本飛空艇正飛往朱諾",bc_map,"0xff8200";
	end;
OnTimer290000:
	mapannounce "airplane","等一下飛空艇就要降落在朱諾了",bc_map,"0xff8200";
	end;
OnTimer300000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","我們已經到達朱諾了，下機的旅客再會了",bc_map,"0xff8200";
	end;
OnTimer310000:
	mapannounce "airplane","我們目前正停靠在朱諾中，等一下即將要出發囉",bc_map,"0xff8200";
	end;
OnTimer320000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","飛空艇要起飛了，下一個目的毀葛",bc_map,"0xca4bf3";
	end;
OnTimer340000:
	mapannounce "airplane","目前本飛空艇正飛往毀葛",bc_map,"0xca4bf3";
	end;
OnTimer370000:
	mapannounce "airplane","等一下飛空艇就要降落在毀葛了",bc_map,"0xca4bf3";
	end;
OnTimer380000:
	set $@airplanelocation,3;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","我們已經到達毀葛了，下機的旅客再會了",bc_map,"0xca4bf3";
	end;
OnTimer390000:
	mapannounce "airplane","我們目前正停靠在毀葛中，等一下即將要出發囉",bc_map,"0xca4bf3";
	end;
OnTimer400000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","飛空艇要起飛了，下一個目的朱諾",bc_map,"0xff8200";
	end;
OnTimer420000:
	mapannounce "airplane","目前本飛空艇正飛往朱諾",bc_map,"0xff8200";
	end;
OnTimer450000:
	mapannounce "airplane","等一下飛空艇就要降落在朱諾了",bc_map,"0xff8200";
	end;
OnTimer460000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","我們已經到達朱諾了，下機的旅客再會了",bc_map,"0xff8200";
	end;
OnTimer470000:
	mapannounce "airplane","我們目前正停靠在朱諾中，等一下即將要出發囉",bc_map,"0xff8200";
	end;
OnTimer480000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","飛空艇要起飛了，下一個目的艾音布羅克",bc_map,"0x00ff00";
	stopnpctimer;
	initnpctimer;
}

airplane,240,64,5	script	Exit#airplane1a::ExitAirplane	857,{
	end;
}
airplane,247,64,5	duplicate(ExitAirplane)	Exit#airplane1b	857
airplane,240,40,1	duplicate(ExitAirplane)	Exit#airplane2a	857
airplane,247,40,1	duplicate(ExitAirplane)	Exit#airplane2b	857

airplane,100,69,3	script	機組員#ein-1	852,{
	mes "[機組員]";
	mes "如果你要在飛空艇下來的話,請利用到上層的樓梯,感謝你搭乘飛空艇,";
	mes "祝你有個愉快的一天!";
	close;
}

airplane,64,94,1	script	Umbala Kid#ein_p	787,{
	emotion e_swt2;
	mes "[Kid]";
	if (event_umbala >= 3) {
		mes "Wow, mom!";
		mes "L-look at this!";
		mes "We're flying! W-we're...";
		mes "We're in the freakin' sky!";
	}
	else {
		mes "Makumalagu!";
		mes "Saampa joojimbo";
		mes "kaku na jedi Solo.";
		mes "Bwahahahahahahaah!";
	}
	close;
}

airplane,66,93,3	script	Umbala Lady#ein_p	783,{
	emotion e_dots;
	mes "[Lady]";
	if (event_umbala >= 3) {
		mes "Shush...";
		mes "Honey, behave~";
		mes "Don't act so excited";
		mes "when we're out in a";
		mes "public place like this!";
	}
	else {
		mes "Chooktu!";
		mes "Sacraup matii!";
		mes "Shaka gurftalfi";
		mes "huntiki manjoo!";
	}
	close;
}

airplane,71,91,7	script	Umbala Man#ein_p	789,{
	if (event_umbala >= 3) {
		mes "[Chrmlim]";
		mes "Hey there~";
		mes "From that look on";
		mes "your face, I see that";
		mes "you can understand";
		mes "me. ^333333*Whew...!*^000000";
		next;
		emotion e_pif;
		mes "[Chrmlim]";
		mes "I've been helping the";
		mes "Airship enterprise by";
		mes "having the Airship Crewmen";
		mes "train in Umbala to overcome";
		mes "any acrophobia they might have through bungee jumping. Neat, eh?";
		next;
		mes "[Chrmlim]";
		mes "But...";
		mes "Some of them couldn't";
		mes "overcome their fear of";
		mes "heights. And a few even";
		mes "ended up, um, ^333333in Nifflheim^000000.";
	}
	else {
		mes "[Chrmlim]";
		mes "Bajoo ga";
		mes "nukta Airship.";
		next;
		mes "[Chrmlim]";
		mes "...";
		mes "......";
		next;
		emotion e_pif;
		mes "[Chrmlim]";
		mes "Shabala moow bajama";
		mes "Airship kulaha googoona ";
		mes "salu. Dama, kookoo na nu";
		mes "yukuta. Um, fashuku na ret!";
	}
	close;
}

airplane,250,58,2	script	飛空艇 工作人員#airplane	67,{
	// 毀葛 quest addition
	if (hg_ma1 == 3) {
		mes "[飛空艇 工作人員]";
		mes "Welcome";
		mes "to the Airship.";
		mes "How may I help you?";
		next;
		select("Do you have a passenger named Thierry?");
		mes "[飛空艇 工作人員]";
		mes "I am sorry, but I do not think that we have a passenger by that name.";
		close;
	}
	// 毀葛 quest end
	mes "[飛空艇 工作人員]";
	mes "你好, 我們飛空艇隨時會盡其所能的提供給顧客便捷又舒適的旅行服務, 請問您需要幫忙嗎?";
	next;
	switch (select("利用飛空艇的方法:船長室的位置:內部設施介紹:取消")) {
	case 1:
		mes "[飛空艇 工作人員]";
		mes "假如想搭乘飛空艇到達目的地的話, 必須聽到機內廣播通知到達目的地後, 經由兩側的出口下機即可";
		next;
		mes "[飛空艇 工作人員]";
		mes "萬一下機時錯過機會的話, 由於飛空艇是一直循環飛行的, 所以請等到下一次時再下機";
		close;
	case 2:
		mes "[飛空艇 工作人員]";
		mes "船長室位於飛空艇的最前端, 那裡有指揮飛空艇的船長和駕駛船的舵手在掌舵, 船長為人很好, 一定要見個面";
		close;
	case 3:
		mes "[飛空艇 工作人員]";
		mes "為了避免旅客在旅行途中感到無聊, 所以在飛空艇的內部準備了小型遊戲, 旅途勞頓時可以當做休息的空間, 請多加利用";
		close;
	case 4:
		mes "[飛空艇 工作人員]";
		mes "祝你旅途平安愉快";
		close;
	}
}

airplane,80,71,2	script	傑樂塔#01airplane	834,{
	mes "[傑樂塔]";
	mes "我是為了傳達教誨才踏入旅途的, 透過教誨能使盧恩--米德加茲全部團結起來的話, 才是我真正的期望";
	close;
}

airplane,65,63,4	script	少女#01airplane	714,{
	mes "[少女]";
	mes "這艘飛空艇有到薑餅城吧? 不過我等了老半天都沒聽到到達薑餅城的廣播...";
	close;
}

airplane,72,34,6	script	亞努司#01airplane	702,{
	mes "[亞努司]";
	mes "在空中看的景色非常漂亮, 若能像鳥般翱翔天空該多好呢";
	close;
}

airplane,221,158,2	script	Pilot#airplane	852,{
	// 毀葛 quest addition
	if (hg_ma1 == 3) {
		mes "[Pilot]";
		mes "I wish that I could go drink a cold fresh beer.";
		mes "Drinking is the goal of my life! Drinking gives me energy!";
		mes "I am nothing without drinks!";
		next;
		mes "[Pilot]";
		mes "But! Driving under the influence is not good.";
		mes "But! That makes me want to drink more and more!";
		emotion e_sob;
		next;
		select("Do you know a passenger named Thierry?");
		mes "[Pilot]";
		mes "This uniform is";
		mes "really dapper, but";
		mes "it's way too thick to";
		mes "wear around the Airship.";
		next;
		mes "[Pilot]";
		mes "...";
		mes "......";
		mes "No one ever really";
		mes "comes into this room.";
		mes "And the captain IS a reindeer. I could just strip to my boxers.";
		next;
		emotion e_omg;
		mes "[Pilot]";
		mes "Wah!? Who is it!";
		next;
		mes "- ...He is not listening to you, at all. -";
		close;
	}
	// 毀葛 quest end
	switch (rand(1,4)) {
	case 1:
		mes "[Pilot]";
		mes "It's been sooo";
		mes "long since I've";
		mes "enjoyed a nice, cold";
		mes "alcoholic brew. But the";
		mes "job requires me to be as";
		mes "clear headed as I can!";
		next;
		mes "[Pilot]";
		mes "Always drink responsibly!";
		mes "Still, I can't remember the";
		mes "last time I had a real vacation";
		mes "or even a day off. Yeap, some";
		mes "booze, some chips, some TV";
		mes "and serius R&R is in order.";
		emotion e_sob;
		close;
	case 2:
		mes "[Pilot]";
		mes "Man, the weather";
		mes "is really nice today.";
		mes "Bright, open skies make";
		mes "for some good visibility";
		mes "and safe, carefree flying.";
		close;
	case 3:
		mes "[Pilot]";
		mes "You know, our captain's a";
		mes "respectable guy. Him and";
		mes "his brother are actually well";
		mes "known in the aircraft industry.";
		mes "Who knew reindeer made";
		mes "such good captains?";
		next;
		mes "[Pilot]";
		mes "Just between you";
		mes "and me, I gotta tell";
		mes "you, that Santa was onto";
		mes "something, getting reindeers";
		mes "and elves to work for him.";
		mes "The man must be a genius!";
		close;
	default:
		mes "[Pilot]";
		mes "You know, this whole";
		mes "piloting thing in the air,";
		mes "it's rather new, you know?";
		mes "Yeah, they got this Airship";
		mes "operation in a hurry.";
		next;
		emotion e_omg;
		mes "[Pilot]";
		mes "Still, they where real";
		mes "serius, really thought";
		mes "ahead. I mean, they had us";
		mes "training while the Airships";
		mes "were still being invented.";
		mes "Isn't that freakin' crazy?!";
		close;
	}
}

airplane,50,66,5	script	霍慈#airplane	86,{
	mes "[霍慈]";
	mes "歡迎光臨，我這埵陶c售新鮮的蘋果和榨蘋果汁的服務喔~";
	next;
	switch (select("我要購買蘋果:我要把蘋果榨成蘋果汁:你為什麼會在這裡:取消")) {
	case 1:
		mes "[霍慈]";
		mes "Please enter the amount";
		mes "of Apples that you wish to";
		mes "buy. Each Apple is 15 zeny";
		mes "and you can buy a maximum";
		mes "of 500 at a time. Please enter";
		mes " '0' to cancel your order.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[霍慈]";
				mes "Thanks for stopping";
				mes "by my shop. Farewell!";
				mes "Come by anytime when";
				mes "you feel like having an";
				mes "Apple to snack on~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[霍慈]";
				mes "You've entered a number";
				mes "higher than the maximum";
				mes "value of 500. Please enter";
				mes "the number of Apples you";
				mes "wish to purchase again.";
				next;
			}
			else {
				mes "[霍慈]";
				mes "A total of ^FF0000" + .@input + "^000000 Apples";
				mes "will cost you ^FF0000" + .@pay + " Zeny^000000 zeny.";
				mes "Would you like to continue?";
				next;
				if (select("Yes:No") == 2) {
					mes "[霍慈]";
					mes "Thanks for stopping";
					mes "by my shop. Farewell!";
					mes "Come by anytime when";
					mes "you feel like having an";
					mes "Apple to snack on~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[霍慈]";
			mes "I'm sorry, but you don't";
			mes "have enough money to";
			mes "purchase that many Apples.";
			mes "Please check your zeny or";
			mes "purchase fewer Apples.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[霍慈]";
			mes "Hmmm, I don't think";
			mes "you've got enough room in";
			mes "your inventory to carry this";
			mes "many Apples. Why don't you free up some of your inventory space?";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[霍慈]";
			mes "Thanks for stopping by";
			mes "my shop. I hope you enjoy";
			mes "the flavor of these Apples~!";
			close;
		}
	case 2:
		mes "[霍慈]";
		mes "Okay, I'll need";
		mes "^FF00003 Apples and 1 Empty Bottle^000000";
		mes "to make 1 Apple Juice for you.";
		mes "Would you like to proceed?";
		next;
		switch (select("Yes:No")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[霍慈]";
				mes "I'm sorry, but you don't";
				mes "have enough materials to";
				mes "create a bottle of Apple Juice.";
				mes "Remember, I need 3 Apples";
				mes "and 1 Empty Bottle to do it.";
				close;
			}
			else {
				mes "[霍慈]";
				mes "Thank you,";
				mes "please wait";
				mes "just a moment.";
				next;
				mes "^3355FF*Grind grind*";
				mes "*Grind grind*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[霍慈]";
				mes "There you go~";
				mes "I hope you enjoy!";
				mes "Please feel free to";
				mes "stop by for your Apple";
				mes "and Apple Juice needs";
				mes "at anytime, adventurer~";
				close;
			}
		case 2:
			mes "[霍慈]";
			mes "Thanks for stopping";
			mes "by my shop. Farewell!";
			mes "Come by anytime when";
			mes "you feel like having an";
			mes "Apple to snack on~";
			close;
		}
	case 3:
		mes "[霍慈]";
		mes "I used to be a wandering";
		mes "vagabond when, one day,";
		mes "I took a nap and something";
		mes "struck my head and awoke";
		mes "me from my restful slumber.";
		next;
		mes "[霍慈]";
		mes "It turns out that I was";
		mes "sleeping beneath an apple";
		mes "tree and that an apple fell";
		mes "and hit me on the head.";
		mes "I was dying of hunger and";
		mes "was about to eat that Apple...";
		next;
		mes "[霍慈]";
		mes "But suddenly, Kain, my old";
		mes "friend from the mining days,";
		mes "asked me to help him around";
		mes "on the Airship. So I did, and";
		mes "it was there where I found some";
		mes "people playing the Dice game.";
		next;
		mes "[霍慈]";
		mes "I was bored and curious";
		mes "and ended up wagering that";
		mes "single Apple in a game of";
		mes "dice. But for some reason,";
		mes "I had this incredible lucky";
		mes "streak. One apple became two... ";
		next;
		mes "[霍慈]";
		mes "Two became four and";
		mes "before I knew it, I had";
		mes "cornered the Apple market!";
		mes "I won so many Apples, I just";
		mes "started my own business here";
		mes "on the Airship. Weird, huh?";
		next;
		mes "[霍慈]";
		mes "So Apples are good";
		mes "for you. They were";
		mes "certainly very good";
		mes "to me. Hahahahaah~!";
		close;
	case 4:
		mes "[霍慈]";
		mes "Thank you for";
		mes "using my shop.";
		mes "Farewell~";
		close;
	}
}

//============================================================ 
//= 國際線路線
//= ... -> 依斯魯得島 -> 朱諾 -> 拉赫 -> ...
//============================================================ 

airplane_01,243,73,0	script	#AirshipWarp-3	45,1,1,{
	end;
	
OnTouch:
	switch ($@airplanelocation2) {
	case 0: warp "ra_fild12",292,204; end;
	case 1: 
		if(checkre(0))
			warp "izlude",200,73;
		else
			warp "izlude",200,56;
		end;
	case 2: warp "yuno",12,261; end;
	}

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;
	
OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;
}

airplane_01,243,29,0	duplicate(#AirshipWarp-3)	#AirshipWarp-4	45,1,1

airplane_01,1,1,0	script	International_Airship	-1,{
	end;
OnInit:
OnEnable:
	initnpctimer;
	end;
OnTimer25000:
	mapannounce "airplane_01","目前本飛空艇正飛往依斯魯得島",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane_01","等一下飛空艇就要降落在依斯魯得島了",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation2,1;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","我們已經到達依斯魯得島了，下機的旅客再會了",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane_01","我們目前正停靠在依斯魯得島中，等一下即將要出發囉",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","飛空艇要起飛了，下一個目的是朱諾",bc_map,"0x70dbdb";
	end;
OnTimer105000:
	mapannounce "airplane_01","目前本飛空艇正飛往朱諾",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane_01","等一下飛空艇就要降落在朱諾了",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation2,2;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","我們已經到達朱諾了，下機的旅客再會了",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane_01","我們目前正停靠在朱諾中，等一下即將要出發囉",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","飛空艇要起飛了，下一個目的是拉赫",bc_map,"0xFF8200";
	end;
OnTimer185000:
	mapannounce "airplane_01","目前本飛空艇正飛往拉赫",bc_map,"0xFF8200";
	end;
OnTimer210000:
	mapannounce "airplane_01","等一下飛空艇就要降落在拉赫了",bc_map,"0xFF8200";
	end;
OnTimer220000:
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","我們已經到達拉赫了，下機的旅客再會了",bc_map,"0xFF8200";
	end;
OnTimer230000:
	mapannounce "airplane_01","我們目前正停靠在拉赫中，等一下即將要出發囉",bc_map,"0xFF8200";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","飛空艇要起飛了，下一個目的是依斯魯得島",bc_map,"0x00ff00";
	stopnpctimer;
	set .moninv, .moninv + 1;
	if (.moninv == 7) {
		if (rand(1,3) == 3) {
			donpcevent "Airship#airplane02::OnEnable";
			end;
		}
		set .moninv, 0;
	}
	initnpctimer;
	end;
}

airplane_01,240,64,5	script	Exit#airplane_011a::ExitAirplane01	857,{
	end;
}
airplane_01,247,64,5	duplicate(ExitAirplane01)	Exit#airplane_011b	857
airplane_01,240,40,1	duplicate(ExitAirplane01)	Exit#airplane_012a	857
airplane_01,247,40,1	duplicate(ExitAirplane01)	Exit#airplane_012b	857

airplane_01,250,58,2	script	飛空艇 工作人員#ap01	67,{
	mes "[飛空艇 工作人員]";
	mes "你好, 我們飛空艇隨時會盡其所能的提供給顧客便捷又舒適的旅行服務, 請問您需要幫忙嗎?";
	next;
	switch (select("利用飛空艇的方法:船長室的位置:內部設施介紹:取消")) {
	case 1:
		mes "[飛空艇 工作人員]";
		mes "假如想搭乘飛空艇到達目的地的話, 必須聽到機內廣播通知到達目的地後, 經由兩側的出口下機即可";
		next;
		mes "[飛空艇 工作人員]";
		mes "萬一下機時錯過機會的話, 由於飛空艇是一直循環飛行的, 所以請等到下一次時再下機";
		close;
	case 2:
		mes "[飛空艇 工作人員]";
		mes "船長室位於飛空艇的最前端, 那裡有指揮飛空艇的船長和駕駛船的舵手在掌舵, 船長為人很好, 一定要見個面";
		close;
	case 3:
		mes "[飛空艇 工作人員]";
		mes "為了避免旅客在旅行途中感到無聊, 所以在飛空艇的內部準備了小型遊戲, 旅途勞頓時可以當做休息的空間, 請多加利用";
		close;

	case 4:
		mes "[飛空艇 工作人員]";
		mes "祝你旅途平安愉快";
		close;
	}
}

airplane_01,50,66,5	script	Apple Merchant#air01	86,{
	if (checkweight(1201,1) == 0) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you loose some weight. -";
		close;
	}
	mes "[Meltz]";
	mes "我們已經到達Meltz's";
	mes "Shop where you can";
	mes "purchase Apples or grind";
	mes "them to make Apple Juice.";
	next;
	switch (select("Buy Apples.:Make Apple Juice.:Cancel.")) {
	case 1:
		mes "[Meltz]";
		mes "Please enter the amount";
		mes "of Apples that you wish to";
		mes "buy. Each Apple is 15 zeny";
		mes "and you can buy a maximum";
		mes "of 500 at a time. Please enter";
		mes "'0' to cancel your order.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Meltz]";
				mes "Thanks for stopping";
				mes "by my shop. Farewell!";
				mes "Come by anytime when";
				mes "you feel like having an";
				mes "Apple to snack on~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Meltz]";
				mes "You've entered a number";
				mes "higher than the maximum";
				mes "value of 500. Please enter";
				mes "the number of Apples you";
				mes "wish to purchase again.";
				next;
			}
			else {
				mes "[Meltz]";
				mes "A total of ^FF0000" + .@input + "^000000 Apples";
				mes "will cost you ^FF0000" + .@pay + "^000000 zeny.";
				mes "Would you like to continue?";
				next;
				if (select("Yes:No") == 2) {
					mes "[Meltz]";
					mes "Thanks for stopping";
					mes "by my shop. Farewell!";
					mes "Come by anytime when";
					mes "you feel like having an";
					mes "Apple to snack on~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Meltz]";
			mes "I'm sorry, you don't have";
			mes "enough money with you.";
			mes "Please check your funds or";
			mes "purchase less Apples.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Meltz]";
			mes "Hmm, I don't think you've";
			mes "got enough room to carry";
			mes "this many Apples. You might";
			mes "want to free up your inventory";
			mes "space.";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Meltz]";
			mes "Thanks for stopping by";
			mes "my shop. I hope you enjoy";
			mes "the flavor of these Apples~!";
			close;
		}
	case 2:
		mes "[Meltz]";
		mes "Okay, I'll need";
		mes "^FF00003 Apples and 1 Empty Bottle^000000";
		mes "to make 1 Apple Juice for you.";
		mes "Would you like to proceed?";
		next;
		switch (select("Yes:No")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Meltz]";
				mes "I'm sorry, but you don't";
				mes "have enough materials to";
				mes "create a bottle of Apple Juice.";
				mes "Remember, I need 3 Apples";
				mes "and 1 Empty Bottle to do it.";
				close;
			}
			else {
				mes "[Meltz]";
				mes "Thank you, please wait.";
				next;
				mes "^3355FF*Grind* *Grind*";
				mes "*Grind* *Grind*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[Meltz]";
				mes "There you go~";
				mes "Please come again.";
				close;
			}
		case 2:
			mes "[Meltz]";
			mes "Thanks for stopping";
			mes "by my shop. Farewell!";
			mes "Come by anytime when";
			mes "you feel like having an";
			mes "Apple to snack on~";
			close;
		}
	case 3:
		mes "[Meltz]";
		mes "Thanks for stopping";
		mes "by my shop. Farewell!";
		mes "Come by anytime when";
		mes "you feel like having an";
		mes "Apple to snack on~";
		close;
	}
}

airplane_01,221,158,2	script	Pilot#airplane_01	852,{
	switch (rand(1,4)) {
	case 1:
		mes "[Pilot]";
		mes "Longitude, 131 degrees east.";
		mes "Latitude, 37 degrees north.";
		mes "We're right on course, captain.";
		close;
	case 2:
		mes "[Pilot]";
		mes "Looks like a really";
		mes "cloudy day. Always hard";
		mes "to navigate when the skies";
		mes "aren't clear. Guess we'll";
		mes "need to amp the radar.";
		close;
	case 3:
		mes "[Pilot]";
		mes "The Captain is a good";
		mes "man and I can't think of";
		mes "a finer person to command";
		mes "this ship. Still, he's pretty";
		mes "tough, a real slave driver.";
		next;
		mes "[^ff0000Tarlock^000000]";
		mes "^ff0000Hey...!^000000";
		mes "^ff0000Less chit-chat^000000";
		mes "^ff0000and more piloting!^000000";
		next;
		mes "[Pilot]";
		mes "R-right away, sir!";
		mes "(See what I mean?)";
		close;
	default:
		mes "[Pilot]";
		mes "This uniform is";
		mes "really dapper, but";
		mes "it's way too thick to";
		mes "wear around the Airship.";
		next;
		mes "[Pilot]";
		mes "...";
		mes "......";
		mes "No one ever really";
		mes "comes into this room.";
		mes "And the captain IS a reindeer.";
		mes "I could just strip to my boxers.";
		next;
		emotion e_omg;
		mes "[Pilot]";
		mes "Oh...! Hello there!";
		mes "E-e-enjoying your flight?!";
		close;
	}
}

airplane_01,83,61,2	script	Dianne#01airplane_01	72,2,2,{
	mes "[Dianne]";
	mes "It's so weird!";
	mes "I went to visit the";
	mes "Airship Captain and";
	mes "all I saw was this";
	mes "weird reindeer. Oh!";
	mes "Do you think that...";
	close;

OnTouch:
	emotion e_sob;
	end;
}

airplane_01,69,63,2	script	Mendel#01airplane_01	55,{
	mes "[Mendel]";
	mes "As I expected, the";
	mes "in-flight meals are";
	mes "three star quality at best.";
	mes "*Harrrumph* I really should";
	mes "have brought my chef so that";
	mes "I could enjoy a real meal.";
	close;
}

airplane_01,71,31,2	script	Swordsman Shimizu#air_01	106,{
	mes "[Swordsman Shimizu]";
	mes "Finally, after five";
	mes "years of waiting...";
	mes "I can have my revenge!";
	next;
	mes "[Swordsman Shimizu]";
	mes "I just...";
	mes "Have to make sure that";
	mes "I don't keep missing my";
	mes "stop. But soon, very soon,";
	mes "vengeance will be mine!";
	close;
}

//============================================================ 
//= Typing Challenge
//============================================================ 
airplane_01,32,61,4	script	Nils#ein	49,1,1,{
	mes "[Nils]";
	mes "我們已經到達the";
	mes "^ff0000RO Typing Challenge^000000.";
	mes "Would you like to play";
	mes "a quick typing game?";
	next;
	switch (select("Play ^ff0000RO Typing Challenge^000000:Information:View Top Records:Cancel")) {
	case 1:
		mes "[Nils]";
		mes "Okay, we have";
		mes "a new challenger!";
		mes "Enter the following";
		mes "text as quickly as you";
		mes "can without making any";
		mes "mistakes! Let's start~!";
		setarray .@line1_1$[0],	"^3cbcbccallipygian salacius lascivious^000000",
					"^3cbcbcBy the power of^000000",
					"^0000ffthkelfkskeldmsiejdlslehfndkelsheidl^000000",
					"^3cbcbcburrdingdingdingdilidingdingdingphoohudaamb^000000",
					"^3cbcbcCoboman no chikara-yumei na^000000",
					"^3cbcbcI'm the king of All Weirdos! Now^000000",
					"^3cbcbcYou give me no choice. I guess it's^000000";
		setarray .@line1_2$[0],	"^3cbcbclicentious prurient concupiscent^000000",
					"^3cbcbcp-po-poi-po-poi-poin-poing^000000",
					"^3cbcbcskemd^000000",
					"^3cbcbcandoorabambarambambambambamburanbamding^000000",
					"^3cbcbcchikara-daiookii na chikara da ze!^000000",
					"^3cbcbcyou know of my true power. Obey~!^000000",
					"^3cbcbctime for me to reveal my secret...^000000";
		setarray .@line1_3$[0],	"",
					"^3cbcbcGOD-POING. I NEVER LOSE!^000000",
					"",
					"",
					"^3cbcbcCOBO ON^000000",
					"",
					"";
		setarray .@word1$[0],	"callipygian salacius lascivious licentious prurient concupiscent",
					"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
					"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
					"burrdingdingdingdilidingdingdingphoohudaambandoorabambarambambambambamburanbamding",
					"Coboman no chikara-yumei na chikara-daiookii na chikara da ze! COBO ON",
					"I'm the king of All Weirdos! Now you know of my true power. Obey~!",
					"You give me no choice. I guess it's time for me to reveal my secret...";
		setarray .@line2_1$[0],	"^3cbcbcuNflAPPaBLe LoVaBLe SeCreTs AnD^000000",
					"^ff1493LiGhTsPeEd RiGhT SPEed LeFT TURn^000000",
					"^ff1493hfjdkeldjsieldjshfjdjeiskdlefvbd^000000",
					"^ff1493burapaphuralanderamduanbatuhiwooi^000000",
					"^ff1493belief love luck grimace sweat rush^000000",
					"^800080opeN, Open!op3n.openOpen0p3nOpEn0pen^000000",
					"^3cbcbcfReeDoM ecstAcy JoUrnaliSm ArMplt^000000";
		setarray .@line2_2$[0],	"^3cbcbcboWLIiNg aGaINST tHe KarMA of YoUtH^000000",
					"^ff1493RiGhT BuRn OrIGInAL GaNgSteR SmACk^000000",
					"",
					"^ff1493kabamturubamdingding^000000",
					"^ff1493folktale rodimus optimus bumblebee^000000",
					"^800080`open'0Pen open? open!111OPENSESAME^000000",
					"^3cbcbcDisCoverY hEaDaChE MoonbeAmS jUsTiCE^000000";
		setarray .@word2$[0],	"uNflAPPaBLe LoVaBLe SeCreTs AnD boWLIiNg aGaINST tHe KarMA of YoUtH",
					"LiGhTsPeEd RiGhT SPEed LeFT TURn RiGhT BuRn OrIGInAL GaNgSteR SmACk",
					"hfjdkeldjsieldjshfjdjeiskdlefvbd",
					"burapaphuralanderamduanbatuhiwooikabamturubamdingding",
					"belief love luck grimace sweat rush folktale rodimus optimus bumblebee",
					"opeN, Open!op3n.openOpen0p3nOpEn0pen`open'0Pen open? open!111OPENSESAME",
					"fReeDoM ecstAcy JoUrnaliSm ArMplt DisCoverY hEaDaChE MoonbeAmS jUsTiCE";
		setarray .@letters[0],	1300,
					1250,
					1180,
					1380,
					1740,
					1440,
					1450;
		set .@wordtest, rand(7);
		next;
		mes "[Nils]";
		mes .@line1_1$[.@wordtest];
		mes .@line1_2$[.@wordtest];
		mes .@line1_3$[.@wordtest];
		set .@start_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		next;
		input .@save1$;
		set .@end_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		set .@total_time, .@end_time - .@start_time;
		mes "[Nils]";
		mes .@line2_1$[.@wordtest];
		mes .@line2_2$[.@wordtest];
		set .@start_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		next;
		input .@save2$;
		set .@end_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		set .@total_time, .@total_time + (.@start_time - .@end_time);
		set .@tasoo, (.@letters[.@wordtest] / .@total_time) * 6;
		if ((.@save1$ == .@word1$[.@wordtest]) && (.@save2$ == .@word2$[.@wordtest])) {
			mes "[Nils]";
			mes "Your record is ^ff0000" + .@total_time + " seconds^000000 and";
			mes "the total letters are " + .@tasoo + ".";
			next;
			if (.@tasoo >= 1300) {
				mes "[Nils]";
				mes "Hmmm, this record isn't";
				mes "humanly possible unless you";
				mes "copy and paste the whole";
				mes "sentence. Please play fairly";
				mes "next time.";
				close;
			}
			if (.@tasoo >= $050320_ein_typing) {
				mes "[Nils]";
				mes "The previous top record was";
				mes "made by ^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "with the total ^0000ff" + $050320_ein_typing + "^000000 letters.";
				mes "However, ^ff0000" + strcharinfo(0) + "^000000,";
				mes "you made the new top record";
				mes "this time. Congratulations!";
				set $050320_minus1_typing$, strcharinfo(0);
				set $050320_ein_typing, .@tasoo;
				close;
			}
			else {
				mes "[Nils]";
				mes "^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "is the current";
				mes "record holder with";
				mes "a letter total of ^0000ff" + $050320_ein_typing + "^000000";
				mes "characters. Try to beat";
				mes "that record next time~";
				close;
			}
		}
		else {
			mes "[Nils]";
			mes "Oooh...";
			mes "I'm sorry, but";
			mes "you entered the";
			mes "text incorrectly...";
			close;
		}
	case 2:
		mes "[Nils]";
		mes "The ^ff0000RO Typing Challenge^000000";
		mes "is a game where you enter";
		mes "the given text as quickly as you";
		mes "can. The name of the top player";
		mes "is recorded for posterity. If you";
		mes "want fame, here's your chance!";
		next;
		mes "[Nils]";
		mes "I'd just like to let";
		mes "you know that you type";
		mes "all the text that you see";
		mes "in the single input line that";
		mes "you're given. So don't press";
		mes "the enter key, just click 'OK.'";
		close;
	case 3:
		mes "[Nils]";
		mes "^0000ff" + $050320_minus1_typing$ + "^000000";
		mes "is the current";
		mes "record holder with";
		mes "a letter total of ^0000ff" + $050320_ein_typing + "^000000";
		mes "characters. Try to beat";
		mes "that record next time~";
		close;
	case 4:
		mes "[Nils]";
		mes "Feel free to take on the";
		mes "^ff0000RO Typing Challenge^000000";
		mes "anytime. I'll be here~";
		close;
	}
}

//============================================================ 
//= Apple Gambling
//============================================================ 
airplane_01,33,68,4	script	Clarice	74,{
	mes "[Clarice]";
	mes "Hi, I'm Clarice~";
	mes "How would you like";
	mes "to wager some Apples";
	mes "in a friendly game of Dice?";
	next;
	callfunc "applegamble","Clarice";
	end;
}

function	script	applegamble	{

	switch (select("我要玩一把擲骰子:我想瞭解擲骰子遊戲的規則:取消")) {
	case 3:
		mes "["+getarg(0)+"]";
		mes "如果您想玩遊戲的話隨時來找我吧，我";
		mes "在這裡發呆也蠻無聊的，那麼再見囉";
		close;
	case 2:
		mes "["+getarg(0)+"]";
		mes "現在我來講解擲骰子的遊戲規則，其實很";
		mes "簡單，首先開始遊戲時，可以下注最多5";
		mes "0顆蘋果，當然如果沒有蘋果就不能參";
		mes "加遊戲了";
		next;
		mes "["+getarg(0)+"]";
		mes "蘋果下注後，批次各擲兩次骰子，由擲出";
		mes "來的合計點數大的一方獲勝，當然如果游";
		mes "戲一次定江山就不大好玩，所以兩邊各有";
		mes "再一次（第三次）擲骰子的機會";
		next;
		mes "["+getarg(0)+"]";
		mes "要不要再擲一次骰子是個人的自由啊，不過";
		mes "一旦再擲一次骰子後，點數若超過12點時";
		mes "，由超過的一方為輸家";
		next;
		mes "["+getarg(0)+"]";
		mes "當你選擇擲第三次骰子時，由您先擲，如";
		mes "過擲出來的點數超過12點，即使我不再擲";
		mes "，也有我主動獲勝";
		next;
		mes "["+getarg(0)+"]";
		mes "在遊戲獲勝的一方，可以全部擁有雙方第";
		mes "一次所下注的蘋果數量，我這樣說明已經";
		mes "很清楚了吧";
		close;
	case 1:
		break;
	}
	mes "["+getarg(0)+"]";
	mes "若想要玩擲骰子遊戲，就需要有蘋果，請";
	mes "問您要下多少個蘋果的賭注呢?";
	mes "最多可一下到50顆，想要取消遊戲請輸入0";
	next;
	while(1) {
		input .@amount;
		if (.@amount == 0) {
			mes "["+getarg(0)+"]";
			mes "Changed your mind?";
			mes "I understand. Well then,";
			mes "I hope we can play sometime.";
			close;
		}
		else if (.@amount < 1 || .@amount > 50) {
			mes "["+getarg(0)+"]";
			mes "You can't bet more than";
			mes "50 Apples. Remember, we";
			mes "need to keep these stakes";
			mes "reasonable. Please enter";
			mes "a value no greater than 50.";
			next;
			continue;
		}
		mes "["+getarg(0)+"]";
		mes "So you'll be";
		mes "betting ^FF0000"+.@amount+"^000000 Apples.";
		mes "Is that right?";
		next;
		if (select("Yes:No") == 2) {
			mes "["+getarg(0)+"]";
			mes "Mm, made a mistake?";
			mes "Alright, please enter the";
			mes "number of Apples you";
			mes "wish to place in this bet";
			next;
			continue;
		}
		if (countitem(512) <.@amount) {
			mes "I'm sorry, but you";
			mes "don't seem to have";
			mes "enough Apples for this";
			mes "bet... You can't gamble";
			mes "if you can't play, you know.";
			next;
			continue;
		}
		delitem 512,.@amount;
		mes "["+getarg(0)+"]";
		mes "Good!";
		mes "Now we can start";
		mes "this game! I'll roll first~";
		break;
	}
	mes "^3355FF*Rolling and rumbling*^000000";
	next;
	set .@giveapple, .@amount*2;
	set .@table1, rand(1,6);
	set .@table2, rand(1,6);
	set .@tablesub, .@table1 + .@table2;
	set .@tabletotal, .@tablesub;
	mes "["+getarg(0)+"]";
	mes "I got a ^0000FF" + .@table1 + "^000000 and a ^0000FF" + .@table2 + "^000000.";
	mes "That's a total of ^0000FF" + .@tablesub + "^000000.";
	mes "^FF0000" + strcharinfo(0) + "^000000, now it's your turn.";
	next;
	select("Cast Dice.");
	mes "^3355FF*Rolling and rumbling*^000000";
	set .@player1, rand(1,6);
	set .@player2, rand(1,6);
	set .@playersub, .@player1 + .@player2;
	if (.@playersub > 9 && .@amount > 39) {
		set .@player1, rand(1,6);
		set .@player2, rand(1,6);
		set .@playersub, .@player1 + .@player2;
	}
	set .@playertotal, .@playersub;
	next;
	mes "["+getarg(0)+"]";
	mes "^FF0000" + strcharinfo(0) + "^000000, you have ^FF0000" + .@player1 + "^000000 and ^FF0000" + .@player2 + "^000000. The total is ^FF0000" + .@playersub + "^000000 .";
	next;
	mes "["+getarg(0)+"]";
	if(.@playersub == .@tablesub) {
		mes "Currently my total is ^0000FF" + .@tablesub + "^000000 and ^FF0000" + strcharinfo(0) + "^000000, your total is ^FF0000" + .@playersub + "^000000. We are making an even game. Would you like to cast dice again?";
	} else if (.@playersub > .@tablesub) {
		mes "Currently my total is ^0000FF" + .@tablesub + "^000000 and ^FF0000" + strcharinfo(0) + "^000000, your total is ^FF0000" + .@playersub + "^000000. ^FF0000" + strcharinfo(0) + "^000000, you are currently winning this game. Would you like to cast dice again?";
	} else if(.@tablesub > .@playersub) {
		mes "Currently my total is ^0000FF" + .@tablesub + "^000000 and ^FF0000" + strcharinfo(0) + "^000000, your total is ^FF0000" + .@playersub + "^000000. I am winning this game. Would you like to cast dice again?";
	}
	next;
	switch (select("Cast dice.:Cancel.")) {
	case 1:
		mes "^3355FF*Rolling and rumbling*^000000";
		set .@player3, rand(1,6);
		set .@playertotal, .@playertotal + .@player3;
		next;
		mes "["+getarg(0)+"]";
		if (.@playertotal > 12) {
			mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. You lost this game. I am sorry but please try again.";
			close;
		}
		else if (.@playertotal < .@tablesub) {
			mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. Even though you casted dice again, still your total is smaller than mine. You lost the game. I am sorry and please try again.";
			close;
		}
		else if (.@playertotal == .@tablesub) {
			if (.@tablesub > 8) {
				mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. I don't want to take any risk, let's end this game in a draw. Let's play again some other time~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
		}
		else {
			mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. Now it is my turn.";
		}
		break;
	case 2:
		mes "["+getarg(0)+"]";
		if (.@playersub > .@tablesub) {
			mes "I see, you don't want to take risk of losing the game. Okay, let me cast dice again.";
		}
		else if (.@playersub == .@tablesub) {
			if (.@tablesub > 8) {
				mes "I see, you don't want to take risk of losing this game. Neither do I, let's end this game in a draw. Let's play again some other time~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "Alright.";
			mes "Let me cast the dice again.";
		}
		else {
			mes "It couldn't hurt to try.";
			mes "Well, I win this time.";
			mes "I'm sorry, let's try play";
			mes "again sometime.";
			close;
		}
		break;
	}
	next;
	mes "^3355FF*Rolling and rumbling*^000000";
	set .@table3, rand(1,6);
	set .@tabletotal, .@tabletotal + .@table3;
	next;
	mes "["+getarg(0)+"]";
	if (.@tabletotal > 12) {
		mes "I got ^0000FF" + .@table3 + "^000000 and the total is now ^0000FF" + .@tabletotal + "^000000. I lost this game since my total exceeded 12. Let me give you my apples. Congratulations, that was a great game.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal > .@tabletotal) {
		mes "I got ^0000FF" + .@table3 + "^000000 and the total is now ^0000FF" + .@tabletotal + "^000000. With total ^FF0000" + .@playertotal + "^000000 you won this game, ^FF0000" + strcharinfo(0) + "^000000. Let me give you my apples. It was a great game and I hope we will play again some other time.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal == .@tabletotal) {
		mes "I got ^0000FF" + .@table3 + "^000000 and the total is now ^0000FF" + .@tabletotal + "^000000. With total ^FF0000" + .@playertotal + "^000000 this game came out even, ^FF0000" + strcharinfo(0) + "^000000. Let me give you your apple back. It was a great game and I hope we will play again some other time.";
		close2;
		getitem 512,.@amount; //Apple
		end;
	}
	else if (.@playertotal < .@tabletotal) {
		mes "I got ^0000FF" + .@table3 + "^000000 and the total is now ^0000FF" + .@tabletotal + "^000000. With total ^FF0000" + .@playertotal + "^000000 you lost this game, ^FF0000" + strcharinfo(0) + "^000000. I am sorry but please try again.";
		close;
	}
}
