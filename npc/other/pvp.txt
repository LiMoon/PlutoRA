//===== rAthena Script ======================================= 
//= PvP NPCs
//===== By: ==================================================
//= kobra_k88; L0ne_W0lf, Elias (og2)
//===== Current Version: =====================================
//= 2.5
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= PvP NPCs that are found in the Inns in major cities.
//= Includes Yoyo Mode, Nightmare Mode, and Event Mode.
//===== Additional Comments: ================================= 
//= 2.0 Narrators/GateKeepers,etc. Translated from the Official 2011 [Elias]
//= 2.1 Changed PvP解說員 and GateKeeper to Floating NPC [Elias]
//= 2.2 Some cleaning. [Euphy]
//= 2.3 Fixed grammatical errors. [Joseph]
//= 2.4 Fixed incorrect use of 'close' and some cleaning. [Joseph]
//= 2.5 Renewal/Pre-Renewal split. [Euphy]
//============================================================

// Information
//============================================================ 
-	script	PvP解說員::pvpe	-1,{
	mes "[PVP解說員]";
	mes "你好!";
	mes "我是擔任PVP";
	mes "解說的";
	mes "PVP 解說員!";
	next;
	switch (select("^3355ffPVP模式^000000?:什麼叫做PVP模式?:PVP模式的規則是什麼?:存儲現在位置:結束對話")) {
	case 1:
		mes "[PVP解說員]";
		mes "PVP模式就是";
		mes "'Player VS Player Mode'";
		mes "的簡寫..";
		mes "也就是一種在假想的世界裡，";
		mes "享受玩家間大戰的特別戰鬥";
		next;
		mes "[PVP解說員]";
		mes "簡單的說就是與其他玩家打鬥的模式";
		mes "只要向我身邊這位^3355ff守門人^000000說一聲";
		mes "表示願意嘗試的話";
		mes "他便會";
		mes "引導你進入為PVP模式所準備的打鬥場";
		next;
		mes "[PVP解說員]";
		mes "請你一定要小心";
		mes "也請好好享受戰鬥的樂趣吧";
		mes "要進入PVP模式";
		mes "玩家等級要達到31級以上";
		next;
		mes "[PVP解說員]";
		mes "擁有參加資格以後";
		mes "可以任意選擇下列兩個模式中的其中一個";
		mes "那就是夢魘模式";
		mes "及溜溜猴模式。";
		next;
		mes "[PVP解說員]";
		mes "在夢魘模式中的遊戲";
		mes "規則跟在實戰";
		mes "是一樣的";
		mes "因此道具會有掉落的情況發生";
		mes "經驗值也會損失。";
		next;
		mes "[PVP解說員]";
		mes "請熟知這一點，";
		mes "以避免發生爭執。";
		mes "請仔細考慮清楚後再進入。";
		close;
	case 2:
		mes "[PVP解說員]";
		mes "透過我身旁這位 ' PVP守門人 ' ";
		mes "請記得";
		mes "繳交500金幣";
		mes "便可以往PVP模式的打鬥場";
		mes "移動前往。";
		next;
		mes "[PVP解說員]";
		mes "打鬥模式分為";
		mes "溜溜猴模式和";
		mes "夢魘模式";
		mes "請一定小心選擇自己的模式";
		mes "這兩種";
		mes "模式的打鬥場裡";
		next;
		mes "[PVP解說員]";
		mes "各有數名解說員";
		mes "他們會將31級以上的參加者";
		mes "每10等級";
		mes "再做細分";
		next;
		mes "[PVP解說員]";
		mes "有5個PVP專用房間可供選擇";
		mes "選擇你所想去的那個房間";
		mes "便可移動到那裡";
		mes "從解說員頭上的窗口";
		next;
		mes "[PVP解說員]";
		mes "可以知道等級的限制";
		mes "以及此地圖正在進行打鬥的人數";
		mes "這點請你要注意";
		next;
		mes "[PVP解說員]";
		mes "解說員頭上的窗口";
		mes "會出現【參加人數/總人數】這樣的數據";
		next;
		mes "[PVP解說員]";
		mes "另一點請注意";
		mes "PVP專用地圖";
		mes "是無法";
		mes "儲存位置的";
		mes "請注意以上這點";
		close;
	case 3:
		mes "[PVP解說員]";
		mes "進入PVP模式後";
		mes "畫面右下角";
		mes "出現的是";
		mes "該地圖";
		mes "玩家自己的排行 / 地圖玩家人數";
		next;
		mes "[PVP解說員]";
		mes "相信會對你有幫助";
		mes "在PVP模式中的經驗?";
		mes "只有在PVP模式中適用";
		mes "這點要注意";
		mes "一開始每位玩家的經驗?";
		next;
		mes "[PVP解說員]";
		mes "基本上都是' 5 '";
		mes "在途中若是";
		mes "贏得勝利的話";
		mes "可增加經驗 1";
		mes "若是失敗的話";
		next;
		mes "[PVP解說員]";
		mes "則經驗會減少 5";
		mes "如果經驗減到0以下";
		next;
		mes "[PVP解說員]";
		mes "則會被踢出PVP模式";
		mes "不得繼續參與";
		mes "但是如果經驗大於0";
		mes "卻被殺死的話";
		mes "可以藉由同伴";
		next;
		mes "[PVP解說員]";
		mes "回復體力，繼續參與遊戲";
		mes "這樣你懂了吧";
		next;
		mes "[PVP解說員]";
		mes "打鬥的規則";
		mes "與外面戰鬥的規則";
		mes "是一樣的";
		close;
	case 4:
		mes "[PVP解說員]";
		mes "已經成功存儲位置";
		mes "非常感謝您的使用";
		if(strnpcinfo(4) == "morocc_in") { savepoint "morocc_in",141,139; }
		if(strnpcinfo(4) == "alberta_in") { savepoint "alberta_in",22,148; }
		if(strnpcinfo(4) == "prt_in") { savepoint "prt_in",54,137; }
		if(strnpcinfo(4) == "geffen_in") { savepoint "geffen_in",70,59; }
		if(strnpcinfo(4) == "payon_in01") { savepoint "payon_in01",142,46; }
		break;
	case 5:
		mes "[PVP解說員]";
		mes "這是一個屬於玩家與玩家間的大戰";
		mes "也是一個";
		mes "能讓玩家們在戰鬥中找到樂趣的";
		mes "PVP模式!";
		mes "期待各位";
		mes "勇於前來挑戰 !";
		break;
	}
	close;
}
morocc_in,144,138,4	duplicate(pvpe)	PvP解說員#pe1	84
alberta_in,22,146,4	duplicate(pvpe)	PvP解說員#pe2	84
prt_in,56,140,4	duplicate(pvpe)	PvP解說員#pe3	84
geffen_in,67,63,4	duplicate(pvpe)	PvP解說員#pe4	84
payon_in01,142,50,4	duplicate(pvpe)	PvP解說員#pe5	84
geffen_in,67,63,4	duplicate(pvpe)	PvP解說員#pe6	84

// Mode Select
//============================================================ 
-	script	守門人::gkut	-1,{
	mes "[守門人]";
	mes "很高興為你服務";
	mes "我是為您打開PVP打鬥場之門";
	mes "' PVP 守門人";
	mes "有關PVP模式的說明";
	mes "請你去向旁邊那位解說員";
	mes "詢問!";
	next;
	switch ( select("前往^ff3355PVP 夢魘模式^000000 打鬥場:前往^3355FFPVP 溜溜猴模式^000000 打鬥場:前往^3355FFPVP 爭霸模式^000000 打鬥場:結束對話")) {
		case 1:
			mes "[守門人]";
			mes "目前已停止使用 ";
			mes "PVP夢魘模式了.";
			mes "您願意PVP的話 ";
			mes "請利用溜溜猴模式吧.";
			break;
		case 2:
			// 台版的費用為 10,000zeny
			mes "[守門人]";
			mes "參加費是10,000 Zeny 您要移動到";
			mes "溜溜猴模式";
			mes "移動到PVP打鬥場嗎?";
			next;
			switch( select("移動:取消")) {
			case 1:
				if ((Zeny > 499) && (BaseLevel > 30)) {
					set Zeny, Zeny - 500;
					callsub L_Warp,"pvp_y_room";
				}
				else {
					mes "[守門人]";
					mes "對不起!";
					mes "您的 Zeny 不夠 ，或者";
					mes "您的等級不到31級";
				}
				close;
			case 2:
				break;
			}
			break;
		case 3:
			mes "[守門人]";
			mes "歡迎光臨∼";
			mes "你在確認一下";
			mes "你有沒有參賽券或者觀賞券好嗎？";
			next;
			if ((countitem(7028) == 0) && (countitem(7029) == 0)) {
				mes "[守門人]";
				mes "..... 呃? 沒有嗎？";
				mes "很抱歉，這個競技場是";
				mes "只有帶來參賽券或者觀賞券的人";
				mes "才能進來的。";
				close;
			}
			mes "[守門人]";
			mes "好的，感謝您的參與，祝您玩得愉快!";
			if (Zeny >= 500) set Zeny, Zeny - 500;
			callsub L_Warp,"pvp_room";
			break;
		case 4:
			mes "[守門人]";
			mes "這是一個屬於玩家與玩家間的大戰";
			mes "也是一個";
			mes "能讓玩家們在戰鬥中找到樂趣的";
			mes "PVP模式!";
			mes "期待各位";
			mes "勇於前來挑戰 !";
			break;
	}
	close;

L_Warp:
	set .@map$,getarg(0);
	if (checkre(0)) {
		switch(rand(1,10)) {
			case 1: warp .@map$,34,38; break;
			case 2: warp .@map$,46,38; break;
			case 3: warp .@map$,58,38; break;
			case 4: warp .@map$,70,38; break;
			case 5: warp .@map$,82,38; break;
			case 6: warp .@map$,82,58; break;
			case 7: warp .@map$,65,58; break;
			case 8: warp .@map$,49,58; break;
			case 9: warp .@map$,33,58; break;
			case 10: warp .@map$,52,24; break;
		}
	} else
		warp .@map$,51,23;
	return;
}
morocc_in,144,142,4	duplicate(gkut)	守門人#gke1	83
alberta_in,26,146,4	duplicate(gkut)	守門人#gke2	83
prt_in,52,140,4	duplicate(gkut)	守門人#gke3	83
geffen_in,63,63,4	duplicate(gkut)	守門人#gke4	83
payon_in01,140,53,4	duplicate(gkut)	守門人#gke5	83

// PVP Warper Function
//============================================================ 
// Note: The warper NPCs are in the pre-re/re paths.

//callfunc "F_PVP_FSRS"{,<min_lvl>,<max_lvl>};
function	script	F_PVP_FSRS	{
	if (getargcount()) {
		if (BaseLevel < getarg(0) || BaseLevel > getarg(1)) {
			mes "[打鬥場接待員]";
			mes "抱歉，您的等級需在"+getarg(0)+"到"+getarg(1)+"之間。";
			close;
		}
	}
	if (strnpcinfo(4) == "pvp_y_room") {
		set .@base$, "pvp_y_"+strnpcinfo(2);
		setarray .@Maps$[0],	.@base$+"-1",	.@base$+"-2",	.@base$+"-3",	.@base$+"-4",	.@base$+"-5";
		setarray .@Name$[0],	"普隆德拉",		"依斯魯得島",		"斐揚",		"艾爾貝塔",		"夢羅克";
		setarray .@Limit[0],	128,			128,			128,			128,			128;
	} else {
		setarray .@Maps$[0],	"pvp_n_8-1",	"pvp_n_8-2",	"pvp_n_8-3",	"pvp_n_8-4",	"pvp_n_8-5";
		setarray .@Name$[0],	"Sandwich",		"Lock on",		"Four Room",	"Under cross",	"Compass Room";
		setarray .@Limit[0],	64,				32,				32,				32,				32;
	}
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		set .@menu$, .@menu$+.@Name$[.@i]+" ["+getmapusers(.@Maps$[.@i])+" / "+.@Limit[.@i]+"]:";
	set .@menu$, .@menu$+"Cancel.";
	set .@i, select(.@menu$)-1;
	if (.@i == 5) close;
	if (getmapusers(.@Maps$[.@i]) >= .@Limit[.@i]) {
		mes "[打鬥場接待員]";
		mes "地圖人數已達極限";
		close;
	}
	warp .@Maps$[.@i],0,0;
	end;
}



// Event Contestant Entrance
//============================================================ 
pvp_room,54,85,4	script	Registration Staff#1	105,{
	if (countitem(7028) == 0) {
		mes "[PVP Combat Square Register Staff]";
		mes "Eh? How did you get in here?";
		mes "This is the entrance for players only.";
		mes "For details about viewers please proceed to the Register Staff on your right.";
		close;
	}
	set .@size, getarraysize(.warp_x);
	if (.@size == 0) {
		setarray .warp_x,40,59,20,40;
		setarray .warp_y,59,40,40,20;
	}
	if (select("Combat Square one:Cancel") == 1) {
		mes "[PVP Combat Square Register Staff]";
		mes "'"+ strcharinfo(0) +"'";
		mes "Are you ready?!";
		next;
		if (select("Yes!:No!") == 1) {
			mes "[PVP Combat Square Register Staff]";
			mes "OK! I will send you inside";
			mes "Good luck!";
			close2;
			delitem 7028,1; //Invite_For_Duel
			set .@rand, rand(.@size);
			warp "pvp_2vs2", .warp_x[.@rand], .warp_y[.@rand];
			deletearray .warp_x[.@rand],1;
			deletearray .warp_y[.@rand],1;
			end;
		}
		mes "[PVP Combat Square Register Staff]";
		mes "Come back anytime you are ready.";
		close;
	}
	close;

OnInit:
	waitingroom "Combat Square players entrance only",0;
	end;
}

// Spectator Entrance
//============================================================ 
-	script	Spectator's Entrance#dum::PVPSpectator	-1,{
	if (countitem(7029) == 0) {
		mes "[PVP Compete Square Register Staff]";
		mes "This is the entrance for viewers.";
		mes "For details about players entrance please proceed to the Register Staff on your left.";
		close;
	}
	if (select("Compete Square one:Cancel") == 1) {
		mes "[PVP Combat Square Register Staff]";
		mes "You got it, thanks for participating. Have fun!";
		close2;
		delitem 7029,1; //Admission_For_Duel
		switch(rand(1,4)) {
			case 1: warp "pvp_2vs2",39,7; end;
			case 2: warp "pvp_2vs2",39,73; end;
			case 3: warp "pvp_2vs2",7,39; end;
			case 4: warp "pvp_2vs2",73,39; end;
		}
	}
	close;

OnInit:
	waitingroom "Compete Square viewer's entrance",0;
	end;
}
pvp_room,62,85,4	duplicate(PVPSpectator)	Spectator's Entrance#1	105
pvp_room,70,85,4	duplicate(PVPSpectator)	Spectator's Entrance#2	105
pvp_room,78,85,4	duplicate(PVPSpectator)	Spectator's Entrance#3	105
pvp_room,86,85,4	duplicate(PVPSpectator)	Spectator's Entrance#4	105

// Spectator Warps
//============================================================ 
-	script	Combat Square Staff#dum::PVPSpecWarp	-1,{
OnTouch:
	mes "[Combat Square Staff]";
	mes "May I help you?";
	if (select("To the center viewer seat.:Leave Combat Square.") == 1) {
		warp "pvp_2vs2",38,38;
		end;
	}
	warp "pvp_room",84,39;
	end;
}
pvp_2vs2,5,4,0	duplicate(PVPSpecWarp)	Combat Square Staff#1	45,1,1
pvp_2vs2,5,74,0	duplicate(PVPSpecWarp)	Combat Square Staff#2	45,1,1
pvp_2vs2,74,74,0	duplicate(PVPSpecWarp)	Combat Square Staff#3	45,1,1
pvp_2vs2,74,5,0	duplicate(PVPSpecWarp)	Combat Square Staff#4	45,1,1

pvp_2vs2,40,40,0	script	Combat Square Staff#5	45,1,1,{
	mes "[Combat Square Staff]";
	mes "May I help you?";
	switch(select("To the side viewer seat.:Leave Combat Square.")) {
	case 1:
		switch(rand(1,4)) {
			case 1: warp "pvp_2vs2",39,7; end;
			case 2: warp "pvp_2vs2",39,73; end;
			case 3: warp "pvp_2vs2",7,39; end;
			case 4: warp "pvp_2vs2",73,39; end;
		}
	case 2:
		warp "pvp_c_room",84,39;
		end;
	}
}

// PVP Area Exit warp
//============================================================ 
pvp_room,51,19,0	script	out#eventpvp	45,4,2,{
OnTouch:
	mes "[Combat Square Staff]";
	mes "Did you have fun in Combat Square?";
	mes "May I ask where you want to go?";
	next;
	switch(select("普隆德拉:夢羅克:吉芬:斐揚:艾爾貝塔:取消")) {
		case 1: warp "prontera",107,60; end;
		case 2: warp "morocc",157,96; end;
		case 3: warp "geffen",120,36; end;
		case 4: warp "payon",96,100; end;
		case 5: warp "alberta",41,243; end;
		case 6: close;
	}
}
